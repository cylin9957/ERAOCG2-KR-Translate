@CARDNAME_26618,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = RR－ワイルド・ヴァルチャー
	CASE "略称"
		RESULTS = ワイルド・Ｖ
	CASE "カテゴリ"
		RESULTS:0 = レイドラプターズ
ENDSELECT


@CARD_26618(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
	
		RETURN 効果モン
	CASE "属性"
		RETURN 闇属性
	CASE "種族"
		RETURN 鳥獣族
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 2000
ENDSELECT

RETURN 0

@CARD_EXPLANATION_26618(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の②の効果は１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(0,効果召喚)
PRINTL ①：自分フィールドにモンスターが存在しない場合、このカードはリリースなしで召喚できる。
CALLFORM CARD_ABLE(2,戦場起動)
PRINTL ②：このカードをリリースして発動できる。レベルの合計が６以下になるように、
PRINTL 自分の手札・デッキ・墓地から「RR」モンスターを２体まで特殊召喚する。

@CARDCAN_26618(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件達成
CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,26618)
SIF RESULT == 1
	RETURN 0

IF 種類 == 効果召喚
	SIF モンスター数(決闘者) != 0
		RETURN 0
	RETURN 1
ELSEIF 種類 == 戦場起動
	SIF 発動済_2(決闘者,26618)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL CARD_SS_CHECK(決闘者,0,"",1)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_26618(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 残りレベル

IF 種類 == 効果召喚
	RETURN 1
ELSEIF 種類 == 戦場起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,26618,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			効果範囲手札 = 1
			効果範囲デッキ = 1
			効果範囲墓地 = 1
			効果条件カテゴリ = レイドラプターズ
			効果条件種類 = レベル持ち
			CALLFORM 効果条件レベル_ADV(6, "以下")
			CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
			TRYCALLFORM CARD_{GETVAR(@"%決闘者%_戦場:カード召喚先")}, "レベル"
			残りレベル = 6 - RESULT
			IF 残りレベル >= 1 && モンスター数(決闘者) < 5
				CALL FULLRESET
				効果範囲手札 = 1
				効果範囲デッキ = 1
				効果範囲墓地 = 1
				効果条件カテゴリ = レイドラプターズ
				効果条件種類 = レベル持ち
				効果条件任意 = 1
				CALLFORM 効果条件レベル_ADV(残りレベル, "以下")
				CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF
ENDIF