@CARDNAME_73203,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = サンアバロン・カースド・リボーン
	CASE "略称"
		RESULTS = 聖天樹の呪精
	CASE "カテゴリ"
		RESULTS:0 = サンアバロン
		RESULTS:1 = ゲームオリジナル
ENDSELECT


@CARD_73203(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"

		RETURN 永続魔法
ENDSELECT

RETURN 0

@CARD_EXPLANATION_73203(種類)
#DIM DYNAMIC 種類

PRINTL このカード名の①②の効果はそれぞれ１ターンに１度しか使用できない。
PRINTL ①：自分フィールドにモンスターが存在しない場合、自分の墓地の
PRINTL 「サンアバロン」Lモンスター１体を対象として発動できる。そのモンスターを特殊召喚する。
PRINTL この効果で特殊召喚したモンスターの攻撃力はそのリンクマーカーの数×１０００になり、
PRINTL 効果は無効化され、素材にできず、このターン攻撃できない。
PRINTL ②：自分が戦闘・効果でダメージを受けた場合、フィールドのこのカードを墓地へ送って発動できる。
PRINTL 受けたダメージの数値分だけ相手にダメージを与える。
PRINTL ③：このカードがフィールドから離れた場合に発動する。フィールドのモンスターを全て破壊する。



@CARDCAN_73203(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,73203)
SIF RESULT == 1
	RETURN 0
IF 種類 == 魔罠起動 && GETVAR(@"%決闘者%_魔_表:場所") == 1
	SIF 発動済_1(決闘者,73203)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF モンスター数(決闘者) != 0
		RETURN 0
	CALL FULLRESET
	効果範囲墓地 = 1
	効果条件種類 = リンク
	効果条件カテゴリ = サンアバロン
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	IF 種類 == 魔罠起動
		SIF GETVAR(@"%決闘者%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	RETURN 1
ELSEIF 種類 == 効果後誘発
	SIF 発動済_2(決闘者,73203)
		RETURN 0
	SIF ゾーン != "魔罠"
		RETURN 0
	CALL CARD_CHECK_FIN_BIT(決闘者,"被弾者")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 戦場→墓地 || 種類 == 破壊→墓地 || 種類 == 戦闘→墓地 || 種類 == 戦場→戦離 || 種類 == 除外時
	RETURN 1
ENDIF

@CARDEFFECT_73203(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件達成
#DIM DYNAMIC 選択カード_ID
#DIMS DYNAMIC 選択カード名前

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 && GETVAR(@"%決闘者%_魔_表:場所") == 1
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,73203,1)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲墓地 = 1
		効果条件種類 = リンク
		効果条件カテゴリ = サンアバロン
		効果追加種類:0 = 効追無効
		効果追加種類:1 = 効追不材
		効果追加種類:2 = 26
		CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所,"攻撃表示")
		攻撃力修正 = GETVAR(@"%決闘者%_MZ_星:RESULT") * 1000
		CALL SET_FIELD_INFO(決闘者,"戦場",RESULT,"攻固",攻撃力修正)
	ENDIF
ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	;カード発動＆設置まで
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,73203)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所,魔罠行き)
	ENDIF
ELSEIF 種類 == 効果後誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,73203,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		CALL FULLRESET
		CALL LIFE_CHANGE(決闘者,"減少","効果",ダメージ受値,1)
	ENDIF
ELSEIF 種類 == 戦場→墓地 || 種類 == 破壊→墓地 || 種類 == 戦闘→墓地 || 種類 == 戦場→戦離 || 種類 == 除外時
	CALL CARD_REMOVAL_BASIC(決闘者,"両戦場全体",場所,"墓地")
ENDIF
