@CARDNAME_61254,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 大霊術－「二星」
	CASE "略称"
		RESULTS = 大霊術－「二星」
	CASE "カテゴリ"
		RESULTS:0 = 령사
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_61254(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN フィールド
ENDSELECT

RETURN 0


@CARD_EXPLANATION_61254(종류)
#DIM DYNAMIC 종류

PRINTL 자신 필드는 「大霊術－「二星」」가 된다。
PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,발동誘発)
PRINTL ①：자신필드에 光・어둠속성の「령사」몬스터가 존재하는 한발동할 수 있다。
PRINTL 상대가 발동した마법 / 함정의 효과를 무효로 한다。
CALLFORM CARD_ABLE(2,마함起動)
PRINTL ②：자신 메인 페이즈에 발동할 수 있다。
PRINTL 패의 光・어둠속성の마법사족 몬스터 １장을 상대에게 見せ、その속성によって이하의 효과를 적용한다。
PRINTL ●어둠속성：덱에서 빛속성の「령사」 몬스터 １장、또는 「령사」마법 / 함정카드 １장을 패에 넣는다。
PRINTL ●빛속성：자신의 묘지 / 제외상태인 어둠속성の「령사」몬스터와 見せた몬스터를 특수 소환한다。
CALLFORM CARD_ABLE(0,破壊→묘지)
PRINTL ③：이 카드가 상대의 효과에 의해 파괴되어 묘지へ送られた時に발동할 수 있다。
PRINTL 이 카드와 、光・어둠속성の「령사」몬스터를 １장ずつ제외하고、
PRINTL 필드의 모든 카드를 いかなる내성을 무시하고 제외한다。


@条件달성속성検出_大霊術二星(듀얼리스트,종류,ゾーン,場所,条達속성)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM REF 条達속성, 0

CALL FULLRESET
효과範囲손패 = 1
효과条件속성 = 어둠속성
효과条件종족 = 마법사족
CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
IF RESULT == 1
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件スーパー속성 = 빛속성
	효과条件スーパーカテゴリ = 령사
	효과条件カテゴリ = 령사
	효과条件종류 = 마함全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		条達속성:어둠속성 = 1
ENDIF

CALL FULLRESET
효과範囲손패 = 1
효과条件속성 = 빛속성
효과条件종족 = 마법사족
CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
IF RESULT == 1
	CALL FULLRESET
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件속성 = 어둠속성
	효과条件カテゴリ = 령사
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1 && モンスター数(듀얼리스트) <= 3
		条達속성:빛속성 = 1
ENDIF


@CARDCAN_61254(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

#DIM DYNAMIC 条達속성, 8

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,61254)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_2(듀얼리스트,61254)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL 条件달성속성検出_大霊術二星(듀얼리스트,종류,ゾーン,場所,条達속성)
	SIF !MATCH(条達속성, 1)
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ELSEIF 종류 == 발동誘発
	SIF 발동済_1(듀얼리스트,61254)
		RETURN 0
	SIF ゾーン != "마함" 
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2	
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과条件속성:0 = 빛속성
	효과条件속성:1 = 어둠속성
	효과条件カテゴリ = 령사
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 마함全般
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함" 
		RETURN 0
	RETURN 1
ELSEIF 종류 == 破壊→묘지
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件속성 = 빛속성
	효과条件カテゴリ = 령사
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件속성 = 어둠속성
	효과条件カテゴリ = 령사
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_61254(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 検出속성, 8
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 '= RESULTS

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,61254,2)
	IF RESULT == 0
		CALL 条件달성속성検出_大霊術二星(듀얼리스트,종류,ゾーン,場所,検出속성)

		CALL FULLRESET
		효과範囲손패 = 1
		CALL 효과条件속성_複数(検出속성)
		효과条件종족 = 마법사족
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		選択カード_ID = RESULT:99
		IF 選択カード != 9999
			VARSET RESULT
			CALLFORM CARD_{選択カード_ID}, "속성"
			IF MATCH(RESULT, 어둠속성)
				CALL FULLRESET
				효과条件スーパー속성 = 빛속성
				효과条件スーパーカテゴリ = 령사
				효과条件カテゴリ = 령사
				효과条件종류 = 마함全般
				CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
			ELSE
				CALL FULLRESET
				효과範囲묘지 = 1
				효과範囲제외 = 1
				효과条件속성 = 어둠속성
				효과条件カテゴリ = 령사
				효과条件종류 = モン全般
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
				IF RESULT == 1
					CALL CARD_SELECT(듀얼리스트,選択カード)
					CALL CARD_SP_SUMMON(듀얼리스트,本参照先（CS）,選択カード（CS）)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,61254)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,61254,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"無効")
	ENDIF
ELSEIF 종류 == 지속효과
	CALL SET_FIELD_MAGIC_INFO(듀얼리스트, "大霊術－「二星」")
ELSEIF 종류 == 破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,61254)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件속성 = 빛속성
		효과条件カテゴリ = 령사
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件속성 = 어둠속성
			효과条件カテゴリ = 령사
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
			IF RESULT == 1
				FOR カウンタ, 0, 5
					SIF GETVAR(@"%듀얼리스트%_필드:カウンタ") == -1
						CONTINUE
					CALL CARD_DISPOSE(듀얼리스트,"필드",カウンタ,제외行き)
				NEXT
				FOR カウンタ, 0, 5
					SIF GETVAR(@"%듀얼리스트%_마함:カウンタ") == -1
						CONTINUE
					CALL CARD_DISPOSE(듀얼리스트,"마함",カウンタ,제외行き)
				NEXT
				FOR カウンタ, 0, 5
					SIF GETVAR(@"%対面者%_필드:カウンタ") == -1
						CONTINUE
					CALL CARD_DISPOSE(対面者,"필드",カウンタ,제외行き)
				NEXT
				FOR カウンタ, 0, 5
					SIF GETVAR(@"%対面者%_마함:カウンタ") == -1
						CONTINUE
					CALL CARD_DISPOSE(対面者,"마함",カウンタ,제외行き)
				NEXT
			ENDIF
		ENDIF
	ENDIF
ENDIF
