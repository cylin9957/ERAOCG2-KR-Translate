@CARDNAME_4121,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先
#DIM DYNAMIC カード主

カード主 = NO_TO_CHARANUM(4121)

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = ノア・フルール
	CASE "略称"
		RESULTS = ノア
	CASE "カテゴリ"
		RESULTS:0 = 詩姫
		RESULTS:4 = バトルスピリッツ
		IF TALENT:カード主:愛妻
			RESULTS:1 = 契約
		ENDIF
ENDSELECT


@CARD_4121(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "種類"
	
		RETURN 効果モン
	CASE "属性"
		RETURN 炎属性
	CASE "種族"
		RETURN ドラゴン族
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 900
	CASE "守備力"
		RETURN 700
	CASE "性別"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4121(種類)
#DIM DYNAMIC 種類
#DIM DYNAMIC カード主

カード主 = NO_TO_CHARANUM(4121)

IF TALENT:カード主:愛妻
	PRINTL このカード名の①②③の効果はそれぞれ１ターンに１度しか使用できない。
	CALLFORM CARD_ABLE(1,手札起動)
	PRINTL ①：自分フィールドのコアカウンターを１つ取り除いて発動できる。
	PRINTL このカードを手札から特殊召喚する。
	CALLFORM CARD_ABLE(2,効果後誘発)
	PRINTL ②：自分フィールドに「詩姫」モンスターまたは炎属性モンスターが召喚・特殊召喚された場合に発動できる。
	PRINTL 相手フィールドの攻撃力２０００以下のモンスター１体を破壊する。
	CALLFORM CARD_ABLE(3,戦場起動)
	PRINTL ③：自分の魔法・罠ゾーンの「創界神」にコアカウンターを２つ置くことで発動できる。
	PRINTL その「創界神」のコアカウンター×２以下のレベルの「詩姫」モンスターまたは炎属性モンスター１体を手札から特殊召喚できる。
ELSE
	PRINTL このカード名の①の効果は１ターンに１度しか使用できない。
	CALLFORM CARD_ABLE(2,効果後誘発)
	PRINTL ①：自分フィールドに「詩姫」モンスターまたは炎属性モンスターが召喚・特殊召喚された場合に発動できる。
	PRINTL 相手フィールドの攻撃力２０００以下のモンスター１体を破壊する。
ENDIF

@CARDCAN_4121(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件達成
#DIM DYNAMIC カード主

カード主 = NO_TO_CHARANUM(4121)

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,4121)
SIF RESULT == 1
	RETURN 0


IF 種類 == 手札起動
	IF TALENT:カード主:愛妻
		SIF 発動済_1(決闘者,4121)
			RETURN 0
		SIF メインフェイズ != 1
			RETURN 0
		SIF ターンプレイヤー != 決闘者
			RETURN 0
		CALL CARD_SS_CHECK(決闘者,4121,"手札")
		SIF RESULT == 1
			RETURN 0
		CALL FULLRESET
		効果範囲戦場 = 1
		効果範囲魔罠 = 1
		効果条件カテゴリ = バトルスピリッツ
		効果条件表示 = 表側表示
		効果条件CNT = 1
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT:9 < 1
			RETURN 0
	ELSE
		RETURN 0	
	ENDIF
	RETURN 1
ELSEIF 種類 == 効果後誘発
	SIF 発動済_2(決闘者,4121)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
	CALL FULLRESET
	効果条件属性 = 炎属性
	効果条件スーパーカテゴリ = 詩姫
	CALL CARD_CHECK_FIN_DATA(決闘者,"召喚全般")
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果条件表示 = 表側表示
	効果条件攻撃力 = 2000
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 戦場起動
	IF TALENT:カード主:愛妻
		SIF メインフェイズ != 1
			RETURN 0
		SIF ターンプレイヤー != 決闘者
			RETURN 0
		SIF 発動済_3(決闘者,4121)
			RETURN 0
		CALL FULLRESET
		効果範囲魔罠 = 1
		効果条件カテゴリ = 創界神
		効果条件表示 = 表側表示
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		RETURN 1
	ELSE
		RETURN 0	
	ENDIF
ENDIF

@CARDEFFECT_4121(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタ数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 軽減数
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC カード主

カード主 = NO_TO_CHARANUM(4121)


IF 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,4121,1)
	IF RESULT == 0
		PRINTL コアカウンターを取り除くカードを選択
		CALL FULLRESET
		効果範囲戦場 = 1
		効果範囲魔罠 = 1
		効果条件カテゴリ = バトルスピリッツ
		効果条件表示 = 表側表示
		効果条件個Ｃ所持 = 1
		CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)
		IF RESULT != 9999
			PRINTL コアが取り除かれました
			CALL CARD_SELECT(決闘者,RESULT)
			CALL SET_FIELD_INFO(対象者（CS）,参照先（CS）,選択カード（CS）,"個Ｃ",-1)

			CALL FULLRESET
			CALL CARD_SP_SUMMON(決闘者,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 種類 == 効果後誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,4121,2)
	IF RESULT == 0
		CALL FULLRESET
		効果条件表示 = 表側表示
		効果条件攻撃力 = 2000
		CALL CARD_REMOVAL_BASIC(決闘者,"戦場単体",場所,"墓地")
	ENDIF
ELSEIF 種類 == 戦場起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,4121,3)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲魔罠 = 1
		効果条件カテゴリ = 創界神
		効果条件表示 = 表側表示
		CALL CARD_CHOOSE_ADV(決闘者,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL CARD_SELECT(決闘者,選択カード)
			CALL SET_FIELD_INFO(決闘者,参照先（CS）,選択カード（CS）,"個Ｃ",2)

			CALL FULLRESET
			効果範囲魔罠 = 1
			効果条件カテゴリ = 創界神
			効果条件CNT = 1
			CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
			カウンタ数 = RESULT:9
			
			CALL FULLRESET
			効果範囲手札 = 1
			効果条件属性 = 炎属性
			効果条件スーパーカテゴリ = 詩姫
			CALL 効果条件レベル_ADV(カウンタ数 * 2,"以下")
			CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF

