;----------------------------------------------------------------
;모브生成関数
;----------------------------------------------------------------
@HELLO_MOBS(MOB_NO)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 能力値
#DIM DYNAMIC 설정캐릭터_ID
#DIM DYNAMIC ガチャ結果
#DIM DYNAMIC 구상OP
#DIM DYNAMIC 子供用番号
#DIM DYNAMIC MOB_NO
#DIM DYNAMIC 설정値
;MOB_NO
;1=マジシャン(마법使い), 2=スケバン(ドラゴン), 3=オカマニ(アンデッド), 4=バイト戦士(戦士), 5=メカニック(機械),
;6=農家さん(獣), 7=ヒップホッパー(鳥獣),8=B級マニア(鱗獣), 9=地雷걸(悪魔), 10=シスター(天使), 
;11=걸スカウト(昆虫), 12=錬金術師(元素霊), 13=フローリスト(植物), 14=自称宇宙人(사이킥), 15=ハイスコア걸(サイバース),
;16=水着ギャル(海生), 17=超常リポ(未確認),
;20=アカデミア, 21=オシリスレッド, 22=オベリスクブルー, 23=ラーイエロー 
;31=마법少女, 32=獄道, 33=幽霊, 34=オフィスレディ, 35=パートのママ, 36=女医さん, 37=YuberEats
;40=セキュリティ, 41=바니, 42=D-ホイーラー, 43=暴走族, 44=スリルライダー
;800=モンスター


IF MOB_NO == 0
	$CHOOSE_AGAIN
	MOB_NO = RAND:45 + 1
	SIF !EXISTCSV(9100 + MOB_NO)
		GOTO CHOOSE_AGAIN
ENDIF

설정캐릭터_ID = CHARANUM
ADDCHARA 9100 + MOB_NO


;牝性,ふたなり,낭자애
IF GROUPMATCH(MOB_NO,4,8,14,31,32)
	SELECTCASE RAND:100
		CASE IS <= 5 ;5%で낭자애
			TALENT:설정캐릭터_ID:성별 = 雄性
			TALENT:설정캐릭터_ID:낭자애 = 1
		CASE IS <= 10 ;5%でふたなり
			TALENT:설정캐릭터_ID:성별 = ふたなり
		CASEELSE
			TALENT:설정캐릭터_ID:성별 = 牝性
	ENDSELECT
ELSE
	SELECTCASE RAND:100
		CASE IS <= 2 ;2%でふたなり
			TALENT:설정캐릭터_ID:성별 = ふたなり
		CASEELSE
			TALENT:설정캐릭터_ID:성별 = 牝性
	ENDSELECT
ENDIF
NO:설정캐릭터_ID = 모브知合数 + 9100
BASE:설정캐릭터_ID:사정 = 0
BASE:설정캐릭터_ID:발기 = 0
BASE:설정캐릭터_ID:무드 = 0
BASE:설정캐릭터_ID:이성 = MAXBASE:설정캐릭터_ID:이성
BASE:설정캐릭터_ID:분노 = 0
BASE:설정캐릭터_ID:만족 = 1000
BASE:설정캐릭터_ID:절정 = 0
BASE:설정캐릭터_ID:쾌감 = 0

CFLAG:설정캐릭터_ID:발기도 = 0
CFLAG:설정캐릭터_ID:동행 = 0
;성교플래그
TEQUIP:설정캐릭터_ID:Ｖ섹스 = -1
TEQUIP:설정캐릭터_ID:Ａ섹스 = -1
TCVAR:설정캐릭터_ID:V삽입관리플래그 = -1
TCVAR:설정캐릭터_ID:A삽입관리플래그 = -1
;中出し표시용플래그
TCVAR:설정캐릭터_ID:질내사정대상 = -1
TCVAR:설정캐릭터_ID:애널사정대상 = -1

CALL NAME_RANDOM(설정캐릭터_ID)
;素質決定
CALL SET_CHARA_RAND(설정캐릭터_ID,MOB_NO)
TALENT:설정캐릭터_ID:듀얼리스트 = 1
TALENT:설정캐릭터_ID:모브 = MOB_NO

CALL SET_CHARA_DECK(TALENT:설정캐릭터_ID:종족)
CFLAG:설정캐릭터_ID:초기설정덱 = RESULT

모브知合数 += 1
RETURN 설정캐릭터_ID

;----------------------------------------------------------------
;모브の덱決定
;----------------------------------------------------------------
@SET_CHARA_DECK(설정종족)
#DIM DYNAMIC MOB_NO
#DIM DYNAMIC 설정덱
#DIM DYNAMIC 설정종족

SELECTCASE 설정종족
CASE 마법사족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 1002
	CASE 1
		설정덱 = 1930
	CASE 2
		설정덱 = 2040
	ENDSELECT
CASE 드래곤족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 10004
	CASE 1
		설정덱 = 1930
	CASE 2
		설정덱 = 2676
	ENDSELECT
CASE 언데드족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 2654
	CASE 1
		설정덱 = 1355
	CASE 2
		설정덱 = 2030
	ENDSELECT
CASE 전사족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 10025
	CASE 1
		설정덱 = 2653
	CASE 2
		설정덱 = 2361
	ENDSELECT
CASE 기계족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 2677
	CASE 1
		설정덱 = 10057
	CASE 2
		설정덱 = 10058
	ENDSELECT
CASE 야수족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 2517
	CASE 1
		설정덱 = 10037
	CASE 2
		설정덱 = 10030
	ENDSELECT
CASE 비행야수족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 2050
	CASE 1
		설정덱 = 2562
	CASE 2
		설정덱 = 10031
	ENDSELECT

CASE 비늘야수족
	SELECTCASE RAND:1
	CASE 0
		설정덱 = 10020
	CASE 1
	;에일리언
	CASE 2
	;基本恐룡
	ENDSELECT
CASE 악마족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 10006
	CASE 1
		설정덱 = 2562
	CASE 2
		설정덱 = 10031
	ENDSELECT
CASE 천사족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 1062
	CASE 1
		설정덱 = 1405
	CASE 2
		설정덱 = 2085
	ENDSELECT
CASE 곤충족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 10003
	CASE 1
		설정덱 = 1435
	CASE 2
		설정덱 = 2085
	ENDSELECT
CASE 원소정령족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 2255
	CASE 1
		설정덱 = 10059
	CASE 2
		설정덱 = 2660
	ENDSELECT
CASE 식물족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 10011
	CASE 1
		설정덱 = 2150
	CASE 2
		설정덱 = 2671
	ENDSELECT
CASE 사이킥족
	SELECTCASE RAND:2
	CASE 0
		설정덱 = 10054
	CASE 1
		설정덱 = 10061
;	CASE 2
;		설정덱 = 2671
	ENDSELECT
CASE 사이버스족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 10014
	CASE 1
		설정덱 = 10060
	CASE 2
		설정덱 = 10062
	ENDSELECT
CASE 바다생물족
	SELECTCASE RAND:3
	CASE 0
		설정덱 = 2575
	CASE 1
		설정덱 = 2655
	CASE 2
		설정덱 = 1600
	ENDSELECT
CASE 미확인족
	SELECTCASE RAND:1
	CASE 0
		설정덱 = 11004
	ENDSELECT
ENDSELECT


RETURN 설정덱

;----------------------------------------------------------------
;모브の素質決定
;----------------------------------------------------------------
@SET_CHARA_RAND(설정캐릭터_ID, MOB_NO)
#DIM DYNAMIC 설정캐릭터_ID
#DIM DYNAMIC ガチャ結果
#DIM DYNAMIC 구상OP
#DIM DYNAMIC 子供用番号
#DIM DYNAMIC MOB_NO
#DIM DYNAMIC 설정値
#DIM DYNAMIC 増減値
#DIM DYNAMIC 増減値２

;先に体格とかの素質を決める
CALL SET_CHARA_RAND_BODY(설정캐릭터_ID, MOB_NO)
;감각素質決定
CALL SET_CHARA_RAND_SENSE(설정캐릭터_ID, MOB_NO)

増減値 = 0
増減値２ = 0
;연령
SELECTCASE MOB_NO

	CASE 31,32,33 ;학생고정
		TALENT:설정캐릭터_ID:연령 = -2
	CASE 2, 20 TO 25 ;スケバン,아카데미아 学生固定
		TALENT:설정캐릭터_ID:연령 = -1
	CASE 34,35,36,40,42,44 ;オフィス레이디 若者~大人
		TALENT:설정캐릭터_ID:연령 = RAND:2
	CASE 35
		TALENT:설정캐릭터_ID:연령 = 1
	CASEELSE
		;学生~大人
		TALENT:설정캐릭터_ID:연령 = RAND:3 - 1
ENDSELECT


;배짱(臆病/気丈)
SELECTCASE MOB_NO
	;スケバンは気丈確率増加
	CASE 2
		増減値 = 48
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 10, MAX(12 + -(増減値), 5), MAX(12 + 増減値, 5))
;태도(素直/反抗的)
SELECTCASE MOB_NO
	;スケバンは反抗的確率増加
	CASE 2
		増減値 = 60
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 11, MAX(20 + -(増減値), 5), MAX(20 + 増減値, 5))
;반응(大人しい/生意気) 태도によって確率増減
増減値 = TALENT:설정캐릭터_ID:태도 * 4
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 	Q, 8 + -(増減値), 8 + 増減値)
;프라이드(高い/低い) 배짱・태도によって確率増減
増減値 = TALENT:설정캐릭터_ID:배짱 * 4 + TALENT:설정캐릭터_ID:태도 * 4
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 	W, 8 + -(増減値), 8 + 増減値)
;ツン/デレ 배짱・태도によって確率増減
増減値 = TALENT:설정캐릭터_ID:배짱 * 2 + TALENT:설정캐릭터_ID:태도 * 4
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 14, MAX(1 + -(増減値),5), 8 + 増減値)
;쾌감반응(否定/素直)
SELECTCASE MOB_NO
	;シスターは否定確率増加
	CASE 10
		増減値 = -25
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 70, MAX(5 + -(増減値), 3), MAX(9 + 増減値, 3))
;자제심 
SELECTCASE MOB_NO
	;シスターは確率増加
	CASE 10
		増減値 = 13
	CASEELSE
		増減値 = 0
ENDSELECT
;쾌감に素直と同居しない
SIF TALENT:설정캐릭터_ID:쾌감반응 <= 0
	CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 20, 7 + 増減値)
;성적흥미(保守的/好奇心)
SELECTCASE MOB_NO
	;シスターは保守的確率増加
	CASE 10
		増減値 = -44
	;걸スカウト,超常リポは好奇心確率増加
	CASE 11,17
		増減値 = 7
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 23, MAX(6 + -(増減値), 3), MAX(13 + 増減値, 5))
;양기／음기 
SELECTCASE MOB_NO
	;水着さんは양기確率増加
	CASE 16
		増減値 = 42
	;オカマニ,地雷系は음기確率増加
	CASE 3,9
		増減値 = -22
	CASEELSE
		増減値 = 0
ENDSELECT
;배짱によって確率増減
増減値 += TALENT:설정캐릭터_ID:배짱 * 4
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 24, MAX(8 + -(増減値), 3), MAX(8 + 増減値, 3))
;일선넘지않음
SELECTCASE MOB_NO
	;シスターは確率増加
	CASE 10
		増減値 = 24
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 25 + -(増減値), 6 + 増減値)
;자기과시욕(引っ込み思案/目立ちたがり) 
SELECTCASE MOB_NO
	;マジシャン,ヒップホッパー,水着さんは目立ちたがり確率増加
	CASE 1,7,16
		増減値 = 42
	;オカマニは引っ込み思案確率増加
	CASE 3
		増減値 = -22
	CASEELSE
		増減値 = 0
ENDSELECT
;배짱によって確率増減
増減値 += TALENT:설정캐릭터_ID:배짱 * 4
;양기／음기 によって確率増減
増減値 += TALENT:설정캐릭터_ID:양기／음기  * 4
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 26, MAX(8 + -(増減値), 3), MAX(8 + 増減値, 3))
;정조(정조無頓着/정조観念)
SELECTCASE MOB_NO
	;シスターは確率増加
	CASE 10,11
		増減値 = 40
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 30, MAX(6 + -(増減値), 5), MAX(10 + 増減値, 5))
;수치심(恥薄い/恥じらい)
SELECTCASE MOB_NO
	;マジシャン,ヒップホッパー,水着さんは恥薄い確率増加
	CASE 1,7,16
		増減値 = -42
	;シスターは恥じらい確率増加
	CASE 3
		増減値 = 5
	CASEELSE
		増減値 = 0
ENDSELECT
;자기과시욕によって確率増減
増減値 = TALENT:설정캐릭터_ID:자기과시욕 * -4
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 33, MAX(8 + -(増減値), 3), MAX(8 + 増減値, 3))
;무관심
;好奇心・目立ちたがり・恥じらいと排反
SIF TALENT:설정캐릭터_ID:성적흥미 <= 0 && TALENT:설정캐릭터_ID:자기과시욕 <= 0 && TALENT:설정캐릭터_ID:수치심 < 1
	CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 21, 7)
;감정부족함 一部素質と排反
;臆病・生意気・ツンデレ・好奇心・目立ちたがり・恥じらいと排反
SIF  TALENT:설정캐릭터_ID:배짱 > -1 && TALENT:설정캐릭터_ID:반응 < 1 && TALENT:설정캐릭터_ID:츤／데레 < 1 && TALENT:설정캐릭터_ID:성적흥미 < 1 && TALENT:설정캐릭터_ID:자기과시욕 < 1 && TALENT:설정캐릭터_ID:수치심 < 1
	CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 22, 7)
;자기애(抑圧/解放) 一部素質と排反
増減値 = 1
増減値２ = 1
;抑圧は生意気・好奇心・양기・目立ちたがり・쾌감に素直と排反
SIF  TALENT:설정캐릭터_ID:반응 == 1 || TALENT:설정캐릭터_ID:성적흥미 == 1 || TALENT:설정캐릭터_ID:양기／음기 == 1 || TALENT:설정캐릭터_ID:자기과시욕 == 1 || TALENT:설정캐릭터_ID:쾌감반응 == 1
	増減値 = 0
;解放は臆病・ツンデレ・자제심・감정부족함・保守的・음기・일선넘지않음・정조観念・引っ込み思案・恥じらい・쾌감の否定と排反
SIF  TALENT:설정캐릭터_ID:배짱 == -1 || TALENT:설정캐릭터_ID:츤／데레 == 1 || TALENT:설정캐릭터_ID:자제심 || TALENT:설정캐릭터_ID:성적흥미 == -1 || TALENT:설정캐릭터_ID:양기／음기 == -1 || TALENT:설정캐릭터_ID:일선넘지않음 || TALENT:설정캐릭터_ID:자기과시욕 == -1 || TALENT:설정캐릭터_ID:수치심 == -1 || TALENT:설정캐릭터_ID:쾌감반응 == -1
	増減値２ = 0
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 31, 6 * 増減値, 12 * 増減値２)
;통각(強い/弱い)
SELECTCASE MOB_NO
	;スケバン,酪農家,걸スカウト,超常リポは強い確率増加
	CASE 1,6,11,17
		増減値 = -20
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 40, MAX(5 + -(増減値), 3), MAX(5 + 増減値, 3))
;젖기쉬움정도(にくい/やすい)
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 41, 5, 8)
;습득속도(遅い/早い)
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 50, 5, 8)
;솜씨좋은손가락
SELECTCASE MOB_NO
	;マジシャン,メカニック,酪農家,걸スカウト,フローリスト,ハイスコア걸は確率増加
	CASE 1,5,6,11,13,15
		増減値 = 10
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 51, 5 + 増減値)
;혀사용자
SELECTCASE MOB_NO
	;ヒップホッパーは確率増加
	CASE 7
		増減値 = 10
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 52, 5 + 増減値)
;자위しやすい
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 60, 7)
;악취내성(敏感/鈍感)
SELECTCASE MOB_NO
	;フローリストは敏感確率増加
	CASE 13
		増減値 = -5
	;メカニック,걸スカウト,錬金術師,ハイスコア걸,超常リポは鈍感確率増加
	CASE 5,11,12,15,17
		増減値 = 5
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 61, MAX(8 + -(増減値), 3), MAX(8 + 増減値, 3))
;악취내성が付いたなら、さらに1/4で汚れ無視、潔癖症に(つまり実質2%で+-2、6%で+-1)
SIF TALENT:설정캐릭터_ID:악취내성 != 0 && RAND:4 == 0
	TALENT:설정캐릭터_ID:악취내성 *= 2
;헌신적
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 62, 9)
;중독しやすい
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 71, 8)
;도착적
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 80, 8)
;性的趣向(両刀-1/男嫌い1/女嫌い2/人間不信3)
;両刀(女性は少し確率高め、ふたなりは確率高め、낭자애は確率がさらに高い)
IF TALENT:설정캐릭터_ID:성별 == 雄性 && TALENT:설정캐릭터_ID:낭자애
	増減値 = 25
ELSEIF TALENT:설정캐릭터_ID:성별 == ふたなり
	増減値 = 15
ELSEIF TALENT:설정캐릭터_ID:성별 == 牝性
	増減値 = 8
		ENDIF
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 81, 5 + 増減値, -1)
;両刀がつかなかった場合のみ人間不信판정
SIF !(TALENT:설정캐릭터_ID:성별기호)
	CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 81, 1, 3)
;両刀も人間不信もつかなかった場合のみ異性嫌い판정
IF !(TALENT:설정캐릭터_ID:성별기호)
	IF TALENT:설정캐릭터_ID:성별 == 雄性
		;낭자애は異性嫌いの確率低め&男嫌いがつく場合あり
		IF TALENT:설정캐릭터_ID:낭자애
			CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 81, 5, 5, 1, 2)
		ELSE
			CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 81, 5, 2)
		ENDIF
	ELSEIF TALENT:설정캐릭터_ID:성별 == 牝性 || TALENT:설정캐릭터_ID:성별 == ふたなり
		CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 81, 5)
	ENDIF
ENDIF
;새드、마조
CALL RCHARA_TALENT_OPPOSE(설정캐릭터_ID, 82, 83, 8, 8)
;매력
;낭자애、女性は確率高め
SIF TALENT:설정캐릭터_ID:성별 == 雄性 && !TALENT:설정캐릭터_ID:낭자애
	増減値 = 7
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 92, 10 - 増減値)
;매혹
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 93, 8)
;수수께끼의매력
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 94, 2)
;회복速度(遅い/早い)
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 130, 7, 8)
;유치
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 151, 3)
;술내성
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 58, 6)
;データ캐릭터
SELECTCASE MOB_NO
	;メカニック,ハイスコア걸は確率増加
	CASE 5,15
		増減値 = 22
	;걸スカウトは微増
	CASE 11
		増減値 = 8
	CASEELSE
		増減値 = 0
	ENDSELECT
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 109, MAX(3 + 増減値, 3))
;페이스배분
;臆病・反抗的・目立ちたがり・프라이드低い・ツンデレ・恥じらいで確率増加、大人しい・恥薄い・무관심・감정부족함で確率低下
増減値 = 0
SIF TALENT:설정캐릭터_ID:배짱 < 0
	増減値 += 3
SIF TALENT:설정캐릭터_ID:태도 > 0
	増減値 += 5
SIF TALENT:설정캐릭터_ID:반응 < 0
	増減値 -= 3
SIF TALENT:설정캐릭터_ID:프라이드 < 0
	増減値 += 3
SIF TALENT:설정캐릭터_ID:츤／데레 > 0
	増減値 += 10
SIF TALENT:설정캐릭터_ID:무관심
	増減値 -= 30
SIF TALENT:설정캐릭터_ID:감정부족함
	増減値 -= 30
SIF TALENT:설정캐릭터_ID:자기과시욕 > 0
	増減値 += 3
増減値 += TALENT:설정캐릭터_ID:수치심 * 3
CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 28, MAX(5 + -(増減値), 5), MAX(5 + 増減値, 5))
;저항
;쾌감に素直・好奇心とは同居しない
SIF TALENT:설정캐릭터_ID:성적흥미 < 1 && TALENT:설정캐릭터_ID:쾌감반응 < 1
	CALL RCHARA_TALENT_DUAL(설정캐릭터_ID, 29, 3)
;メスガキ
増減値 = 0
;연령で確率増減
SELECTCASE TALENT:설정캐릭터_ID:연령
	CASE IS < 0 ;学生以下
		増減値 = 2
	CASEELSE
		増減値 = 0
ENDSELECT
;체형で確率増減
SELECTCASE TALENT:설정캐릭터_ID:체형
	CASE IS < 0 ;小柄以下
		増減値 += 2
	CASE IS > 0 ;長身以上
		増減値 -= 2
ENDSELECT
;프라이드低い,무관심,감정부족함,引っ込み思案,抑圧とは排反
SIF TALENT:설정캐릭터_ID:프라이드 > -1 && !TALENT:설정캐릭터_ID:무관심 && !TALENT:설정캐릭터_ID:감정부족함 && TALENT:설정캐릭터_ID:자기과시욕 > -1 && TALENT:설정캐릭터_ID:자기애 > -1
	CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 87, MAX(3 + 増減値, 1))
;即堕ち
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 72, 3)
;근육성질
SELECTCASE MOB_NO
	;スケバン,農家は確率増加
	CASE 2,6
		増減値 = 2
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 106, MAX(3 + 増減値, 1))
;褐色肌
SELECTCASE MOB_NO
	;水着は確率増加
	CASE 16
		増減値 = 50
	CASEELSE
		増減値 = 0
ENDSELECT
CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 108, MAX(10 + 増減値, 1))

;捕食者
増減値 = 0
SIF TALENT:설정캐릭터_ID:프라이드 > 0 ;프라이드高い
	増減値 += 3
SIF TALENT:설정캐릭터_ID:성적흥미 > 0 ;양기
	増減値 += 3
SIF TALENT:설정캐릭터_ID:페이스배분 < 0 ;マイペース
	増減値 += 3
SIF TALENT:설정캐릭터_ID:정조 < 0 ;정조無頓着
	増減値 += 3
SIF TALENT:설정캐릭터_ID:자기애 > 0 ;解放
	増減値 += 10
SIF TALENT:설정캐릭터_ID:습득속도 > 0 ;習得早い
	増減値 += 3
SIF TALENT:설정캐릭터_ID:솜씨좋은손가락
	増減値 += 2
SIF TALENT:설정캐릭터_ID:혀사용자
	増減値 += 2
SIF TALENT:설정캐릭터_ID:매혹
	増減値 += 3
SIF TALENT:설정캐릭터_ID:수수께끼의매력
	増減値 += 2
SIF TALENT:설정캐릭터_ID:절륜
	増減値 += 5
;臆病・ツンデレ・자제심・감정부족함・保守的・음기・일선넘지않음・정조観念・引っ込み思案・恥じらい・쾌감の否定と排反
SIF  TALENT:설정캐릭터_ID:배짱 > -1 && !TALENT:설정캐릭터_ID:자제심 && TALENT:설정캐릭터_ID:성적흥미 > -1 && TALENT:설정캐릭터_ID:양기／음기 > -1 && !TALENT:설정캐릭터_ID:일선넘지않음 && TALENT:설정캐릭터_ID:자기과시욕 > -1 && TALENT:설정캐릭터_ID:수치심 > -1 && TALENT:설정캐릭터_ID:쾌감반응 > -1
	CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 35, MAX(3 + 増減値, 1))

;竿持ちは攻撃力と절륜판정
IF GETBIT(TALENT:설정캐릭터_ID:성별,1)
	SELECTCASE RAND:100
		;神
		CASE IS < 3
			TALENT:설정캐릭터_ID:형태 = 8
		;メスチンポ
		CASE IS < 6
			TALENT:설정캐릭터_ID:형태 = 1
		;危機感感じる
		CASE IS < 16
			TALENT:설정캐릭터_ID:형태 = 7
		;未成熟
		CASE IS < 26
			TALENT:설정캐릭터_ID:형태 = 2
		;ごんぶと
		CASE IS < 41
			TALENT:설정캐릭터_ID:형태 = 6
		;粗チン
		CASE IS < 56
			TALENT:설정캐릭터_ID:형태 = 3
		;おおきめ
		CASE IS < 78
			TALENT:설정캐릭터_ID:형태 = 5
		;普通
		CASEELSE
			TALENT:설정캐릭터_ID:형태 = 4
	ENDSELECT
	;절륜(おおきめ以上なら確率増加)
	CALL RCHARA_TALENT_SINGLE(설정캐릭터_ID, 129, MAX((TALENT:설정캐릭터_ID:형태 - 4) * 25, 5))
	TALENT:설정캐릭터_ID:동정 = 0
ENDIF

TALENT:설정캐릭터_ID:키스미경험 = 1
SIF TALENT:설정캐릭터_ID:성별 != 雄性
	TALENT:설정캐릭터_ID:처녀 = 1

CFLAG:설정캐릭터_ID:키스상실상대 = -1
SIF TALENT:설정캐릭터_ID:듀얼리스트 >= 1 ;Anonym추가
	CFLAG:설정캐릭터_ID:오프라인레이트 = 4000
CALL SET_BODYSIZE(설정캐릭터_ID,0,1)

;以下暫定
SELECTCASE MOB_NO
	CASE 1 ;マジシャン
		増減値 = 229
	CASE 2 ;スケバン
		増減値 = 129
	CASE 3 ;オカマニ
		増減値 = 114
	CASE 4 ;バイト戦士
		増減値 = 506
	CASE 5 ;メカニック
		増減値 = 158
	CASE 6 ;農家さん
		増減値 = 406
	CASE 7 ;ヒップホッパー
		増減値 = 313
	CASE 8 ;Ｂ級マニア
		増減値 = 313
	CASE 9 ;地雷걸
		増減値 = 128
	CASE 10 ;シスター
		増減値 = 162
	CASE 11 ;걸スカウト
		増減値 = 160
	CASE 12 ;錬金術師
		増減値 = 136
	CASE 13 ;フローリスト
		増減値 = 510
	CASE 14 ;自称宇宙人
		増減値 = 117
	CASE 15 ;ハイスコア걸
		増減値 = 405
	CASE 16 ;水着ギャル
		増減値 = 193
	CASE 17 ;不思議レポ
		増減値 = 197
	
	CASE 17 ;不思議レポ
		増減値 = 197

	CASE 17 ;不思議レポ
		増減値 = 197

	CASE 17 ;不思議レポ
		増減値 = 197

	CASE 20 TO 25 ;アカデミア
		増減値 = 137

	CASE 31
		増減値 = 157
	CASE 32
		増減値 = 198
	CASE 33
		増減値 = 127
	CASE 34
		増減値 = 351
	CASE 35
		増減値 = 510
	CASE 36
		増減値 = 409
	CASE 37
		増減値 = 352

	CASE 40
		増減値 = 158
	CASE 41
		増減値 = 454
	CASE 42 TO 44
		増減値 = 158

	CASEELSE
		増減値 = 0
ENDSELECT
CFLAG:설정캐라_ID:현복장 = 増減値
CALL CLOTHES_SETTING(설정캐릭터_ID,増減値)

;----------------------------------------------------------------
;모브の体格決定
;----------------------------------------------------------------
@SET_CHARA_RAND_BODY(설정캐릭터_ID, MOB_NO)
#DIM DYNAMIC 설정캐릭터_ID
#DIM DYNAMIC MOB_NO
;체형
IF TALENT:설정캐릭터_ID:성별 == 雄性 && TALENT:설정캐릭터_ID:낭자애
	;낭자애は小型と普通の２択
	SELECTCASE RAND:100
		CASE IS < 20
			TALENT:설정캐릭터_ID:체형 = 0
		CASEELSE
			TALENT:설정캐릭터_ID:체형 = -1
	ENDSELECT
ELSEIF MOB_NO != 800 ;モンスター以外
;通常50%,小柄20%,長身20%,大柄10%
	SELECTCASE RAND:10
		CASE 0
			TALENT:설정캐릭터_ID:체형 = 2
		CASE IS < 3
			TALENT:설정캐릭터_ID:체형 = 1
		CASE IS < 5
			TALENT:설정캐릭터_ID:체형 = -1
		CASEELSE
			TALENT:설정캐릭터_ID:체형 = 0
	ENDSELECT
ELSE
;通常60%, 小柄10%,幼児7%,長身10%,大柄7%,巨大6%
	SELECTCASE RAND:100
		CASE 0 TO 60
			TALENT:설정캐릭터_ID:체형 = 0
		CASE 61 TO 70
			TALENT:설정캐릭터_ID:체형 = -1
		CASE 71 TO 80
			TALENT:설정캐릭터_ID:체형 = 1
		CASE 81 TO 87
			TALENT:설정캐릭터_ID:체형 = -2
		CASE 88 TO 94
			TALENT:설정캐릭터_ID:체형 = 2
		CASEELSE
			TALENT:설정캐릭터_ID:체형 = 3
	ENDSELECT
ENDIF

IF TALENT:설정캐릭터_ID:성별 != 雄性
	;バスト
	;体格関係なし、普26%,貧18%,巨乳18%,絶12%,爆乳12%,虚7%,ウシ乳7%
	SELECTCASE RAND:100
		CASE IS < 7
			TALENT:설정캐릭터_ID:가슴사이즈 = 3
		CASE IS < 14
			TALENT:설정캐릭터_ID:가슴사이즈 = -3
		CASE IS < 26
			TALENT:설정캐릭터_ID:가슴사이즈 = 2
		CASE IS < 38
			TALENT:설정캐릭터_ID:가슴사이즈 = -2
		CASE IS < 56
			TALENT:설정캐릭터_ID:가슴사이즈 = 1
		CASE IS < 74
			TALENT:설정캐릭터_ID:가슴사이즈 = -1
	ENDSELECT
ELSE
	TALENT:설정캐릭터_ID:가슴사이즈 = -3
ENDIF
;ヒップ
;体格関係なし、並32%,貧21%,安産22%,デカ15%質量兵器10%
SELECTCASE RAND:100
	CASE IS < 10
		TALENT:설정캐릭터_ID:엉덩이사이즈 = 3
	CASE IS < 25
		TALENT:설정캐릭터_ID:엉덩이사이즈 = 2
	CASE IS < 47
		TALENT:설정캐릭터_ID:엉덩이사이즈 = 1
	CASE IS < 68
		TALENT:설정캐릭터_ID:엉덩이사이즈 = -1
ENDSELECT
;太腿
;60%でケツと同じ太さ。20%で-1, 20%で+1
TALENT:설정캐릭터_ID:허벅지사이즈 = TALENT:설정캐릭터_ID:엉덩이사이즈
SELECTCASE RAND:10
	CASE IS < 2
		TALENT:설정캐릭터_ID:허벅지사이즈--
	CASE IS < 4
		TALENT:설정캐릭터_ID:허벅지사이즈++
ENDSELECT
TALENT:설정캐릭터_ID:허벅지사이즈 = LIMIT(TALENT:설정캐릭터_ID:허벅지사이즈,-1,3)

;----------------------------------------------------------------
;모브の감각素質決定
;----------------------------------------------------------------
@SET_CHARA_RAND_SENSE(설정캐릭터_ID, MOB_NO)
#DIM DYNAMIC 설정캐릭터_ID
#DIM DYNAMIC カウンタ
#DIM DYNAMIC MOB_NO

;엉덩이광はなんか名前的につけない
;키스魔は一括키스미경험なのでつけない

SELECTCASE RAND:1000
	;全身雑魚&敏感(0.1%)
	CASE IS < 1
		TALENT:설정캐릭터_ID:음호 = 1
		;巨乳以上は음유もつく
		SIF TALENT:설정캐릭터_ID:가슴사이즈 > 0
			TALENT:설정캐릭터_ID:음유 = 1
		TALENT:설정캐릭터_ID:허접클리 = 1
		TALENT:설정캐릭터_ID:허접유두 = 1
		FOR カウンタ, 101,105 
			TALENT:설정캐릭터_ID:カウンタ = 1
		NEXT
	;雑魚(計5%)
	;1雑魚+1敏感(2.5%)
	CASE IS < 26
		CALL SET_CHARA_RAND_SENSE_PRETTYWEAK(설정캐릭터_ID, MOB_NO)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
	;1雑魚+1鈍感(2.5%)
	CASE IS < 51
		CALL SET_CHARA_RAND_SENSE_PRETTYWEAK(설정캐릭터_ID, MOB_NO)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;4敏感(計5%)
	;4敏感+1鈍感(5%)
	CASE IS < 101
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;3敏感(計10%)
	;3敏感(2%)
	CASE IS < 121
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
	;3敏感+2鈍感(5%)
	CASE IS < 171
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;3敏感+1鈍感(3%)
	CASE IS < 201
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;2敏感(計30%)
	;2敏感+3鈍感(5%)
	CASE IS < 251
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;2敏感+2鈍感(10%)
	CASE IS < 351
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;2敏感+1鈍感(10%)
	CASE IS < 451
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;2敏感(5%)
	CASE IS < 501
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)	
	;1敏感(計40%)
	;1敏感+4鈍感(3%)
	CASE IS < 531
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;1敏感+3鈍感(5%)
	CASE IS < 581
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)	
	;1敏感+2鈍感(10%)
	CASE IS < 681
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)	
	;1敏感+1鈍感(17%)
	CASE IS < 851
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)	
	;1敏感(10%)
	CASE IS < 951
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 1)
	;1鈍感(3%)
	CASE IS < 981
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;2鈍感(1%)
	CASE IS < 991
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;3鈍感(0.5%)
	CASE IS < 996
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;4鈍感(0.3%)
	CASE IS < 999
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
		CALL SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, -1)
	;全身鈍感(0.1%)
	CASEELSE
		FOR カウンタ, 101,105 
			TALENT:설정캐릭터_ID:カウンタ = -1
		NEXT
ENDSELECT

SIF TALENT:설정캐릭터_ID:성별 == 雄性
	TALENT:설정캐릭터_ID:Ｖ감도 = 0

;----------------------------------------------------------------
;모브の超敏感감각素質決定
;----------------------------------------------------------------
@SET_CHARA_RAND_SENSE_PRETTYWEAK(설정캐릭터_ID, MOB_NO)
#DIM DYNAMIC 설정캐릭터_ID
#DIM DYNAMIC MOB_NO

SELECTCASE RAND:3
CASE 0
	TALENT:설정캐릭터_ID:음호 = 1
		TALENT:설정캐릭터_ID:Ｖ감도 = 1
CASE 1
	;巨乳以上は음유が付く可能性もある
		IF TALENT:설정캐릭터_ID:가슴사이즈 > 0
			SELECTCASE RAND:5
				CASE IS < 1
					TALENT:설정캐릭터_ID:음유 = 1
					TALENT:설정캐릭터_ID:허접유두 = 1
					TALENT:설정캐릭터_ID:Ｂ감도 = 1
				CASE IS < 3
					TALENT:설정캐릭터_ID:음유 = 1
					TALENT:설정캐릭터_ID:Ｂ감도 = 1
				CASEELSE
					TALENT:설정캐릭터_ID:허접유두 = 1
					TALENT:설정캐릭터_ID:Ｂ감도 = 1
			ENDSELECT
		ELSE
			TALENT:설정캐릭터_ID:허접유두 = 1
			TALENT:설정캐릭터_ID:Ｂ감도 = 1
		ENDIF
CASE 2
	TALENT:설정캐릭터_ID:허접클리 = 1
		TALENT:설정캐릭터_ID:Ｃ감도 = 1
ENDSELECT

;----------------------------------------------------------------
;모브の감각素質を１つ決定
;----------------------------------------------------------------
@SET_CHARA_RAND_SENSE_SINGLE(설정캐릭터_ID, MOB_NO, 설정値)
#DIM DYNAMIC 설정캐릭터_ID
#DIM DYNAMIC MOB_NO
#DIM DYNAMIC 설정値
#DIM DYNAMIC 空き감각, 5
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB

FOR カウンタ, 101,105 
	IF TALENT:설정캐릭터_ID:カウンタ == 0
		空き감각:カウンタB = カウンタ
		カウンタB++
	ENDIF
NEXT

IF カウンタB < 1
	RETURN
ELSEIF カウンタB == 1
	TALENT:설정캐릭터_ID:空き감각 = 설정値
ELSE
	TALENT:설정캐릭터_ID:(空き감각:(RAND:カウンタB)) = 설정値
ENDIF

;----------------------------------------------------------------
;모브の名前決定
;----------------------------------------------------------------
@NAME_RANDOM(캐릭터_ID)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 洋風和風
#DIMS DYNAMIC 選択苗字
#DIMS DYNAMIC 選択名前
{
#DIMS CONST 苗字リスト = 
	"아이우라","아오이","아이조메","아이노","아오바","아가리","아키타니","아키나이","아키노","아쿠자와",
	"아사카","아사미","아자미","아사히","아스카","아즈사","아즈마","아세비","아나미","아마아메",
	"아마가사","아마사카","아마치","아마네","아마노","아마바네","아마미야","아메쿠","아야노","아야메",
	"아야츠지","아야하라","아라","아라미","이오리베","이오로이","이가라시","이사가와","이스즈","이즈미",
	"이타미","이치노세","이치라이","이토노","이나바","이나미네","이부키","이부스키","우에시나","우시로",
	"우텐메","우나즈키","우미로네","우메사자키","우리하리","우루마","우루와시","에나미","에노미야","에토우",
	"오오아마","오오바","오오하라","오토가와","오토사카","오토하타","오니시","오보로","카에데","카가미",
	"카키세코","카키세마","카구라","카구라자카","카게츠","카사네","카시와나기","카타기리","카츠키","카나에",
	"카나미야","카나메","카노에","카노토","카부라기","카마츠카","카미가키","카미모로","카미야마","카미와즈미",
	"카야타케","카라이트","칸가에","키아마메","키오이","키사라기","키쿠보","키자키","키시로","키타즈메",
	"키타미야","키부카와","키도우","키노","키얀","키요쿠모","키라","키리시마","키리노","쿠스노미",
	"쿠레","쿠리라","쿠로에","케리","고가츠히","코가라시","코고에","코코로","코즈에","코츠나메야",
	"코미나토","코미야","사이드","사에구사","사오토메","사쿠라","사카무키","사카나사","사사메","사츠카와",
	"사츠키","사토우","사노","시이카","시오바라","시오미","시가","시가키","시구레","시치죠우",
	"시나시하마","시미즈","시모츠지","샤바나","슈젠지","시라카시","시라하마","시로가네","시로사키","스즈네",
	"스즈노하","스도우","스미","세나미","세노가미","센바라","소가와","소요기","타카우","타카나시",
	"타치바나","타츠키","타테바나","타마키","치하루","치요다","치요노","츠키다테","츠키타리","츠키노",
	"츠키미","츠키모리","츠유리","츠바키","츠유리","츠메즈메","츠루노","테시가와라","텐카와","텐도우",
	"텐마","토가이","토키타","토키메","토키노","토키와","토키츠카","토키토우","토키노야","토츠카와",
	"토츠기","토마요치","토마리","토미타","토모에","토리하라","토리유미","나카카도","나가토","나가미나",
	"나기리","나츠키","나츠미","나츠메","나나쿠사","나나세","나나토","나미야","나메미","나츠미야",
	"나요리","나루미야","난구우","니시미야","니시노","니죠우","니와","니부","누카리야","누노세",
	"네코바","네코미야","노마","노우미","노치가미","하기하시","하시마","하나기","하타네","하루카",
	"하루세","하루미야","한나미","히이라기","히가","히즈메","히치타","히토토세","히나시","히노사카",
	"히무로","후카미","후키하루","후지노","후지미야","후세","후타바","후미츠키","후미노","호가노",
	"호쿠사테라","호즈미","호조미","호무라","호호리","마이라","마키노","마시타","마세","마치즈키",
	"마니우다","미우라","미카게","미쿠니","미스키","미스루기","미사키","미즈키","미스미","미소노",
	"미조로케","미타마","미치지리","미즈키","미츠하라","미토","미토리","미나가와","미나기","미나미",
	"미후지","미후유","미호노","미호로","미마","미야코","미나카미","미노베","미요이","무카도",
	"무츠즈미","무나기","메라","모치즈키","모토바","모모에","모모세","야에노","야에가시","야오토메",
	"야규우","야쿠모","야나기","야부미","야마노하","야야나기","야와라기","유키노","유사","유즈키",
	"유모리","유노기","유라","유루기","요리모","요루미","와카츠키","와카바","와타누키",
}
{
#DIMS CONST 名前リスト = 
	"아이","아이카","아이나","아이미","아오이","아카리","아키코","아키나","아키라","아케미",
	"아사카","아사히","아즈키","아즈사","아나미","아마나","아마네","아미","아야","아야카",
	"아야네","아야노","아야메","아리나","아리사","안나","이오리","이스즈","이치고","이나미",
	"이부키","이로하","우이","우이카","우미카","우메카","에나","에미","에리","에리카",
	"에리코","에리나","오리코","카에데","카에데코","카오리","카오루","카구라","카게츠","카코",
	"카즈미","카즈미","카나","카나에","카나타","카나메","카노에","카노코","카노토","카호",
	"카요","카린","키이코","키코","키나코","키노에","키노토","키미코","쿄우카","쿄우코",
	"키라라","키라리","키라메","키리카","키리노","쿠미","쿠미","크리스","쿠루미","쿠레하",
	"쿠로에","코코로","코코나","코즈에","코토카","코토미","코나미","코노에","코노하","코노미",
	"코하루","코마치","사에","사에코","사키","사키나","사쿠야","사쿠라","사사라","사츠키",
	"사토코","사토미","사나","사나미","사야","사야카","사야네","사유리","사요","사요코",
	"사라","시이카","시오N","시구레","시즈카","시즈쿠","시즈코","시노부","슈우코","쥬리",
	"쇼우","쇼우코","스즈코","스즈네","스바루","세이카","세이코","세이라","세나","세나노",
	"세나미","세라","타에","타카나","타키네","타츠키","타츠미","타마","타마키","치아키",
	"치이코","치카","치카코","치구사","치사토","치토세","치나","치나미","치하루","치유키",
	"치요","치요카","치요코","츠카사","츠쿠요","츠바키","츠보미","테마리","토우카","토우키",
	"토우코","토키","토키카","토키코","토키노","토키메","토마리","토모","토모에","토모카",
	"토모코","토모네","나오","나오코","나오미","나기","나기코","나기사","나구사","나코",
	"나고미","나즈나","나츠","나츠키","나츠하","나츠미","나츠메","나데시코","나나카","나나세",
	"나나미","나호","나유키","나요리","나루","나루카","나루코","나루미","난리","니이카",
	"니이나","니카","니나","네네","네네카","네무","노노","하쿠","하즈키","하루",
	"하루카","하루나","히카루","히나","히나타","히나노","히비키","히미카","히메","히메카",
	"히메코","히메나","히메노","후우카","후우코","후타바","후미","후미코","후미나","후미네",
	"후미노","호노카","호타루","호무라","마이","마오","마키","마키카","마키나","마키하",
	"마키노","마츠리 ","마도카","마나","마나카","마호","마미","마미코","마리아","마리코",
	"마리나","미이나","미에","미카","미카네","미칸","미키","미키코","미쿠니","미쿠리",
	"미코","미코토","미사","미사키","미사토","미즈카","미즈키","미소노","미타네","미타마",
	"미즈키","미츠키","미도리","미나","미나코","미나토","미나미","미노에","미노토","미노리",
	"미후유","미호","미호코","미키호","미호노","미야코","미유","미유카","미유키","메구미",
	"메구루","메구루코","메루","모에","모에카","모에코","모에나","모미","모미지","모모",
	"모모에","모모카","모모코","야이카","야에","야에코","야코","야스나","야스미","야치에",
	"야치요","야히로","야요이","유이","유우","유우키","유우코","유우나","유카리","유키",
	"유키에","유키카","유키코","유키노","유키미","유즈키","유부키","유마","유미","유미카",
	"유미코","유미나","유메","유메카","유메코","유메나","유리","유리카","유리네","요나",
	"요네카","란","란카","리아","이이나","리에","리오","리카","리카코","리코",
	"리나","리나코","리호","리리","리리카","리리코","리리네","린","린카","린코",
	"린네","루리카","루리코","레이","레이코","레이나","레이라","레나","렌","렌카",
}
{
#DIMS CONST 洋風名前リスト = 
"아바","아사라","아델하이트","아름","아라","아린","아이다","아이비","아이나","아이니",
	"아이노","아이야","아이라","아이리","아이린","아이리스","아우구스타","아브도차","아우라","아브릴",
	"아우렐리아","아우로라","아가테","악사나","아그리피나","아고스티나","애슐리","아주라","아스타","아스트릿",
	"아달지자","아델레","아델라","아델라이드","아델리나","아드리아나","아드리엔느","아나","아나이스","아나스타샤",
	"아나토리아","아나벨","애니","아니타","아녜스","아니에라","아네트","아넬마","애비","아비게일",
	"아폴린","아폴리나리야","아폴로니아","아마타","아말리아","아만다","아멜리아","아멜리","아라나","아라이다",
	"아라벨라","아라벨","아리아네","아리안나","알리샤","아리다","아린느","아리엘","아리사","앨리스",
	"아리나","알다","알테아","알바","알폰시나","알프레다","알베르타","알베르티나","알마","알미",
	"알멜","알야","알렛","알레샤","알렉산드라","알렉시아","알레그라","알레시아","알렛","알레프치나",
	"알로이지아","안","안젤라","안제리크","안제리나","안제리카","안젤","안소니아","안티아","안토니나",
	"안드레아","안투아네트","안나","안나마리아","안나리사","안니나","안네","안네타","안네마리","안네리에",
	"안네리제","앰버","안피사","앙리에트","이디스","이나","이네스","이야","이리스","이브",
	"이반젤린","이반나","이베타","이본느","이블린","예니","옌나","이오란다","이사드라","이사벨라",
	"이자벨","이졸데","이데아","이나리","이네스","이네사","이베트","이멜다","이라","일라이자",
	"일라리아","이리나","일바","일자","일다","일마","일미","일무가르트","일무힐데","일무힐트",
	"일메라","일메리","일멘트루트","일야","일레인","일렌느","일로나","잉그리드","인나","버지니아",
	"발레리","바이올렛","바실리사","바네사","발랑틴느","발바라","발트루데","발트루트","발부르가","발부르크",
	"발마","발레리아","발레리아나","발레리","발렌티나","반나","비비","비비아나","비비안느","비비안",
	"비비안누","비에나","비에노","비올라","비올렌느","비올레타","비올렛","빅토리아","빅투아르","빌바",
	"빌베","빌지니아","빌헬미나","빌마","빈첸차","빈프리데","베네사","베네란다","베라","베르디아나",
	"베르나","베로니카","베로니크","웬디","벤델가르트","우넬마","우라","우라리","우리야나","우르술라",
	"우루프","우르리케","에이브릴","에이다","에이니","에이네","에이프릴","에이미","에이야","에이라",
	"에이린","에바","에바","에반젤리나","에베리나","에우제니아","에우페미아","에우프라지아","에브프락시야","에우랄리아",
	"에브린느","에디트","에델가르트","에베","에리카","에카테리나","에그란티느","에스터","에스텔라","에스텔",
	"에스메랄다","에다","에디타","에델트루트","에드비제","에드몬다","에놀라","에프프로시니야","에마","에마누엘라",
	"에미","에미","에밀리아","에밀리에","에밀리야","에밀리아","에밀리아나","에밀리","에밀리엔느","에메린느",
	"에메린","엘라","엘리아네","엘리아나","엘리","엘리자","엘리자벳","엘리제","엘리나","엘린느",
	"엘리비라","에리카","에리사","에리자베타","에리자베트","에리자베스","에리세","에리데","에리나","에리노아",
	"에린","엘비","엘비라","엘비르","엘케","엘사","엘자","엘시","엘시리아","엘세",
	"엘제","엘다","엘나","엘네스타","엘네스티네","엘네스티네","엘프리데","엘마","엘미","엘미니아",
	"엘미느","엘메린다","엘멘트루트","엘멘힐데","엘멘힐트","엘레나","엘렌느","엘레오노라","엘레트라","엘레나",
	"엘렌","엘로이자","엘로디","엔니","오이비","오이리","오귀스틴느","오드리","오나","오렐리아",
	"옥사나","오잔나","오타비아","오티리아","오티리에","오틸리","오딜","오데타","오데트","오네스타",
	"오넬바","오노린느","오피리아","오리아나","오리아네","오리안느","올리브","올리비아","올리비에라","오리에타",
	"올리가","올림피아","올가","올텐시아","올넬라","올레스티라","온디나","옴브레타","게르트루드","카야",
	"카라","칼리","칼리나","가이아","카이사","카야","카이라","카산드라","카스테헬미","카타리나",
	"카타리나","카티","카티아","카테나","카테리나","카트리나","카트린느","가브리엘라","카미유","카밀라",
	"카멜리아","카리나","카리스타","카린","카르멜라","카르멘","카를로타","카렌","카롤라","카롤리나",
	"키아라","키아","기젤라","키라","키티","키트리","캐서린","캐시","캐시","캐머런",
	"캐리","캐롤라인","캐롤","킬스티","길베르타","킴벌리","그윈네스","클라브지야","클라우디아","그라치아나",
	"그라치에라","그라치아","글래디스","그라피라","클라라","클라리체","클라리스","크림힐트","크리스타","크리스탈",
	"크리스티나","크리스티아나","크리스티나","크리스틴","그리젤다","클레어","그레이스","그레텔","그레트헨","클레리아",
	"클레르","크레첸츠","그레타","클레망스","클레망틴느","클레멘티나","클레멘틴느","클레멘틴","그렌다","크로에",
	"클라우디아","클로데트","클로드","글로리아","클로틸드","글로리아나","케이","케이시","케이티","케이트",
	"케일리","켈리","겔다","겔트루트","코델리아","코트니","코리","코스탄차","코니","코랄리",
	"코랑틴느","콜린","콜린느","코르둘라","코르넬리아","코렛","콘실리아","콘스탄스","콘스탄체","콘체타",
	"사라","사이니","사이야","사이라","자이라","사반나","자샤","자스키아","사데","사넬마",
	"사비나","자비네","사브리나","사만사","사라","자라","샐리","살로메","샌디","산드라",
	"산드린느","지아나","지크리트","지크린데","지그룬","지나","지니","실라","진","시비",
	"제이드","제이니","제이미","제인","제시","제시카","제나","제니","제니퍼","제마",
	"제랄디나","셰리","셰릴","제르베즈","시그네","시스코","지슬렌느","지젤","시드니","시니카",
	"지네브라","지넷","시비유","지빌라","시빌","시모나","시몬느","자다","샤를리","샬롯",
	"자클린","저스티나","자스민","자스민","재키","자넷","샤넌","샤를린느","샤를로트","샤론",
	"잔나","잔느","준","주스타","주스티나","주스틴느","쥬제트","주디","주디스","주디타",
	"주디트","줄리아","줄리아나","줄리앙","줄리","줄리엣타","줄리엣","줄리엔느","조안나","조반나",
	"조지아나","쇼나","조지안느","조슬린","조제","조제트","조제핀","조디","조르지나","조르제트",
	"조르자","질리안","질리올라","실리야","실바","실비","실비아","신시아","신디","수잔",
	"수지","스칼렛","수잔나","스티나","스테이시","스테파니아","스테파니","스텔라","스네지아나","세이아",
	"세베라","세브린느","세냐","세오도라","세실리아","세실리","세실","세라피나","세리아","세리나",
	"세리야","셀마","젤마","세레나","세레네","세레스티느","세레스트","조이","조에","소니아",
	"소피아","소피","솔리야","솔렌느","소렌느","다시","다차","다나","타라","달린",
	"달랴","다이애나","타치아나","다니엘라","다니엘","타바사","다프네","달리아","달시","체리아",
	"체리","첼시","체레스티나","찰리","칠다","테아","디아드라","티아나","디아나","티아",
	"디트린데","티나","디나","티라","디오니자","데이지","티나","티파니","테오도라","테오필라",
	"데지데리아","데보라","데메트리아","튜네","델리아","델리치아","델피나","테레제","테레사","테레지아",
	"테레르보","툴라","툴리아","도라","토스카","도나","도나텔라","도미치아나","도미틸라","도미니카",
	"도리아나","도리","도린","도리스","토르데리제","트레이시","도로시아","도로시","도로테아","나탈리야",
	"나탈리아","나탈리","나디아","나딘","낸시","니나","니베스","니콜라","니콜","니콜레타",
	"니나","니넷","니농","닐데","닌파","넬리","네리나","넬","네레아","노에미",
	"노엘라","노엘","노라","노르마","논나","허마이오니","펄","하이디","하이데마리","하일비히",
	"파울라","파올리나","바질리아","패티","패트리샤","파멜라","해리엇","팔비","발토로메아","바르바라",
	"팬지","한나","한나마리","한네로레","밤비나","피아","비앙카","비체","비버리","피에라",
	"비비아나","힐다","힐데가르트","힐트루트","파이나","파우스티나","파비아나","피안마","피안메타","피네",
	"피오나","피오리나","피오렌티나","필리스","필로메나","페브로니야","페베","페오도라","페데리카","펠리시아",
	"펠디난다","펠르난다","포스티나","포르투나타","플라비아","브라이스","플라스코비야","플라미니아","프랑카","프랑신느",
	"프란시스","블랑슈","프랑셋","프랑수아즈","프란체스카","블랑딘느","프리다","프리데","브리짓","프리실라",
	"브리트니","브륀힐트","풀비아","브루나","플뢰르","브루넬라","플룸","프레데리카","브렌다","플로라",
	"플로렌스","플로리아나","플로렌티나","베아트리체","베아트릭스","베아트리스","헤이디","헤이즐","헤스터","베키",
	"베티","페트라","페트로넬라","베니아미나","베네딕트","베네데타","페넬로페","헤라","베라","베랑제르",
	"페린느","헤리나","헤리야","헬비","헬가","베르타","헬루타","베르티나","베르나데타","헬마",
	"헬미나","페를라","헬레나","베레니체","헬렌","헨리에타","폴라","폴린","홀리","폴리나",
	"마가렛","마사","마시","마저리","마샤","마야","마리아","마이나","마이야","마이라",
	"맥신","막달레나","마틸다","매들린","매들린","마들렌","마트로나","마누엘라","마농","마라이아",
	"마리","마리아","마리아나","마리안","마리안나","마리안네","마리","마리자","마리나","마린",
	"마리에타","마리에라","마리엘","마리온","마리카","마리네타","마리네라","마릴리스","마릴린","마릴레나",
	"말비나","말가","말가리타","말그리트","마르케타","마르슬린느","마르셀","마르타","마르첼리나","마르테",
	"마르티나","마를레네","마를레엔","미아","미아","미사","미나","미라","미카엘라","미켈라",
	"미셸","미슐린느","미네르바","뮤리엘","미라","미라나","미라벨","미란다","미리암","미리안",
	"미리야","밀바","밀카","밀드레드","미레이유","미렌느","미렐라","미로슬라바","메리","메이",
	"메이비스","메이지","메이벨","멜라니아","멜라니","메리잔드","멜리사","멜린다","멜비","멜야",
	"멜렌느","메러디스","멜로디","모이라","모건","모나","모데스타","모니카","몰리","몰가나",
	"모레나","야나","유제니","유디트","유니스","유페미아","유스티나","율리아","율리아나","요한나",
	"요란다","라냐","라헬","라이자","라이나","라이야","라일라","라비니아","라우나","라우라",
	"라켈","라셸","라나","라파엘라","라모나","라라","라리사","리아","리아네","리사",
	"리자","리즈","리제","리제롯테","리차","리나","리리야","리비아","리오넬라","리칼다",
	"리사","리제트","리타","리카","리디","리디아","리나","리넷","리비","류리",
	"류크레스","류디빈느","류드미라","릴리아나","릴리안","릴리","린다","린네아","루아나","루이자",
	"루이즈","루시","루샤","루체","루나","루크레치아","루시르","루신다","루치아","루네",
	"루비","루피나","루미","레아","레이첼","레이몬드","레이야","레이라","레네","레오나",
	"레오니","레오노라","레오노라","레온티나","레기나","레지나","레슬리","레티시",
	"레나엘","레베카","렐리아","로이네","로비사","로자","로즈","로즈마리","로제","로나",
	"로라","롤","로렐","로렌","록산느","로자리야","로자리","로잘린데","로지나","로진느",
	"로슬린느","로제르","로제타","로니야","로빈","로베르타","로미","로미나","로밀다","로라",
	"로레나","로레타","로렌차","완다",
}
{
#DIMS CONST 洋風苗字リスト = 
	"아빈","아달베르트","아치데일","아들러","아들룽","아넷","아놀드","아벨라인","아벤트로트","아미티지",
	"아멘트","아몬드","아렌스","아렌트","아론","안","아이겐","아인하르트","아우프레히트","아브린느",
	"악스","아클랜드","아샤르","아스캄","아스터","아스턴","아즈나부르","아스란","아담스","아커만",
	"아셔","애쉬필드","애쉬베리","아텐버러","아헨바흐","애딩턴","아드킨스","아드람","아틀리","애비",
	"아프리아","아멜른","알비에","알셰","알더슨","알텐부르크","알덴호프","알트마이어","알포드","알베르",
	"알펜하임","알름홀트","아레","알렌","알렌비","아론","앤빌","앵커","안슈츠","안데",
	"앤드루스","앙드레","앙투안","앙베르","앙페르","예네","예레미스","이스텔","잉그리스","바인치알",
	"발레리","바론","위얼라이트","빅스","빌펠트","빈센트","윈터","웨인라이트","웨스트","베르데",
	"벤데","보로딘느","에이든","에이블","에이미스","에일리","에인슬리","에인즈워스","에버렛","에반스",
	"에브라르","에이스","에델슈타인","에벨바인","에벨트","에렌베르크","에스텐","에지워스","에펜베르크","에디",
	"에드니","에마르","에메","에메리","엘비유","엘체","엘디","엘네스트","엘핀스톤","엘베",
	"엘멜트","엘로","엘로와","오스틴","오즈리","오덴","오브리","올드리치","오클레르","오지에",
	"오베르","오리올","올콧","올브라이트","올름스테드","오렌지","카벨","커크","카슈","카터",
	"커티스","카트라이트","가넷","가필드","칼러","카일라일","카뮤","칼버트","칼데콧","칼네",
	"갈브레이스","칼리에","카렌","카렌베르크","카론","키리","킨","캐롤","캠벨","캠벨",
	"퀴네","퀸","킬슈","킬스텐","킬너","퀸","그라세","그라니에","클라르바인","그랑빌",
	"크리즈","그린","크리스탈","그리니","그뤼네발트","크루게","쿠르트","크룸","쿠루루","클레이",
	"그레이","크레이그","그레이디","클레버","클레르","클레송","클레그","클레프","클로즈","크로포드",
	"캐리","케이","케이시","켈리","켈르","코츠","코넬","코니","콜디에","코르넬리우스",
	"코렛","사리스","산","실","신","셰르","제라르","셰리","제르베즈","지스카르",
	"시트린","샤하트","샴록","샹폴리옹","슈발리에","슈타인펠트","조나","질","수라","스튜어트",
	"스톤","스노우","스프링필드","세실","쏜","더글라스","다리에","탈리스","다리모어","달시아크",
	"치어스","체셔","디트리히","딘","디즐리","틸릿","딜크","티롤","틴달","데멜",
	"데넷","드포르주","뒤 베레","뒤비에","뒤러","뒤샹","뒤피","뒤랑벨제","테랄","드 리르",
	"두제","도나시앙","드라노에","드란","드레이크","드레제","드레셀","나비에","닐","닌",
	"니코","니콜라","니콜","누빌","네르","노엘","노스","놀즈","버크","파크",
	"하그리브스","하스","하제","하젠바인","하트","하트필드","바틀렛","버나드","버넷","번즈",
	"하이엇","하이젠베르크","하이드리히","파일","하인첼","바인리히","바셰","바슈라르","바조트","바스커빌",
	"바즐레르","백스터","백랜드","바티뉴","허니볼","발라드","파라디스","발라보","배리","발슈미데",
	"발테","발텔","발리에","해로즈","바로우즈","바로","한스","한나포드","피어슨","피크",
	"빈","비텐펠트","피노","비욘","힐트","파리에르","파론","피","필츠","핀즈베리",
	"푸리에","펜","펠리스","펠른바흐","페레르","포춘","폴크발츠","퐁텐","브라이스","브라운",
	"브라셰르","블랙","블랙웰","블랑","블랑셰","블랑제","프란시스","브란덴부르크","프리스","프리제",
	"프리델","프림로즈","플린트","플루드","블룸","블르제","브래디","프로이덴탈","플로에","플로렌스",
	"프로스트","브론","브론델","헤어","베어드","베아트릭스","베이커","헤이즈","헤이든","베이리얼",
	"베일리","헤일","베일","헤인즈","베어","베렌트","베시에르","베케라트","헤리엇","벨",
	"벨크바인","벨쥬롱","벨츠","헬츠펠트","벨티에","벨트람","벨트홀트","벨나르","벨넷","헬멜",
	"벨리오즈","펠르","벨른슈타인","베레스포드","베로","페로","벤자민","헨네펠트","보보아르","보랑",
	"홀슈타인","볼츠","홀름","화이트","화이트필드","머큐리","마르","마이","마이에","맥클린",
	"마리에트","마르셰","말렛","마롤","미아","미셸","미실리에","미스트랄","뮐러","밀",
	"문","메이","메이시","메이스필드","메이브","메이필드","메이플","멜러","메릴","모아",
	"라일","라인펠트","라쿠르","라스페이드","라치에르","라프라드","라브라슈리","라브레","라미","라루에트",
	"란스","란느","리아","리크","리스","리스펠트","리델슈타인","리드","리넬","리펜슈타르",
	"린","리비에르","리엘","리오","리스나르","리셰","립스","리파르","류르","류카",
	"류미에르","릴리","릴리엔크론","르 쉬르","르 테리에","르 포르","르 블랑","르 벨","르 루","르 로아",
	"루이스","루비에","루스","루셀","루니","루클레르","루시에","루슈르","루나르","루페",
	"루블랑","루모니에","레이","레인","레비","레벤","레데","레라","레클람","레제",
	"레첼","레드필드","레니에","레뮤자","렌","로이스","로이드","로엔슈타인","로제르","로즈",
	"로제","로젠슈티르","롤","로자리","로로트",
}

IF 洋風和風 == 0
	選択苗字 = %苗字リスト:(RAND:(VARSIZE("苗字リスト")))%
	選択名前 = %名前リスト:(RAND:(VARSIZE("名前リスト")))%
	NAME:캐릭터_ID = %選択苗字% %選択名前%
ELSEIF 洋風和風 == 1
	選択苗字 = %洋風苗字リスト:(RAND:(VARSIZE("洋風苗字リスト")))%
	選択名前 = %洋風名前リスト:(RAND:(VARSIZE("洋風名前リスト")))%
	NAME:캐릭터_ID = %選択名前% %選択苗字%
ELSEIF 洋風和風 == 2
	IF RAND:2 == 0
		選択苗字 = %洋風苗字リスト:(RAND:(VARSIZE("洋風苗字リスト")))%
		選択名前 = %名前リスト:(RAND:(VARSIZE("名前リスト")))%
		NAME:캐릭터_ID = %選択名前% %選択苗字%
	ELSE
		選択苗字 = %苗字リスト:(RAND:(VARSIZE("苗字リスト")))%
		選択名前 = %洋風名前リスト:(RAND:(VARSIZE("洋風名前リスト")))%
		NAME:캐릭터_ID = %選択名前% %選択苗字%
	ENDIF
ENDIF
CALLNAME:캐릭터_ID = %選択名前%

;以下eratohoKより引用
;-------------------------
;ランダム캐릭터に特定の素質を지정の確率で持たせる関数
;-------------------------
@RCHARA_TALENT_SINGLE(캐릭터_ID, 素質_ID, 確率, 素質値)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 素質_ID
#DIM DYNAMIC 確率
#DIM DYNAMIC 素質値

LOCAL:0 = RAND:100
IF LOCAL:0 < 確率
	TALENT:캐릭터_ID:素質_ID = 素質値 != 0 ? 素質値 # 1
ENDIF

;-------------------------
;ランダム캐릭터に相反する２つの素質を지정の確率で持たせる関数
;-------------------------
@RCHARA_TALENT_OPPOSE(캐릭터_ID, 素質１_ID, 素質２_ID, 確率_素質１, 確率_素質２)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 素質１_ID
#DIM DYNAMIC 素質２_ID
#DIM DYNAMIC 確率_素質１
#DIM DYNAMIC 確率_素質２

LOCAL:0 = RAND:100
IF LOCAL:0 < 確率_素質１
	TALENT:캐릭터_ID:素質１_ID = 1
ELSEIF 99 - LOCAL:0 < 確率_素質２
	TALENT:캐릭터_ID:素質２_ID = 1
ENDIF

;-------------------------
;ランダム캐릭터に-1/1で설정する素質を지정の確率で持たせる関数
;-------------------------
@RCHARA_TALENT_DUAL(캐릭터_ID, 素質_ID, 確率_－１, 確率_＋１, 素質値１, 素質値２)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 素質_ID
#DIM DYNAMIC 確率_－１
#DIM DYNAMIC 確率_＋１
#DIM DYNAMIC 素質値１
#DIM DYNAMIC 素質値２

LOCAL:0 = RAND:100
IF LOCAL:0 < 確率_－１
	TALENT:캐릭터_ID:素質_ID = 素質値１ != 0 ? 素質値１ # -1
ELSEIF 99 - LOCAL:0 < 確率_＋１
	TALENT:캐릭터_ID:素質_ID = 素質値２ != 0 ? 素質値２ # 1
ENDIF
