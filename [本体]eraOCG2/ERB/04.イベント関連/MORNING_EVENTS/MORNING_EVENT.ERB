@MORNING_EVENT
IF RAND:LOTTERY_RAIT == 1
	CALL WIN_THE_LOTTERY
ELSE
	RETURN NO_MORNING_EVENTS
ENDIF
RETURN HAD_MORNING_EVENTS

@WIN_THE_LOTTERY
#DIM CATEGORY
#DIM 이벤트番号, 10
#DIM カウンタ

DRAWLINE
PRINTL 현관 밖에서 소리가 나서 눈을 떴다
PRINTL 현관 밖에는 응모한 추첨 당첨품이 놓여 있었다！
PRINTL

VARSET 이벤트番号, -1
カウンタ = 0

이벤트番号:(カウンタ++) = 0
이벤트番号:(カウンタ++) = 1
이벤트番号:(カウンタ++) = 2
이벤트番号:(カウンタ++) = 3
SIF ITEM:플레이매트 == 0
	이벤트番号:(カウンタ++) = 4
;앞치마は無意味に何度でも当たる
이벤트番号:(カウンタ++) = 5
SIF ITEM:덤벨 == 0
	이벤트番号:(カウンタ++) = 6
이벤트番号:(カウンタ++) = 7
SIF ITEM:접이식우산 == 0
	이벤트番号:(カウンタ++) = 8
SIF ITEM:냄비 == 0
	이벤트番号:(カウンタ++) = 9
	
	
SELECTCASE 이벤트番号:RAND(カウンタ)
	CASE 0
		PRINTL 카드의 모둠세트다！
		PRINTL SR카드 4장、UR카드를 2장 손에 넣었다！（교환용）
		ITEM:SR카드 += 4
		ITEM:UR카드 += 2
	CASE 1
		PRINT ……고 생각했는데, 이는 고향 납세의 답례품인
		PRINTDATA
			DATA 특산 채소 모둠
			DATA 국내산 소고기 모둠
			DATA 신선해산물세트
			DATA 브랜드 쌀
		ENDDATA
		PRINTL 이다！
		PRINTL 식자재를 10개 손에 넣었다！
		ITEM:식재료 += 10
	CASE 2
		CATEGORY = RAND:5
		PRINTFORML 잡지 『%MAGAZINE_TITLE:CATEGORY%』의 최신호를 손에 넣었다
		; 実際の番号に更新 촬영と피사は使っていないので、飛ばす
		SIF CATEGORY > 1
			CATEGORY += 2
		CATEGORY += 70
		PRINTFORML %EXPNAME:CATEGORY%+5
		EXP:MASTER:CATEGORY += 5
	CASE 3
		PRINTFORML 고향 납세로 팩이 도착했다！！
		CALL CARD_PACK_SHORI(3)
    CASE 4
		PRINTW 플레이 매트가 당첨됐다！
		PRINTL 카드 게임 플레이매트가 아니라、변태스러운 플레이 매트였다…
		ITEM:플레이매트 = 1
	CASE 5
		PRINTL 에이프런이 당첨됐다！
		PRINTDATA
			DATA 푸른 눈의 백룡
			DATA 붉은 눈의 흑룡
			DATA 사이버 엔드 드래곤
			DATA 궁극보옥신 레인보우 드래곤
			DATA 스타더스트 드래곤
			DATA 레드 데몬즈 드래곤
			DATA 갤럭시아이즈 포톤 드래곤
			DATA 갤럭시아이즈 타키온 드래곤
			DATA 오드아이즈 펜듈럼 드래곤
			DATA 스타브 베놈 퓨전 드래곤
			DATA 파이어월 드래곤
			DATA 바렐로드 드래곤
		ENDDATA
		PRINTL 무늬가 한정품이다！　멋지다！！
		ITEM:앞치마 = 1
	CASE 6
		PRINTL 덤벨에 당첨됐다！
		PRINTL 방에서도 하자！　트레이닝！！
		ITEM:덤벨 = 1
	CASE 7
		PRINTL 술에 당첨됐다！
		CATEGORY = RAND:5 + 420
		PRINTFORML %ITEMNAME:CATEGORY%를 5개 손에 넣었다！
		ITEM:CATEGORY += 5
	CASE 8
		PRINTL 접이식 우산에 당첨됐다！
		PRINTL 이런 건 의외로 내가 살 타이밍이 없다…
		ITEM:접이식우산 = 1
	CASE 9
		PRINTL 냄비에 당첨됐다！（조리기구）
		IF GET_MONTH() == "冬の月"
			PRINTL 모처럼의 기회니까、모두와 함께 전골을 해 먹는 것도 좋을 것 같아…
		ELSE
			PRINTL 모처럼의 기회니까、겨울이 되면 모두와 함께 전골을 먹고 싶어…
		ENDIF
		ITEM:냄비 = 1
ENDSELECT

