@CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,표시形式,めくり数,反転플래그)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 대상者
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 표시形式
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード,99
#DIM DYNAMIC 選択カード_ID,99
#DIM DYNAMIC めくり数
#DIM DYNAMIC 上級値
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 反転플래그
#DIM DYNAMIC カードID
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 덱サーチ플래그
#DIM DYNAMIC 山切り無し
#DIM DYNAMIC 셔플플래그
#DIM DYNAMIC 상대셔플플래그
#DIM DYNAMIC 효과추가종류보존,20
#DIM DYNAMIC 複数カウンタ
#DIM DYNAMIC 最大値
#DIM DYNAMIC 消去カード
#DIM DYNAMIC 一時보존,99
#DIM DYNAMIC 残り空枠
#DIM DYNAMIC 소환成功
#DIM DYNAMIC ランダム値
#DIM DYNAMIC チェーン불가
VARSET RESULT
VARSET 選択カード,-1

SIF 효과条件山切り無し == 1
	山切り無し = 1

SIF ゾーン == "특수"
	チェーン불가 = 1

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 反転플래그 == 0
	残り空枠 = 5 - モンスター数(듀얼리스트)
	SIF 残り空枠 <= 0
		RETURN 0
	
	IF INRANGE(효과条件複数,1,5)
		;空枠より많을 경우はNGで返す
		SIF 효과条件複数 > 残り空枠
			RETURN 0
	ENDIF
	IF INRANGE(효과条件複数,90,99) || 효과条件複数 == 任意の数
		IF 효과条件複数 == 任意の数
			;任意の数は100 - 残り空枠で98（2장까지 ）とかの설정を作成する
			효과条件複数 = 100 - 残り空枠
		ELSE
			;○○数までは、最大値を변경する。
			;例えば複数=97で残り枠2の경우、98にする
			SIF 100 - 효과条件複数 >= 残り空枠
				효과条件複数 = 100 - 残り空枠
		ENDIF	
	ENDIF
ELSEIF 反転플래그 >= 1
	残り空枠 = 5 - モンスター数(対面者)
	SIF 残り空枠 <= 0
		RETURN 0
	IF INRANGE(효과条件複数,1,5)
		SIF 효과条件複数 > 残り空枠
			RETURN 0
	ENDIF

	IF INRANGE(효과条件複数,90,99) || 효과条件複数 == 任意の数
		IF 효과条件複数 == 任意の数
			;任意の数は100 - 残り空枠で98（2장까지 ）とかの설정を作成する
			효과条件複数 = 100 - 残り空枠
		ELSE
			;○○数までは、最大値を변경する。
			;例えば複数=97で残り枠2の경우、98にする
			SIF 100 - 효과条件複数 >= 残り空枠
				효과条件複数 = 100 - 残り空枠
		ENDIF
	ENDIF
ENDIF




FOR カウンタ,0,10
	효과추가종류보존:カウンタ = 효과추가종류:カウンタ
NEXT

IF 효과範囲덱 == 1 || 효과範囲상대덱 == 1
	CALL URARA_CHANCE(듀얼리스트,処理中_효과ID,"特召")
	IF RESULT == 1
		CALL SHUFFLE(듀얼리스트)
		RETURN 0
	ENDIF
	SIF 효과範囲덱 == 1
		셔플플래그 = 1
	SIF 효과範囲상대덱 == 1
		상대셔플플래그 = 1
	
ENDIF
IF 효과範囲묘지 == 1 || 효과範囲상대묘지 == 1
	CALL WARASHI_CHANCE(듀얼리스트,処理中_효과ID,"特召")
	IF RESULT == 1
		RETURN 0
	ENDIF
ENDIF
IF GETVAR(@"%듀얼리스트%_インパ효과") >= 1
	CALL FLASH(2)
	PRINTFORMW 영왕의 파동《도미나스 임펄스》에 의해 효과가 방해되었습니다。
	SETVAR @"%듀얼리스트%_インパ효과",0
	RETURN 0
ENDIF


$CHOICE_LOOP
カードID = 処理中_효과ID
IF 듀얼리스트 == "자신"
	CUSTOMDRAWLINE —
	PRINT 【카드를 선택해 주세요】
	IF オプション_略표시 == 1
		PRINTBUTTON "[간이 표시 중]", 6666
	ELSE
		PRINTBUTTON "[간이 표시]", 6666
	ENDIF
	PRINTFORM　%"[7777] - 필드 / 패 정보 확인",55,RIGHT%

	PRINTL

	CUSTOMDRAWLINE —
	PRINTL 
ELSEIF 듀얼리스트 == "상대"
	CALL ENEMY_RUTIN_CARD_SS(参照先,場所,カードID)
ENDIF

$INPUT_LOOP
IF 효과条件複数
	FOR カウンタ,0,複数カウンタ
		효과条件選択済:カウンタ = 選択カード:カウンタ 
	NEXT
ENDIF
IF 反転플래그 >= 1
	CALL CARD_FILTER_BASIC(듀얼리스트,"上級",ゾーン,場所,めくり数,7)
ELSE
	CALL CARD_FILTER_BASIC(듀얼리스트,"上級",ゾーン,場所,めくり数,2)
ENDIF
条件달성 = RESULT:1

IF 듀얼리스트 == "자신"
	PRINTL
	IF  (INRANGE(효과条件複数,90,99) || 효과条件複数 == 任意の数) && 複数カウンタ >= 1
		PRINTL 　[8888] - 결정
	ENDIF
	IF 条件달성 != 0 && 효과条件任意 != 1
		SETCOLOR C_GRAY
		PRINTFORM 　(9999) - 돌아간다(선택 가능한 카드가 있습니다)
		RESETCOLOR
	ELSE
		PRINTFORM 　%"[9999] - 돌아간다",41,LEFT%
	ENDIF
	PRINTL
	BINPUT
	IF RESULT == 7777
		CALL DIPLAY_BOARD(2)			
		GOTO CHOICE_LOOP
	ELSEIF RESULT == 6666
		オプション_略표시 ^= 1
		GOTO CHOICE_LOOP
	ENDIF
	選択カード:複数カウンタ = RESULT
ELSEIF 듀얼리스트 == "상대"
	IF RESULT == 9999 &&  (INRANGE(효과条件複数,90,99) || 효과条件複数 == 任意の数) && 複数カウンタ >= 1 
		選択カード:複数カウンタ = 8888
	ELSE
		選択カード:複数カウンタ = RESULT
	ENDIF
	IF 選択カード:複数カウンタ == 8888
		SETCOLOR C_W_RED
		CUSTOMDRAWLINE —
		PRINTL 【특수소환 가능한 카드가 없습니다】
		CUSTOMDRAWLINE —
		RESETCOLOR
		WAIT
	ENDIF
ENDIF



IF 選択カード:複数カウンタ == 9999
	PRINTFORMW 아무것도 하지 않았습니다。
	RETURN 0

ELSEIF 選択カード:複数カウンタ == 8888
	選択カード:複数カウンタ = -1
	GOTO INPUT_SKIP

ELSEIF 選択カード:複数カウンタ != 9999
	CALL CARD_SELECT(듀얼리스트,選択カード:複数カウンタ)
	選択カード_ID = GETVAR(@"%대상者（CS）%_%参照先（CS）%:{選択カード（CS）}")
	CALL SELECT_UI_SET(듀얼리스트,選択カード_ID,3)
	IF RESULT == 1
		GOTO CHOICE_LOOP
	ENDIF
ENDIF

IF 효과条件複数 != 0
	IF 효과条件複数 > (複数カウンタ + 1)
		IF 효과条件固名前選択
			CALL CARD_SELECT(듀얼리스트,選択カード:複数カウンタ)
			CALLFORM CARDNAME_{GETVAR(@"%대상者（CS）%_%参照先（CS）%:{RESULT}")},"名前"
			효과条件固名前:複数カウンタ = %RESULTS%
		ENDIF
		複数カウンタ ++
		SIF INRANGE(효과条件複数,90,99) && 複数カウンタ >= (100 - 효과条件複数)
			GOTO INPUT_SKIP
		GOTO INPUT_LOOP
	ELSE
		$INPUT_SKIP

		FOR カウンタ,0, (複数カウンタ + 1)
			最大値 = -1
			FOR カウンタB,0,(複数カウンタ + 1)
				IF 選択カード:カウンタB >= 最大値
					最大値 = 選択カード:カウンタB
					一時보존:カウンタ = 選択カード:カウンタB
					消去カード = カウンタB
				ENDIF
			NEXT
			ARRAYREMOVE 選択カード, 消去カード, 1
		NEXT
		FOR カウンタ,0,(複数カウンタ + 1)
			選択カード:カウンタ = 一時보존:カウンタ
		NEXT

	ENDIF
ENDIF

IF 효과条件複数 != 0
	IF 효과条件RND == 1
		ランダム値 = RAND:(複数カウンタ + 1)
		CALL CARD_SELECT(듀얼리스트,選択カード:ランダム値)
		選択カード_ID = GETVAR(@"%대상者（CS）%_%参照先（CS）%:選択カード（CS）")
		PRINTFORMW 특수 소환！
		CALL CARD_SP_SUMMON(듀얼리스트, 本参照先（CS）,選択カード（CS）,표시形式, 反転플래그,チェーン불가)
		소환成功 = RESULT
	ELSE
		FOR カウンタ,0,(複数カウンタ + 1)
			SIF 選択カード:カウンタ == -1
				CONTINUE
			CALL CARD_SELECT(듀얼리스트,選択カード:カウンタ)
			選択カード_ID = GETVAR(@"%대상者（CS）%_%参照先（CS）%:選択カード（CS）")
			FOR カウンタB,0,10
				효과추가종류:カウンタB = 효과추가종류보존:カウンタB
			NEXT
			PRINTFORMW 특수 소환！
			CALL CARD_SP_SUMMON(듀얼리스트, 本参照先（CS）,選択カード（CS）,표시形式, 反転플래그,チェーン불가)
			소환成功 += RESULT ;Anonym추가
		NEXT
	ENDIF
ELSE
	PRINTFORMW 특수 소환！
	CALL CARD_SELECT(듀얼리스트,選択カード)
	選択カード_ID = GETVAR(@"%대상者（CS）%_%参照先（CS）%:選択カード（CS）")
	CALL CARD_SP_SUMMON(듀얼리스트, 本参照先（CS）,選択カード（CS）,표시形式, 反転플래그,チェーン불가)
	소환成功 = RESULT
ENDIF






IF 셔플플래그 == 1 && 山切り無し != 1
	CALL SHUFFLE(듀얼리스트)
ENDIF

IF 상대셔플플래그 == 1 && 山切り無し != 1
	CALL SHUFFLE(対面者)
ENDIF

VARSET RESULT
SIF 本参照先（CS） == "덱"
	RESULT:2 = 1
RESULT:1 = 選択カード_ID
RETURN 소환成功


