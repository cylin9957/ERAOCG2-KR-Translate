@CARD_EROTICS(カードID,듀얼리스트,듀얼外処理)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カードID
#DIM DYNAMIC 確認カードID
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 듀얼外処理
VARSET RESULT

VARSET 캐릭터_ID, -1

TRYCALLFORM CARD_{カードID}("連動ID")
SIF RESULT > 0
	カードID = RESULT

SELECTCASE カードID
;謎のレウス헌터さん
	CASE 36650
		IF 듀얼리스트 == "자신"
			確認カードID = MASTER
		ELSE
			確認カードID = OPPOSER
		ENDIF

	CASE MASTER補助, 9990
		確認カードID = MASTER
		
	CASE 상대プレイヤー
		IF 듀얼리스트 == "자신"
			RETURN OPPOSER
		ELSE
			RETURN MASTER
		ENDIF
		RETURN -1


	CASE 現在プレイヤー
		IF 듀얼리스트 == "자신"
			RETURN MASTER
		ELSE
			RETURN OPPOSER
		ENDIF
		RETURN -1
	CASEELSE
		確認カードID = カードID
ENDSELECT

;캐릭터番号総当たりで、確認カードIDと同じ番号なら、その캐릭터IDを返す
FOR カウンタ,1,CHARANUM
	IF NO:カウンタ == 確認カードID
		캐릭터_ID = カウンタ
		RETURN 캐릭터_ID
	ENDIF
NEXT
RETURN -1



@FIND_CHARA_GID(캐릭터_ID,CNT설정)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カードID
#DIM DYNAMIC 確認カードID
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 듀얼外処理
#DIM DYNAMIC CNT설정
#DIM DYNAMIC 条件合致数
#DIM DYNAMIC 条件合致ID,100

;【条件달성０】そのカード_IDがあるなら返す
カードID = NO:캐릭터_ID
IF ENUMFILES("RESOURCES/DUEL", @"{カードID}_DUEL.*")
	IF CNT설정 >= 1
		条件合致ID:条件合致数 = カードID
		条件合致数 ++
	ELSE
		RETURN カードID
	ENDIF
ENDIF
;＞＞　第２チェック、その他カード　＜─────────────────────────────────────
;総当たり
FOR カウンタ,10000,70000
	VARSET RESULTS
	;名前のないカードは弾く
	TRYCALLFORM CARDNAME_{カウンタ}, "名前"
	SIF RESULTS == ""
		CONTINUE

	;複数캐릭터は弾く
	TRYCALLFORM CARD_{カウンタ}, "連動ID"
	SIF RESULT:1 >= 1
		CONTINUE
	;GIDの취득であるためゲーム外での処理
	CALL CARD_EROTICS(カウンタ,0,1)
	SIF RESULT != 캐릭터_ID
		CONTINUE
	;カードIDに当たるリソースを確認して、설정する
	IF ENUMFILES("RESOURCES/DUEL", @"{カウンタ}_DUEL.*")
		IF CNT설정 >= 1
			条件合致ID:条件合致数 = カウンタ
			条件合致数 ++
		ELSE
			RETURN カウンタ
		ENDIF
	ENDIF
NEXT
;＞＞　第３チェック、ALTカードグラ　＜─────────────────────────────────────
;総当たり
IF CNT설정 >= 2
	FOR カウンタ,2,20
		;カードIDに当たるリソースを確認して、설정する
		IF ENUMFILES("RESOURCES/DUEL", @"{カードID}_DUEL_{カウンタ}.*")
			IF CNT설정 >= 1
				条件合致ID:条件合致数 = カードID + (100000 * カウンタ)
				条件合致数 ++
			ELSE
				RETURN 100000 * カウンタ
			ENDIF
		ENDIF
	NEXT
ENDIF

IF CNT설정 >= 1
	FOR カウンタ,0,20
		RESULT:(カウンタ + 1) = 条件合致ID:カウンタ
	NEXT	
	RETURN 条件合致数
ENDIF
RETURN -1

@CARD_EROTICS_FIELD(듀얼리스트,ゾーン,場所,反転플래그)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 대상者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIM DYNAMIC カード_ID
#DIM DYNAMIC 조교대상
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 효과蓄積
#DIM DYNAMIC 反転플래그


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 反転플래그 == 1
	대상者 = %対面者%
ELSE
	대상者 = %듀얼리스트%
ENDIF


IF ゾーン == "필드"
	カード_ID = GETVAR(@"%대상者%_필드:場所")
	IF GETVAR(@"%대상者%_MZ_変化元ID:場所") > -1
		カード_ID = GETVAR(@"%대상者%_MZ_変化元ID:場所")
	ENDIF
ELSEIF ゾーン == "마함"
	カード_ID = GETVAR(@"%대상者%_마함:場所")
	IF GETVAR(@"%대상者%_魔_変化元ID:場所") > -1
		カード_ID = GETVAR(@"%대상者%_魔_変化元ID:場所")
	ENDIF
ELSE
	カード_ID = GETVAR(@"%대상者%_%ゾーン%:場所")
ENDIF

;正規起動は必要ない
CALL CARD_EROTICS(カード_ID)
RETURN RESULT