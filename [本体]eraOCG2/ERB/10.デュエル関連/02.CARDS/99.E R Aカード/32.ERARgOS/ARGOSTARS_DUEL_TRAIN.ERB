@ARGOSTARS_DUEL_TRAIN(듀얼리스트,参照カードID,処理플래그)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 캐릭터番号

#DIM DYNAMIC 変数플래그
#DIM DYNAMIC 処理플래그
#DIM DYNAMIC 前성교불가
#DIM DYNAMIC 後성교불가
#DIM DYNAMIC 男性器あり
#DIM DYNAMIC あなた竿あり
#DIM DYNAMIC 키스불가
#DIM DYNAMIC 친애취득
#DIM DYNAMIC 굴복됨취득
#DIM DYNAMIC 純愛플래그
#DIM DYNAMIC 조교メニュー


;듀얼리스트 = 자신、캐릭터番号はARG☆Sのメンバーで確定していることを前提として処理を進める。
;캐릭터の조교상황に応じて適切な性的조교を行えるようにする。後穴は애널경험が一定以上必要、など。
;メンバーごとの文章の분기も行うようにしたい（今後の目標）

;基本的にあなたから정액が出る前提で色々とやってる。女で竿なしのあなたの場合は処理が相当へんてこになり行える커맨드も少ない。
;"あなた"が物凄い絶倫である前提で色々とムリもやってる（Ｐ使う조교だと確実に사정するようにしてる）ので、자신의 체력には주의を要する。
;また、ゴムは一切つける気がない。アスリート女子への조교は生で中だしをするのが作法。メスであることをわからせていけ。

;ちなみに、純愛ルートだと抱きつくとか키스とかそんぐらいの軽いのも選べる（まずありえんけど키스미경험だと키스すら選べない）
;→択が絞られすぎてても面白みないから、ある程度ハードじゃない커맨드や조교系以外は選べるようにする。

;一部の選択肢の분기テ키스トは未作成なので[SKIP]で区切っている。おいおい추가することにしよう（ゆっくり遊星）

CALL CARD_EROTICS(参照カードID,듀얼리스트)
캐릭터番号 = RESULT

;陥落してるかどうか等の各種플래그を確認して変数に結果を記入
CALL CARDID_TALENTCHECK(参照カードID)
変数플래그 = RESULT
SIF !(変数플래그 & 1)
	前성교불가 = 1
SIF 変数플래그 & 2
	男性器あり = 1
SIF 変数플래그 & 4 || 変数플래그 & 8
	前성교불가 = 1
SIF 変数플래그 & 16
	키스불가 = 1
SIF 変数플래그 & 32
	친애취득 = 1
SIF 変数플래그 & 64
	굴복됨취득 = 1

;後ろ성교の可否を確認（通常の애널섹스可能になるA경험を前提として要求、さらにA감각レベル4以上、鈍感で+1、敏感で-2で使用可能）
IF EXP:캐릭터番号:Ａ경험 < EXPLV:3
	IF ABL:캐릭터番号:Ａ감각 < 5 && TALENT:캐릭터番号:Ａ감도 <= -1
		後성교불가 = 1
	ELSEIF ABL:캐릭터番号:Ａ감각 < 4 && TALENT:캐릭터番号:Ａ감도 == 0
		後성교불가 = 1
	ELSEIF ABL:캐릭터番号:Ａ감각 < 2 && TALENT:캐릭터番号:Ａ감도 <= 1
		後성교불가 = 1
	ENDIF
ELSE
	後성교불가 = 1
ENDIF

;純愛系の状態かどうかを추가で確認（친애 또는 연모なら問題なく純愛、굴복してないならその前ぐらいの호감도+고통각인なければ入れる）
IF 친애취득 == 1 || TALENT:캐릭터番号:연모 == 1
	純愛플래그 = 1
ELSEIF 굴복됨취득 == 0 && CFLAG:캐릭터番号:호감도 > 1500 && MARK:캐릭터番号:고통각인 == 0
	純愛플래그 = 1
ENDIF

;竿があり、상대가 친애 또는 굴복됨の場合は정력減ってたら회복する
IF TALENT:MASTER:성별 & 2
	あなた竿あり = 1
	;ターン数を数えて、文章の분기を増やしてみてもいいかも
	PRINTFORML %CALLNAME:캐릭터番号%의、워밍업에 대해 가볍게 뜨거워진 몸에서 흘러내리는 땀이 그리는 곡선과、
	PRINTFORML 유니폼에서 풍겨오는 암컷의 냄새에、%CALLNAME:MASTER%의 정력이 자극된다・・・
	PRINTW 

	;BASE:MASTER:발기 = MAXBASE:MASTER:발기
	IF ( 친애취득 || 굴복됨취득) && BASE:MASTER:정력 < (MAXBASE:MASTER:정력 / 2)
		BASE:MASTER:정력 = (MAXBASE:MASTER:정력 / 2)
	ENDIF
ENDIF


;SELECTCASE 参照カードID
;アドラ(一旦캐릭터個別분기なしで)
;CASE 1680,11680,41680
	
	IF 친애취득 == 1
		PRINTFORML %CALLNAME:캐릭터番号%는 내방자가 %CALLNAME:MASTER%인것을 눈치채니, 부드럽게 웃었다.
		PRINTFORML 똑바로 %CALLNAME:MASTER%를 바라본다 %CALLNAME:캐릭터番号%의 눈동자 깊은 곳에는, 앞으로 할 "지도"에 대한 기대가 엿보인다.
		
	ELSEIF TALENT:캐릭터番号:연모 == 1
		PRINTFORML %CALLNAME:MASTER%의 모습을 본 %CALLNAME:캐릭터番号%는, 귀까지 빨개져서 부끄러운 듯 몸을 꼬았다.
		PRINTFORML %CALLNAME:캐릭터番号%는 긴장하면서도, 앞으로 둘이서 할 "지도"를 상상하고 있는 듯하다.
		
	ELSEIF 굴복됨취득 == 1 && CFLAG:캐릭터番号:호감도 >= 10000
		PRINTFORML %CALLNAME:MASTER%의 바람을 이해한 %CALLNAME:캐릭터番号%의 표정이, 아름다운 운동선수 소녀에서 주인을 섬기는 음란한 암컷으로 변했다.
		PRINTFORML 잘 훈련된 %CALLNAME:캐릭터番号%는, 자신의 암컷성을 어필하면서도 %CALLNAME:MASTER%의 "지도"를 기대하며 기다리고 있다.
		
	ELSEIF 굴복됨취득 == 1
		;굴복됨 오프닝
		PRINTFORML 겁먹은 표정의 %CALLNAME:캐릭터番号%였지만, 그 볼이 붉어진 이유는 시합의 고양감 때문만은 아닐 것이다.
		PRINTFORML %CALLNAME:MASTER%가 해온 격렬한 조교로 인해, %CALLNAME:캐릭터番号%에게는 노예로서의 마음가짐이 자리잡아가고 있었다.
		
	ELSEIF MARK:캐릭터番号:굴복각인 >= 3
		PRINTFORML %CALLNAME:MASTER%가 요구하는 성행위에 길들여져, 육체가 마음을 배신하고 있다. 그런 자신의 변화에 %CALLNAME:캐릭터番号%는 떨고 있었다.
		
	ELSEIF MARK:캐릭터番号:굴복각인 == 2
		PRINTFORML 두려워하면서도, 도망치지 않고 명령을 가만히 기다리는 %CALLNAME:캐릭터番号%는, 조금씩 %CALLNAME:MASTER%가 원하는 모습에 가까워지고 있었다.
		
	ELSEIF MARK:캐릭터番号:굴복각인 == 1
		PRINTFORML %CALLNAME:MASTER%가 주는 고통이나 굴욕을 두려워하면서도, %CALLNAME:캐릭터番号%는 어깨를 떨며 견디는 것밖에 할 수 없다.
		
	ELSE
		;굴복하지 않은 상태의 오프닝
		PRINTFORML %CALLNAME:캐릭터番号%는 긴장과 두려움에 떨고 있어, 이쪽 모습을 직시하는 것도 할 수 없는 듯했다.
	ENDIF

	PRINTL 

	IF 純愛플래그 == 1 && 친애취득 == 1
		PRINTFORMW 안아준 %CALLNAME:캐릭터番号%는, 볼을 붉히며 %CALLNAME:MASTER%가 하는 대로 몸을 맡겼다。
	ELSEIF 純愛플래그 == 1
		PRINTFORMW 그런 %CALLNAME:캐릭터番号%를, %CALLNAME:MASTER%는 다정하게 살며시 안아주었다。
	ELSE
		PRINTFORMW 자, %CALLNAME:캐릭터番号%에게 어떤 "지도"를 해줄까.
	ENDIF
	
	PRINTL
	
;ENDSELECT

;できる性行為をチェック、あなたに竿がないとできない커맨드が結構ある

;-マッサージ系-
;[0]-マッサージ（健全なほうのマッサージ）　常に可能　※굴복각인3＋욕망3以上でHおねだりするようになる
;[1]-가슴をマッサージ　常に可能
;[2]-前穴をマッサージ　前성교불가だと질内사정しない
;[3]-後穴をマッサージ　後성교불가だと애널사정しない
;[4]-両穴をマッサージ　前＋穴両方可能＆굴복됨みで出現　純愛플래그あると불가

;-봉사系-
;[10]-指に봉사させる　常に可能
;[11]-딥키스させる　키스미경험불가＆M감각1以上&봉사정신1
;[12]-口で봉사させる　키스미경험불가&봉사정신1&竿必要
;[13]-가슴で봉사させる　키스미경험불가＆B감각2以上(貧乳未満は3以上)+봉사정신2&竿必要
;[14]-기승위で봉사させる　前성교可能＆陥落＆봉사정신4以上必要

;-조교系-　純愛플래그が立ってると出てこなくなる
;[20]-자위させる　常に可能　：자위
;[21]-ケツを叩く　常に可能　：尻叩き　・・・호감도変動させて、굴복の珠をプレゼント
;[22]-ぶっかけ　굴복각인3以上、노출증2以上　：ぶっかけ　정액の匂いを漂わせながら試合させる（フレーバー）
;[23]-밧줄で긴박する　밧줄が必要。굴복각인3以上、마조끼3以上、노출증3以上　：구속晒し
;[24]-精神を叩き直す　마조끼5以上、굴복됨＋호감도1万以上、패배버릇　：なぶり殺し　※まだ未作成だが絶対に入れる。絶対にだ。


;-愛情系-　純愛플래그が立ってると出現
;[30]-抱きしめる　常に可能　：조교にならない感じの選択肢
;[31]-애무する　常に可能　：軽い애무
;[32]-키스する　키스미경험불가　軽い딥키스
;[33]-섹스する　　前성교可能＆연모、봉사정신3以上　：기승위を利用
;[34]-抜いてあげる　　친애、アルゴに竿あり　：受펠라を利用　※守備範囲外

CUSTOMDRAWLINE —
PRINTL -마사지 걔-
PRINTL [0] - 마사지한다
PRINTL [1] - 가슴을 마사지한다
PRINTL [2] - 앞구멍을 마사지한다
PRINTL [3] - 뒷구멍을 마사지한다

SIF !前성교불가 && !後성교불가 && 굴복됨취득 && あなた竿あり
	PRINTL [4] - 양 구멍을 마사지한다

PRINTL 
PRINTL -봉사계-

PRINTL [10] - 손가락으로 봉사시킨다
SIF !키스불가 && ABL:캐릭터番号:Ｍ감각 >= 1 && ABL:캐릭터番号:봉사정신 >= 1 && !純愛플래그
	PRINTL [11] - 딥키스 시킨다

[SKIPSTART]
SIF !키스불가 && ABL:캐릭터番号:기교 >= 1 && あなた竿あり && ABL:캐릭터番号:봉사정신 >= 1
	PRINTL [12] - 입으로 봉사시킨다
SIF !키스불가 && ABL:캐릭터番号:기교 >= 2 && ABL:캐릭터番号:봉사정신 >= 1 && あなた竿あり
	PRINTL [13] - 가슴으로 봉사시킨다
SIF !前성교불가 && EXP:캐릭터番号:Ｖ경험 >= EXPLV:3 && ABL:캐릭터番号:봉사정신 >= 3 && あなた竿あり
	PRINTL [14] - 기승위로 봉사시킨다
[SKIPEND]

IF !純愛플래그
	SETCOLOR C_RED
	PRINTL 
	PRINTL -조교계-
	
	PRINTL [20] - 자위시킨다
	PRINTL [21] - 엉덩이를 때린다
	[SKIPSTART]
	SIF MARK:캐릭터番号:굴복각인 >= 2 && ABL:캐릭터番号:노출증 >= 2 && あなた竿あり
		PRINTL [22] - 흩뿌리기
	IF ITEM:16 == 1 || NOITEM == 1
		SIF MARK:캐릭터番号:굴복각인 >= 3 && ABL:캐릭터番号:마조끼 >= 3 && ABL:캐릭터番号:노출증 >= 3
			PRINTL [23] - 밧줄로 묶기
	ENDIF
	SIF 굴복됨취득 && CFLAG:캐릭터番号:호감도 >= 10000 && ABL:캐릭터番号:마조끼 >= 5 && TALENT:캐릭터番号:패배버릇
		PRINTL [24] - 정신을 때려일으킨다
	[SKIPEND]
	RESETCOLOR
ELSE
	SETCOLOR C_PINK
	PRINTL 
	PRINTL -순애계-
	
	PRINTL [30] - 안는다
	[SKIPSTART]
	PRINTL [31] - 애무한다
	SIF !키스불가
		PRINTL [32] - 키스한다
	SIF !前성교불가 && あなた竿あり
		PRINTL [33] - 섹스한다
	SIF 친애취득 && TALENT:캐릭터番号:성별 & 2
		PRINTL [34] - 뽑아준다
	[SKIPEND]
	RESETCOLOR
ENDIF

BINPUT
조교メニュー = RESULT
CUSTOMDRAWLINE —
PRINTL 
;ここから、個別の조교処理
IF 조교メニュー == 0
	;[0]-マッサージ（健全なマッサージ）　常に可能　※굴복각인3＋욕망3以上でHおねだりするようになる
	PRINTFORMW 필드에 나가기 전에 스트레칭을 하자고 하니, %CALLNAME:캐릭터番号%는 당황하면서도 매트에 누웠다.
	PRINTFORMW 꽉, 꽉, 꽉...
	PRINTFORMW 아픔이나 이상함은 없는지 반응을 보며, 정성스럽게 몸을 풀어준다.
	PRINTFORML 탄탄한 등과, 여성스러운 부드러운 어깨를 주물러 풀고, 부드러운 팔과 잘 단련된 허벅지까지 손을 뻗으니,
	PRINTFORMW 처음엔 당황하던 %CALLNAME:캐릭터番号%도, 기분 좋은 듯한 신음소리를 내며 마사지에 몸을 맡겼다...
	PRINTFORMW 꽉, 꽉, 꽉...
	
	IF MARK:캐릭터番号:굴복각인 >= 3 && ABL:캐릭터番号:욕망 >= 3
		PRINTFORMW 마사지가 끝났다고 하니, 완전히 몸을 맡기고 있던 %CALLNAME:캐릭터番号%가 놀란 듯 이쪽을 본다.
		PRINTFORMW 아직 마사지가 부족한 곳이 있냐고 묻자, 볼을 붉힌 %CALLNAME:캐릭터番号%는
		IF (前성교불가 && 後성교불가)
			PRINTFORML 머뭇거리며 유니폼의 가슴을 옆으로 치우고, 가슴 마사지를 부탁했다.
			CALL SOURCE_CALC_액체(캐릭터番号, MASTER, 10000)
			PRINTFORML ...좋다. 솔직하게 부탁한 %CALLNAME:캐릭터番号%에게 보상으로, 그 가슴을 움켜쥐고 주물러 풀어주었다.
			
			;가슴조교
			CALL EROTIC_MOVE("バスト조교",캐릭터番号,MASTER,300)
			
			PRINTL 
			PRINTFORML 마사지가 끝났다고 하니, 성감을 자극받아 상기된 %CALLNAME:캐릭터番号%는 일어나서 "고맙습니다!"라고 감사의 말을 전했다.
			
		ELSEIF EXP:캐릭터番号:Ａ경험 > EXP:캐릭터番号:Ｖ경험
			PRINTFORMW 등을 돌리며 엉덩이를 들어올리고, 유니폼을 옆으로 치우고 애널 마사지를 부탁했다.
			CALL SOURCE_CALC_액체(캐릭터番号, MASTER, 10000)
			PRINTFORML ...좋다. 솔직하게 부탁한 %CALLNAME:캐릭터番号%에게 보상으로, 애널을 휘저어주기로 했다.
			
			;Ａ조교
			CALL EROTIC_MOVE("後穴凌辱",캐릭터番号,MASTER,300)
			IF あなた竿あり
				;竿あると사정したものとして処理を추가
				TCVAR:MASTER:사정쾌락강도 = 1500 * (300 / 100)
				TCVAR:MASTER:사정대상캐릭터_1 = 캐릭터番号
				TCVAR:MASTER:사정부위플래그 = 2
				CALL SOURCE_CALC_쾌Ｃ(MASTER, 캐릭터番号, (150 * 300) / 100)
			ENDIF
			PRINTL 
			PRINTFORML 마사지가 끝났다고 하니, 성감이 자극되어 상기된 %CALLNAME:캐릭터番号%는 일어나서 "고맙습니다!"라고 감사의 말을 전했다.
			
		ELSE
			PRINTFORMW 매트에 똑바로 누워 이쪽을 바라보며, 유니폼을 옆으로 치우고 질 마사지를 부탁했다.
			CALL SOURCE_CALC_액체(캐릭터番号, MASTER, 10000)
			PRINTFORML ...좋다. 솔직하게 부탁한 %CALLNAME:캐릭터番号%에게 보상으로, 앞구멍을 휘저어주기로 했다.
			
			;Ｖ조교
			CALL EROTIC_MOVE("前穴凌辱",캐릭터番号,MASTER,300)
			IF あなた竿あり
				;竿あると사정したものとして処理を추가
				TCVAR:MASTER:사정쾌락강도 = 1500 * (300 / 100)
				TCVAR:MASTER:사정대상캐릭터_1 = 캐릭터番号
				TCVAR:MASTER:사정부위플래그 = 1
				CALL SOURCE_CALC_쾌Ｃ(MASTER, 캐릭터番号, (150 * 300) / 100)
			ENDIF
			PRINTL 
			PRINTFORML 마사지가 끝났다고 하니, 성감이 자극되어 상기된 %CALLNAME:캐릭터番号%는 일어나서 "고맙습니다!"라고 감사의 말을 전했다。
		ENDIF
	ELSE
		PRINTFORMW 마사지가 끝났다고 하니, %CALLNAME:캐릭터番号%는 뭔가 개운치 않은 표정을 지으면서도 일어나서 "...고맙습니다"라고 감사의 말을 전했다。
		;이벤트用関数処理を利用した호감도上昇処理が入るよ。固定なので조교初期段階は下手にエロ行為するよりこれやったほうがいいかもね。
		CALL EVENT_FAVUP(캐릭터番号,50)
	ENDIF

ELSEIF 조교メニュー == 1
	;[1]-가슴をマッサージ　常に可能
	PRINTFORMW 필드에 나가기 전에 스트레칭을 하자고 하니, %CALLNAME:캐릭터番号%는 당황하면서도 매트에 누웠다。
	PRINTFORMW 꽉, 꽉, 꽉...
	PRINTFORMW 몸을 풀어주는 마사지에 더해 성감을 자극하는 지압도 더해, 암컷에 어울리는 몸 만들기의 준비를 해간다.
	CALL SOURCE_CALC_액체(캐릭터番号, MASTER, 3000)
	PRINTFORMW 슬슬 괜찮겠지. %CALLNAME:캐릭터番号%에게 지시해서 매트에 똑바로 눕게 하자, 움직이지 못하게 올라탔다.
	PRINTFORMW 갑작스러운 일에 놀란 %CALLNAME:캐릭터番号%의 유니폼을 옆으로 치우고, 탱글탱글 튀어나온 가슴을 주물러 풀어주었다.
	PRINTFORMW 말랑말랑, 말랑말랑...
	
	;Ｂ조교
	CALL EROTIC_MOVE("バスト조교",캐릭터番号,MASTER,200)
	PRINTL 
	
	IF MARK:캐릭터番号:굴복각인 >= 2 || 純愛플래그 || 굴복됨취득
		PRINTFORML 마사지가 끝났다고 하니, 성감이 자극되어 상기된 %CALLNAME:캐릭터番号%는 일어나서 "고맙습니다"라고 감사의 말을 전했다。
	ELSE
		PRINTFORML 마사지가 끝났다고 하니, 괴로워하며 버티던 %CALLNAME:캐릭터番号%는 천천히 일어나서, 불만스러운 듯 "...고맙습니다"라고 감사의 말을 전했다.
	ENDIF

ELSEIF 조교メニュー == 2
	;[2]-前穴をマッサージ　前성교불가だと질内사정しない
	PRINTFORMW 필드에 나가기 전에 스트레칭을 하자고 하니, %CALLNAME:캐릭터番号%는 당황하면서도 매트에 누웠다。
	PRINTFORMW 꽉, 꽉, 꽉...
	PRINTFORMW 몸을 풀어주는 마사지에 더해 성감을 자극하는 지압도 더해, 암컷에 어울리는 몸 만들기의 준비를 해간다。
	CALL SOURCE_CALC_액체(캐릭터番号, MASTER, 3000)
	PRINTFORMW 슬슬 괜찮겠지. %CALLNAME:캐릭터番号%에게 지시해서 매트에 똑바로 눕게 하자, 움직이지 못하게 올라탔다。
	PRINTFORMW 갑작스러운 일에 놀란 %CALLNAME:캐릭터番号%의 유니폼을 재빨리 옆으로 치우고, 은밀한 부위 마사지를 했다。
	PRINTFORMW 촉촉, 촉촉...
	
	;Ｖ조교
	CALL EROTIC_MOVE("前穴凌辱",캐릭터番号,MASTER,300)
	IF あなた竿あり && (굴복됨취득 || 純愛플래그)
		;竿ある＋純愛モード 또는 굴복됨ならペニス入れて사정したものとして処理を추가
		TCVAR:MASTER:사정쾌락강도 = 1500 * (300 / 100)
		TCVAR:MASTER:사정대상캐릭터_1 = 캐릭터番号
		TCVAR:MASTER:사정부위플래그 = 1
		CALL SOURCE_CALC_쾌Ｃ(MASTER, 캐릭터番号, (150 * 300) / 100)
	ENDIF
	PRINTL 
	
	IF MARK:캐릭터番号:굴복각인 >= 2 || 純愛플래그 || 굴복됨취득
		PRINTFORML 마사지가 끝났다고 하니, 성감이 자극되어 상기된%CALLNAME:캐릭터番号%는 일어나서 "고맙습니다"라고 감사의 말을 전했다.
	ELSE
		PRINTFORML 마사지가 끝났다고 하니, 괴로워하며 버티던%CALLNAME:캐릭터番号%는 천천히 일어나서, 불만스러운 듯 "...고맙습니다"라고 감사의 말을 전했다.
	ENDIF

ELSEIF 조교メニュー == 3
	;[3]-후구멍 마사지　후성교불가면 애널사정하지 않음
	PRINTFORMW 필드에 나가기 전에 스트레칭을 하자고 하니, %CALLNAME:캐릭터番号%는 당황하면서도 매트에 누웠다.
	PRINTFORMW 꽉, 꽉, 꽉...
	PRINTFORMW 몸을 풀어주는 마사지에 더해 성감을 자극하는 지압도 더해, 암컷에 어울리는 몸 만들기의 준비를 해간다.
	CALL SOURCE_CALC_액체(캐릭터番号, MASTER, 3000)
	PRINTFORMW 슬슬 괜찮겠지. %CALLNAME:캐릭터番号%에게 지시해서 매트에 엎드리게 하자, 그 머리를 눌러 움직이지 못하게 했다.
	PRINTFORMW 갑작스러운 일에 놀란 %CALLNAME:캐릭터番号%의 유니폼을 재빨리 옆으로 치우고, 애널 마사지를 했다.
	PRINTFORMW 미끈, 미끈...
	
	;Ａ조교
	CALL EROTIC_MOVE("後穴凌辱",캐릭터番号,MASTER,300)
	IF あなた竿あり && (굴복됨취득 || 純愛플래그)
		;竿ある＋純愛モード 또는 굴복됨ならペニス入れて사정したものとして処理を추가
		TCVAR:MASTER:사정쾌락강도 = 1500 * (300 / 100)
		TCVAR:MASTER:사정대상캐릭터_1 = 캐릭터番号
		TCVAR:MASTER:사정부위플래그 = 2
		CALL SOURCE_CALC_쾌Ｃ(MASTER, 캐릭터番号, (150 * 300) / 100)
	ENDIF
	PRINTL 
	
	IF MARK:캐릭터番号:굴복각인 >= 2 || 純愛플래그 || 굴복됨취득
		PRINTFORML 마사지가 끝났다고 하니, 성감이 자극되어 상기된 %CALLNAME:캐릭터番号%는 일어나서 "고맙습니다"라고 감사의 말을 전했다。
	ELSE
		PRINTFORML 마사지가 끝났다고 하니, 괴로워하며 버티던 %CALLNAME:캐릭터番号%는 천천히 일어나서, 불만스러운 듯 "...고맙습니다"라고 감사의 말을 전했다.
	ENDIF

ELSEIF 조교メニュー == 4
	;[4]-양쪽 구멍 마사지　앞+구멍 모두 가능 & 굴복됨 오픈　純愛플래그 있으면 불가
	PRINTFORMW 필드에 나가기 전에 스트레칭을 하자고 하니, %CALLNAME:캐릭터番号%는 꿀꺽 목을 삼키면서도 매트에 누웠다。
	PRINTFORMW 꽉, 꽉, 꽉...
	PRINTFORMW 이제 마사지도 답답한 듯, 젖은 눈길로 이쪽을 힐끔힐끔 바라보며 은밀한 곳을 만지기 쉽게 몸을 움직이고 있다。
	CALL SOURCE_CALC_액체(캐릭터番号, MASTER, 6000)
	PRINTFORML 어쩔 수 없는 녀석이다。 %CALLNAME:캐릭터番号%에게 지시해서 매트에 엎드리게 하고, 양쪽 구멍으로 잘 부탁하면 넣어주겠다고 하니,
	PRINTFORMW %CALLNAME:캐릭터番号%는 스스로 유니폼을 옆으로 치우고, 질과 애널 양쪽을 활짝 벌려서 아양을 떨듯 암컷 구멍 안쪽을 보여주었다。
	PRINTFORMW 즙이 흐르는 젖은 구멍이, %CALLNAME:MASTER%의 육봉을 채워주길 바라듯 울고 있다...
	
	;両穴조교
	CALL EROTIC_MOVE("両穴凌辱",캐릭터番号,MASTER,300)
	IF あなた竿あり
		;竿あるならペニス入れて사정したものとして処理を추가、사정場所は경험が多いほうに
		TCVAR:MASTER:사정쾌락강도 = 1500 * (300 / 100)
		TCVAR:MASTER:사정대상캐릭터_1 = 캐릭터番号
		IF EXP:캐릭터番号:Ａ경험 > EXP:캐릭터番号:Ｖ경험
			TCVAR:MASTER:사정부위플래그 = 2
		ELSE
			TCVAR:MASTER:사정부위플래그 = 1
		ENDIF
		CALL SOURCE_CALC_쾌Ｃ(MASTER, 캐릭터番号, (150 * 300) / 100)
	ENDIF
	PRINTL 
	
		PRINTFORML 양쪽 구멍을 정성스럽게 마사지 받아 숨이 끊어질 듯한 %CALLNAME:캐릭터番号%는, 쓰러져 있었다.
		PRINTFORML 짜내듯이 내는 목소리로 %CALLNAME:MASTER%에게 "고마워...요..."라고 감사했다.

ELSEIF 조교メニュー == 10
	;[10]-손가락에 봉사하게 하기　항상 가능
	;함락계에서 분기, 또 함락 전에도 봉사정신과 M감각으로 약간 변화함
	IF 純愛플래그
		;순애 패턴
		PRINTFORMW 시합에 나가고 싶다면, 태도로 보여줘. 라고 말하며 손을 내밀자, %CALLNAME:캐릭터番号%는 다가와 손을 잡았다.
		PRINTFORMW 사랑스럽게 %CALLNAME:MASTER%의 손에 볼을 비비고, 조금 장난스럽게 쪽 하고 손에 입을 맞췄다.
		PRINTFORMW 그대로 입을 벌려, %CALLNAME:MASTER%의 손가락을 받아들인다・・・
		
		;指チュパはＭ単発で処理。
		CALL EROTIC_MOVE("Ｍ単発",캐릭터番号,MASTER,250)
		PRINTL 
		
		PRINTFORMW 쪽, 쪽... 쪽...
		
		PRINTFORMW 하나하나 정성스럽게 손가락을 핥은 %CALLNAME:캐릭터番号%는, 마지막엔 약지에 아쉬운 듯 쪽 하고 입을 맞추고 손을 놓았다.
		PRINTFORMW 이쪽을 올려다보는 표정은, 조금 부끄러워하면서도, 굳은 결의가 느껴지는 곧은 눈이었다.
		PRINTFORML 좋아, 경기장으로 가자고 하니, %CALLNAME:캐릭터番号%는 활기찬 목소리로 "네! 고맙습니다!"라고 대답했다.
		
	ELSEIF 굴복됨취득 && ABL:캐릭터番号:봉사정신 >= 4
		;超굴복パターン（봉사정신も高い状態）
		PRINTFORMW 시합에 나가기 전에 해야 할 일이 있지? 라고만 말하고, 푹 하고 의자에 앉았다.
		PRINTFORMW 그 의도를 알아챈 %CALLNAME:캐릭터番号%는 달려와서, 주인님의 성냄새가 나는 사타구니에 얼굴을 가까이 했다.
		PRINTFORMW ...아니, 오늘의 "지도"는 그게 아니야.
		PRINTFORMW 그렇게 말하듯이 가까이 온 얼굴을 손으로 막는다. %CALLNAME:캐릭터番号%는 예상치 못한 반응에 당황하는 듯했다.
		PRINTFORMW 조금 힌트를 주겠다. 라고만 말하고, 신발을 가볍게 흔들어준다.
		PRINTFORMW ...그 의도를 생각하고, 그리고 답에 도달한 듯한 %CALLNAME:캐릭터番号%의 얼굴은, 노예 그 자체의 표정으로 물들어 있었다.
		PRINTFORMW 주인의 신발을 공손하게 벗기자, 약간 젖어 있는 양말이 모습을 드러냈다.
		PRINTFORMW 손을 대려던 그 동작이 잠시 멈춘다. 잠깐 멈춘 뒤, 주인의 의도를 최대한 받아들인 %CALLNAME:캐릭터番号%는, 손 대신 얼굴을 가까이 했다.
		PRINTFORMW 냄새나는 양말을, 손을 쓰지 않고 입과 혀로 평평하게 벗겨냈다.
		PRINTFORMW ...예상 이상으로 "지도"의 성과가 잘 새겨져 있군.
		PRINTFORMW 노예의 재주에 감탄하며 끄덕이자, 위를 올려다보며 %CALLNAME:캐릭터番号%는 환하게 미소를 지었다.
		PRINTFORMW 잘 훈련된 노예의, 주인에게 아양 떠는 암컷의 미소가 거기에 있었다.
		
		;指チュパはＭ単発で処理。
		CALL EROTIC_MOVE("Ｍ単発",캐릭터番号,MASTER,500)
		PRINTL 
		
		PRINTFORMW 촉촉... 쩝쩝... 쩝...

		PRINTFORMW 발가락 사이까지 정성스럽게 핥아 올리는 %CALLNAME:캐릭터番号%는, 주인에 대한 봉사정신의 극치에 이르렀다.
		PRINTFORMW 훌륭한 봉사였다.
		PRINTFORML %CALLNAME:캐릭터番号%의 머리를 쓰다듬으며 그렇게 칭찬해주자,
		PRINTFORML "고맙습니다"라고 %CALLNAME:캐릭터番号%는 경건한 말로 주인에 대한 충성과 사랑을 보였다.
		
	ELSEIF 굴복됨취득
		;굴복됨みパターン
		PRINTFORMW 시합에 나가기 전에 해야 할 일이 있지? 라고 말하며 손가락을 가리키자, %CALLNAME:캐릭터番号%는 요구받은 것을 제대로 이해하고 있었다。
		PRINTFORMW 스스로 나아가 바닥에 무릎을 꿇고, 얼굴을 손가락 높이에 맞춘다. 그 모습은 마치 이제 막 기도를 올릴 신도 같았다.
		PRINTFORMW %CALLNAME:캐릭터番号%는 두려워하는 눈빛으로 주인의 손가락을 바라보며, 공손하게 입을 벌려 천천히 혀를 감아 입 안으로 이끌었다.
		PRINTFORMW 쩝쩝... 쩝... 쪽...
		PRINTFORMW 경기장의 소란스러운 소리가 멀리서 들리는 가운데, %CALLNAME:MASTER%의 손가락에 온 힘을 다해 봉사하고 있다...
		
		;손가락 쪽쪽은 M단발로 처리.
		CALL EROTIC_MOVE("Ｍ単発",캐릭터番号,MASTER,250)
		PRINTL 
		
		PRINTFORMW 쪽쪽, 쪽쪽... 응쩝...
		
		PRINTFORMW 한결같이 주인의 손가락에 입으로 봉사하는 %CALLNAME:캐릭터番号%의 입 안은 침으로 가득 차, 봉사하게 한 손가락은 불을 만큼 젖어 있다.
		PRINTFORMW 손가락을 살짝 움직이자, 그것만으로도 의도를 알아챈 %CALLNAME:캐릭터番号%는 손가락을 빼기 쉽게 크게 입을 벌렸다.
		PRINTFORMW ...훌륭하다.
		PRINTFORML "지도"의 성과가 새겨진 %CALLNAME:캐릭터番号%는 주인의 말에 몸을 떨며 기쁨이 섞인 목소리로 "고맙습니다!"라고 대답했다.
		
	ELSEIF ABL:캐릭터番号:봉사정신 >= 1 || ABL:캐릭터番号:Ｍ감각 >= 1
		;少し慣れてるパターン
		PRINTFORMW 시합에 나가고 싶다면, 태도로 보여줘. 라고 말하며 손가락을 가리키자, %CALLNAME:캐릭터番号%는 요구받은 것을 어렴풋이 알아챈 듯했다.
		PRINTFORMW 스스로 나아가 바닥에 무릎을 꿇고 얼굴을 손가락 높이에 맞춘다.
		PRINTFORMW %CALLNAME:MASTER%의 손가락을 눈앞에 두고 약간 압도된 듯하지만, 마음을 다잡고 입을 벌려 조심스럽게 혀를 손가락에 댔다.
		PRINTFORMW 핥짝... 핥짝. 아기 고양이가 우유를 핥는 듯 서툰 봉사를 했다.
		PRINTFORMW ...이런 봉사도 귀엽긴 하지만, "지도"가 필요하군.
		PRINTFORMW %CALLNAME:캐릭터番号%에게, 더 입속 깊이 손가락을 물도록 지시한다.
		PRINTFORMW 그 명령에, 끄덕인 %CALLNAME:캐릭터番号%는, 천천히 손가락을 입 안으로 삼켜갔다...
		
		;指チュパはＭ単発で処理。
		CALL EROTIC_MOVE("Ｍ単発",캐릭터番号,MASTER,200)
		PRINTL 
		
		PRINTFORMW 쪽, 쩝, 쪽쪽, 쪽...

		PRINTFORMW 아직 동작은 서툴지만, 온 힘을 다해 혀를 감아 봉사하는 %CALLNAME:캐릭터番号%.
		PRINTFORMW 점점 "지도"의 성과가 나타나고 있군.
		PRINTFORMW 봉사를 끝내라고 하니, 빼기 쉽게 크게 벌린 입에서 손가락을 뺀다.
		PRINTFORML 침으로 번들번들 빛나는 손가락을 멍한 표정으로 바라보는 %CALLNAME:캐릭터番号%는, "고맙습니다"라고 감사를 말했다.

	ELSE
		PRINTFORMW 시합에 나가고 싶다면, 태도로 보여줘. 라고 말하며 손가락을 가리키자, %CALLNAME:캐릭터番号%는 당황하며 망설이는 듯했다.
		PRINTFORMW ...참, 먼저 봉사의 정신을 가르치는 것부터 시작해야겠군.
		PRINTFORMW 바닥에 무릎을 꿇게 하고 얼굴을 손가락 높이에 맞춘다. 남의 손가락이 눈앞에 있는 상황에 익숙하지 않은 %CALLNAME:캐릭터番号%는 저도 모르게 얼굴을 찡그렸다.
		PRINTFORMW 외면하려는 %CALLNAME:캐릭터番号%에게 정면을 보라고 꾸짖고, 입을 벌리라고 지시했다.
		PRINTFORMW %CALLNAME:캐릭터番号%가 조심스럽게 벌린 입에 손가락을 밀어 넣는다.
		PRINTFORMW 갑작스러운 일에 저도 모르게 구역질하지만, 신경 쓰지 않고 입 안을 휘저었다.
		PRINTFORMW 얼굴을 피하거나 손가락을 물지 않도록 주의시키면서, %CALLNAME:캐릭터番号%의 입을 유린했다.
		
		;指チュパはＭ単発で処理。
		CALL EROTIC_MOVE("Ｍ単発",캐릭터番号,MASTER,100)
		PRINTL 
		
		PRINTFORMW 쪽쪽, 쪽쪽・・・
		
		PRINTFORMW 처음에는 당황해서 입안을 휘저어지는 대로 당하고 있던 %CALLNAME:캐릭터番号%였지만, 점차 익숙해졌는지 어색하게나마 혀를 얽어 봉사해왔다.
		PRINTFORMW 손가락을 빼자, 침으로 실이 이어진 손가락이 떨어져 나가는 모습을 시선으로 쫓고 있었다.
		PRINTFORMW 꽤 소질이 좋다.
		PRINTFORML 그렇게 칭찬해주자, %CALLNAME:캐릭터番号%는 불쾌한 듯이 "・・・감사합니다"라고 대답했다.
	ENDIF

ELSEIF 조교メニュー == 11
	;[11]-딥키스させる　키스미경험불가＆M감각1以上&봉사정신1
	;陥落系で분기、また陥落前も봉사정신とM감각で若干変化する
	IF 친애취득
		;純愛パターン（친애もちの場合）
		PRINTFORMW 끌어안은 %CALLNAME:캐릭터番号%는 촉촉한 눈빛으로 %CALLNAME:MASTER%를 사랑스럽게 바라보고 있었다。
		PRINTFORMW 이제부터 격렬한 시합이 펼쳐질 텐데, %CALLNAME:MASTER%의 품 안에 얌전히 안겨 있는 모습은 정말 귀엽다。
		PRINTFORMW 운동선수답게 화장기 없는 산뜻한 얼굴이지만, 건강한 입술은 자연스럽게 수컷을 유혹하고, 암컷임을 더욱 의식하게 만든다。
		PRINTFORMW ・・・쪽
		PRINTFORMW 어느새 서로가 먼저랄 것도 없이 입을 맞추고 있었다. %CALLNAME:캐릭터番号%가 아까 마셨던 스포츠 음료의 희미한 소금기가 입안에 퍼진다。
		
		;디ープ키스
		CALL EROTIC_MOVE("디ープ키스",캐릭터番号,MASTER,300)
		PRINTL 
		
		PRINTFORMW 쪽・・・쪽・・・・・・푸하
		
		PRINTFORMW 얼마나 오랫동안 키스하고 있었을까. 숨이 막힐 정도로 서로를 갈구하는 열정적인 키스였다。
		PRINTFORMW 더 이상 키스를 계속하면 시합을 놓칠지도 모른다. 이어지는 건 시합 후에 천천히 하자。
		PRINTFORML 좋아, 경기장으로 가자고 전하자, %CALLNAME:캐릭터番号%는 쾌활한 목소리로 "네! 감사합니다!"라고 대답했다。
		
	ELSEIF 純愛플래그
		;순애 패턴. 정말로 조교 효과가 낮아진다. 초기 상태보다도 낮다. 하지만 사실 호감도에 보너스가 들어간다
		PRINTFORMW 끌어안은 %CALLNAME:캐릭터番号%에게 딥키스를 명령하자 볼이 붉게 물들었다。
		PRINTFORMW %CALLNAME:MASTER%의 명령이라지만, 자신이 먼저 입을 맞추는 것은 부끄러운 모양이다。
		PRINTFORMW 하지만 하지 않으면 시합에 내보낼 수 없다고 가볍게 변명을 준비해주기만 해도, %CALLNAME:캐릭터番号%는 결심한 듯 눈을 감고 입을 가까이 했다。
		PRINTFORMW ・・・・・・・・・・・・쪽
		
		;딥키스
		CALL EROTIC_MOVE("딥키스",캐릭터番号,MASTER,10)
		PRINTL 
		
		PRINTFORMW 작은 새가 쪼아먹는 듯한 키스이지만、자신의 의지로 입을 맞췄다. 그런 %CALLNAME:캐릭터番号%의 머리를 쓰다듬으며 칭찬해준다。
		CALL EVENT_FAVUP(캐릭터番号,100)
		PRINTFORMW 기분 좋게 쓰다듬어지고 있는 %CALLNAME:캐릭터番号%는、처음에 비하면 훨씬 여자아이 같은 인상이 강하게 느껴진다。
		PRINTFORMW 하지만 그런 모습을 자신에게만 보여주게 된 %CALLNAME:캐릭터番号%의 변화는 바람직한 것이다。
		PRINTFORML 좋아、경기장으로 가자고 전하자、%CALLNAME:캐릭터番号%는 쾌활한 목소리로 "네! 감사합니다!"라고 대답했다。
		
	ELSEIF 굴복됨취득
		;굴복됨 패턴。굴복 계열이라면 키스는 그렇게 조교로서 효과가 높지 않다
		PRINTFORMW 시합에 나가고 싶으면 키스를 하라고 전하자、%CALLNAME:캐릭터番号%는 겁먹은 듯한 표정을 잠시 지었다。
		PRINTFORMW 하지만 키스하지 않으면 시합에 나갈 수 없다는 조건이 붙으니、거역할 수 없었다。
		PRINTFORMW %CALLNAME:캐릭터番号%는 결심하고 %CALLNAME:MASTER%에게 다가가 눈을 감고 키스를 조르는 듯이 기다리고 있다。
		PRINTFORMW ・・・뭔가 착각하고 있군。
		PRINTFORMW 키스 기다리는 게 아니야、네가 먼저 하는 거야. 남자를 만족시키는 야한 딥키스를。
		PRINTFORMW 그렇게 말하자、%CALLNAME:캐릭터番号%는 망설이면서도 결심하고 입술을 포갰다。
		
		;딥키스
		CALL EROTIC_MOVE("딥키스",캐릭터番号,MASTER,50)
		PRINTL 
		
		PRINTFORMW 눈을 감으면서도, 열심히 혀를 움직여 입으로 봉사하는 %CALLNAME:캐릭터番号%.
		PRINTFORMW 그 필사적인 모습은 평가해줄 만하지만, 답답함이 있군.
		PRINTFORMW 이제 됐다.
		PRINTFORMW 그런 마음을 담아 %CALLNAME:캐릭터番号%의 머리를 움켜쥐고, 듬뿍 혀를 얽어 침을 교환했다.
		
		;디ープ키스
		CALL EROTIC_MOVE("딥키스",캐릭터番号,MASTER,200)
		PRINTL 
		
		PRINTFORMW 쥽・・・쥽・・・응쥴・・・푸하
		PRINTFORMW 입안을 범해져 산소가 부족해질 것 같은 %CALLNAME:캐릭터番号%를 풀어주자, 약간 어지러운 듯 멍해져 있다.
		PRINTFORML 짜내듯한 목소리로 %CALLNAME:MASTER%에게 "감사합니다・・・"라고 감사했다.
		
	ELSEIF ABL:캐릭터番号:봉사정신 >= 2 || ABL:캐릭터番号:Ｍ감각 >= 2
		;少し慣れてるパターン
		PRINTFORMW 시합에 나가고 싶으면 키스를 해보겠니? 그렇게 전하자 %CALLNAME:캐릭터番号%는 무심코 입을 가리며 이쪽을 바라봤다。
		PRINTFORMW %CALLNAME:캐릭터番号%는 아직도 키스를 소중한 것으로 생각하는 듯하다。
		PRINTFORMW ・・・그래서야말로, 그것을 바치는 "지도"의 의미가 있다。
		PRINTFORMW 쾅!
		PRINTFORMW 락커에 밀쳐져 도망칠 수 없는 %CALLNAME:캐릭터番号%는 체념한 듯 눈을 내리깔고 입에 대한 능욕을 받아들였다。
		
		;디ープ키스
		CALL EROTIC_MOVE("딥키스",캐릭터番号,MASTER,200)
		PRINTL 
		
		PRINTFORMW 쥽・・・쥽・・・응쥴・・・푸하
		PRINTFORMW 혀를 받아들여 능욕당한 채 입안을 범해진 %CALLNAME:캐릭터番号%였지만, 겨우 숨을 쉴 수 있게 되었다。
		PRINTFORMW ・・・얼굴이 붉게 달아오르고 숨을 헐떡이는 건, 단순히 입이 막혀 있었기 때문만은 아닌 것 같다。
		PRINTFORML 다음엔 더 적극적으로 혀를 얽도록 지도해서 시합에 내보내겠다고 전하자,
		PRINTFORMW %CALLNAME:캐릭터番号%는 고개를 숙이면서도 "・・・감사합니다"라고 감사를 전했다。
		
	ELSE
		;조교 부족 상태라면 효과가 그다지 높지 않고, 호감도 감소 효과가 붙는다
		PRINTFORMW 시합에 나가고 싶으면 키스를 해보겠니? 그렇게 전하자 %CALLNAME:캐릭터番号%는 와나와나 어깨를 떨며 이쪽을 바라봤다。
		PRINTFORMW 아직도 반항적인 %CALLNAME:캐릭터番号%는 쉽게 "지도"에 따르지 않는 듯하다。
		PRINTFORMW ・・・좋은 근성이군. 그래서야말로 "지도"할 맛이 있지。
		PRINTFORMW 쾅!
		PRINTFORMW 락커에 밀쳐져 도망칠 수 없는 %CALLNAME:캐릭터番号%의 얼굴을 억지로 잡고, 그 입술을 능욕했다。
		
		;딥키스
		CALL EROTIC_MOVE("딥키스",캐릭터番号,MASTER,100)
		PRINTL 
		
		PRINTFORMW 쥽・・・쥽・・・응쥴・・・악!
		PRINTFORMW %CALLNAME:캐릭터番号%의 입안을 휘젓던 혀에 통증이 달린다.
		PRINTFORMW 무심코 입을 떼자, 저항에 성공한 %CALLNAME:캐릭터番号%는 숨을 헐떡이며 씩 웃었다.
		CALL EVENT_FAVUP(캐릭터番号,-100)
		PRINTFORMW ・・・좋은 근성이군. 그래서야말로 "지도"할 맛이 있지.
		PRINTFORML 그렇게 칭찬해주자, %CALLNAME:캐릭터番号%는 불쾌한 듯이 "・・・감사합니다"라고 대답했다.
	ENDIF

ELSEIF 조교メニュー == 20
	;[20]-자위させる　常に可能　：자위
	;純愛モードでは出てこなくなる項目
	;자위경험、조교자위경험を稼げるので노출증や자위중독を鍛えられる。
	;조교자위경험が入らないので、こっちでEXP:조교대상:조교자위경험を加算する処理を入れておく。（これ노출증上げてくのに1以上必要なの）
	IF 굴복됨취득 && ABL:캐릭터番号:노출증 >= 4
		;超굴복됨みパターン（노출증も高い状態）
		PRINTFORMW %CALLNAME:캐릭터番号%, 자위해라.
		PRINTFORMW 그 한마디만으로, 이미 조교당한 %CALLNAME:캐릭터番号%는 기쁘게 유니폼을 걷어올리고 은밀한 곳을 만지기 시작했다.
		PRINTFORMW 이게 평소에는 열심히 훈련하고, 시합에서는 많은 팬들이 응원하는 "ARG☆S" 선수의 모습이라니.
		PRINTFORMW 그렇게 말로 괴롭혀줬지만, 그 모욕조차도 지금의 %CALLNAME:캐릭터番号%에게는 쾌감을 돋우는 양념에 불과했다.
		
		;자위
		EXP:캐릭터番号:조교자위경험 += 1
		CALL EROTIC_MOVE("자위",캐릭터番号,MASTER,300)
		PRINTL 
		
		IF ABL:캐릭터番号:자위중독 >= 1
			;자위중독용
			PRINTFORMW 자위중독인 %CALLNAME:캐릭터番号%는, 끊임없이 자위를 계속하고 있다・・・
			PRINTFORMW 적당한 곳에서 멈춰주지 않으면, 시합 같은 건 잊고 끝없이 계속할지도 모른다.
			EXP:캐릭터番号:조교자위경험 += ABL:캐릭터番号:자위중독
			CALL EROTIC_MOVE("자위",캐릭터番号,MASTER,300*ABL:캐릭터番号:자위중독)
			PRINTL 
		ENDIF
		
		PRINTFORMW （・・・차라리 시합에서 그 모습을 전 우주에 공개해버리는 건 어떨까?）
		PRINTFORMW 그런 생각이 머리를 스쳤지만, 입 밖에 내는 건 참았다. 지금 쾌락에 지배당한 %CALLNAME:캐릭터番号%는 진짜로 해버릴지도 모른다.
		PRINTFORML 충분히 워밍업해서 숨이 찬 %CALLNAME:캐릭터番号%에게 슬슬 시간이라고 전하자,
		PRINTFORML 짜내듯한 목소리로 %CALLNAME:MASTER%에게 "감사합니다・・・"라고 감사했다.
		
	ELSEIF 굴복됨취득
		;굴복됨みパターン。純愛系の분기がそもそもないので、そんなに분기する感じではない。
		PRINTFORMW 자위를 명령하자, 더는 거역할 수 없는 %CALLNAME:캐릭터番号%는 느릿느릿 유니폼을 걷어올리고 자위를 시작했다.
		PRINTFORMW 명령받아서 어쩔 수 없다는 듯이 행동하고 있지만, 굴복해버린 마음에 이끌려 성감이 점점 높아진다.
		PRINTFORMW ・・・본인은 어디까지 이해하고 있을까.
		PRINTFORMW 자신의 손가락이, 스스로의 기분 좋은 곳을 적극적으로 찾아 괴롭히는 것이 "당연"이 되어버린 이 이상함을・・・
		
		;자위
		EXP:캐릭터番号:조교자위경험 += 1
		CALL EROTIC_MOVE("자위",캐릭터番号,MASTER,300)
		PRINTL 
		
		IF ABL:캐릭터番号:자위중독 >= 1
			;자위중독용
			PRINTFORMW 자위중독인 %CALLNAME:캐릭터番号%는, 끊임없이 자위를 계속하고 있다・・・
			PRINTFORMW 적당한 곳에서 멈춰주지 않으면, 시합 같은 건 잊고 끝없이 계속할지도 모른다.
			EXP:캐릭터番号:조교자위경험 += ABL:캐릭터番号:자위중독
			CALL EROTIC_MOVE("자위",캐릭터番号,MASTER,300*ABL:캐릭터番号:자위중독)
			PRINTL 
		ENDIF
		
		PRINTFORMW 몸이 따뜻해져서, 좋은 워밍업이 되었겠지.
		PRINTFORMW 자위를 잘 해낸 것을 칭찬해줬다.
		PRINTFORML 유니폼을 바로 입고 있던 %CALLNAME:캐릭터番号%는 뾰로통한 표정을 지으며,
		PRINTFORML "감사했습니다! 앞으로도 지도 부탁드립니다!"라고 분노 섞인 감사의 말을 전했다.
		
	ELSE
		;조교不足のパターン。굴복していない경우、あまりノリノリではない。
		PRINTFORMW 워밍업으로 자위를 명령하자, %CALLNAME:캐릭터番号%는 싫은 듯한 얼굴로 저항했다。
		PRINTFORMW ・・・그런 태도로 괜찮은 거냐? 워밍업을 게을리하는 선수는 시합에 내보낼 수 없다.
		PRINTFORMW 그렇게 말해주자, 이쪽이 진심으로 명령하고 있다는 걸 눈치챘는지, 마지못해 자위를 시작했다.
		PRINTFORMW 자신의 손가락으로, 유니폼 위에서 가슴과 은밀한 곳을 가볍게 쓰다듬는다.
		PRINTFORMW ・・・・・・
		PRINTFORMW ・・・이봐, 그 정도로 몸이 따뜻해질 리가 없잖아.
		PRINTFORML 진지하게 자위해라. 제대로 절정까지 할 수 있을 정도로 하지 않으면 다른 선수를 내보낸다. 라고 전하자,
		PRINTFORMW 드디어 체념했는지, 가슴과 은밀한 곳을 직접 만지며 자위를 시작했다.
		
		;자위
		EXP:캐릭터番号:조교자위경험 += 1
		CALL EROTIC_MOVE("자위",캐릭터番号,MASTER,200)
		PRINTL 
		
		IF ABL:캐릭터番号:자위중독 >= 1
			;자위중독용
			PRINTFORMW 자위중독인 %CALLNAME:캐릭터番号%는, 끊임없이 자위를 계속하고 있다・・・
			PRINTFORMW 적당한 곳에서 멈춰주지 않으면, 시합 같은 건 잊고 끝없이 계속할지도 모른다.
			EXP:캐릭터番号:조교자위경험 += ABL:캐릭터番号:자위중독
			CALL EROTIC_MOVE("자위",캐릭터番号,MASTER,300*ABL:캐릭터番号:자위중독)
			PRINTL 
		ENDIF
		
		PRINTFORMW ・・・시합 직전에 그렇게 열심히 자위하다니 %CALLNAME:캐릭터番号%는 변태구나.
		PRINTFORMW 그렇게 칭찬해주자, %CALLNAME:캐릭터番号%의 얼굴이 분노와 분함이 섞인 표정으로 변했다.
		PRINTFORML 유니폼을 재빨리 바로 입은 %CALLNAME:캐릭터番号%는,
		PRINTFORML "감사했습니다! 앞으로도 지도 부탁드립니다!"라고 분노 섞인 감사의 말을 전했다.
	ENDIF

ELSEIF 조교メニュー == 21
	;[21]-ケツを叩く　常に可能　：尻叩き　・・・호감도変動させて、굴복の珠をプレゼン
	;純愛モードでは出てこなくなる項目
	;호감도を下げる효과가 高い。純愛ルートに入らないようにするために調整できる
	;
	IF 굴복됨취득 && ABL:캐릭터番号:마조끼 >= 5
		;超々굴복됨みパターン（ドMになりました）
		PRINTFORMW %CALLNAME:캐릭터番号%에게 평소처럼 엉덩이를 때릴 자세를 취하게 한다。
		PRINTFORMW 무릎 위에 엎드려 하반신을 드러내고, 크게 엉덩이를 치켜든 %CALLNAME:캐릭터番号%。
		PRINTFORMW 그 유니폼의 사타구니는 한 번도 때리지 않았는데도 이미 젖기 시작했다。
		PRINTL 
		SETCOLOR C_RED
		PRINTFORMW ・・・맞을 기대만으로 젖고 있는 건가? 마조가。
		PRINTL 
		RESETCOLOR
		PRINTFORMW 모욕의 말만으로도, 움찔거리며 한심하게 움직이는 엉덩이를 계속 드러내고 있는 %CALLNAME:캐릭터番号%는 절정에 다다를 듯했다。
		
		;엉덩이 때리기
		CALL EROTIC_MOVE("尻叩き",캐릭터番号,MASTER,1000)
		PRINTL 
		
		SETCOLOR C_RED
		PRINTFORMW 찰싹!　찰싹!!　찰싹!!!
		PRINTFORMW 찰싹!　찰싹!!　찰싹!!!
		PRINTFORMW 찰싹!　찰싹!!　찰싹!!!
		RESETCOLOR
		
		PRINTFORMW 아름답게 단련된 %CALLNAME:캐릭터番号%의 둔부가, 새빨간 단풍처럼 물들어간다。
		PRINTFORMW 때리고 있는 이쪽 손이 저려올 정도가 되었지만, %CALLNAME:캐릭터番号%는 여전히,
		PRINTFORM 엉덩이를 맞을 때마다
		SETCOLOR C_PINK
		PRINTFORM "감사합니다ぅ♥！""감사합니다으♥！"
		RESETCOLOR
		PRINTFORMW 라고 감사의 말을 계속 전하고 있다。
		PRINTFORMW ・・・설마 이렇게까지 끝없는 마조성을 숨기고 있었을 줄이야。
		PRINTFORML 엉덩이 때리기를 끝내고 나서도, 아무 말 없이 주인님께 무릎 꿇고 "지도"에 대한 감사의 마음을 완벽하게 보인 뒤
		PRINTFORML 시합 필드로 향하는 %CALLNAME:캐릭터番号%의 뒷모습을 바라보며, 그 성장에 감탄할 수밖에 없었다。
		
		CALL EVENT_FAVUP(캐릭터番号,300)
		JUEL:캐릭터番号:굴복 += 2000+250*(ABL:캐릭터番号:마조끼)
		
	ELSEIF 굴복됨취득 && ABL:캐릭터番号:마조끼 >= 4
		;굴복됨みパターン。마조性がかなり高まっている状態
		PRINTFORMW %CALLNAME:캐릭터番号%에게 평소처럼 엉덩이를 때릴 자세를 취하게 한다。
		PRINTFORMW 무릎 위에 엎드려 하반신을 드러내고, 크게 엉덩이를 치켜든 %CALLNAME:캐릭터番号%。
		PRINTFORMW 그 온몸에서, 주인님에게 맞는 것에 대한 기대에 떨고 있는 마조의 냄새가 풍겨온다。
		PRINTFORMW ・・・원래는 시합 전에 기합을 넣기 위해 했던 것이었는데 말이지。
		PRINTFORMW %CALLNAME:캐릭터番号%는, 완전히 엉덩이에 대한 "지도"의 포로가 되어버린 듯했다。
		
		;尻叩き
		CALL EROTIC_MOVE("尻叩き",캐릭터番号,MASTER,700)
		PRINTL 
		
		SETCOLOR C_RED
		PRINTFORMW 찰싹!
		PRINTFORMW 찰싹!!
		PRINTFORMW 찰싹!!!
		RESETCOLOR
		
		PRINTFORMW 아름답게 단련된 %CALLNAME:캐릭터番号%의 둔부에, 단풍잎 손자국이 찍혀간다.
		PRINTFORM 마조로서의 훈육이 잘 되어 있는 %CALLNAME:캐릭터番号%는 엉덩이를 맞을 때마다
		SETCOLOR C_PINK
		PRINTFORM "감사합니다♥"
		RESETCOLOR
		PRINTFORMW 라고 감사의 말을 외쳤다.
		PRINTFORMW ・・・완전히 주인님을 섬기는 마조 노예가 되었구나.
		PRINTFORML 엉덩이 때리기 후의 의식으로, %CALLNAME:MASTER%에게 무릎 꿇는 %CALLNAME:캐릭터番号%를 내려다보며 그렇게 생각했다.
		
		CALL EVENT_FAVUP(캐릭터番号,200)
		JUEL:캐릭터番号:굴복 += 1000+250*(ABL:캐릭터番号:마조끼)
		
	ELSEIF 굴복됨취득
		;굴복됨 패턴. 마조인지 여부는 불문
		PRINTFORMW %CALLNAME:캐릭터番号%에게 엉덩이 때리기를 한다고 전하자, 벽에 손을 대고 엉덩이를 내민 %CALLNAME:캐릭터番号%.
		PRINTFORMW ・・・완전히 "지도"라는 이름의 마조 조교에 익숙해진 모습이지만, 조금 싱겁군.
		PRINTFORMW 그렇게 생각한 %CALLNAME:MASTER%는 벤치에 앉아 이쪽에 올라오라고 자신의 무릎을 가리켰다.
		PRINTFORMW 그 의도를 이해한 %CALLNAME:캐릭터番号%는 무심코 거절하려 했지만, 잠시 고개를 숙인 뒤 지시에 따랐다.
		PRINTFORMW %CALLNAME:MASTER%의 무릎 위에 엎드린 %CALLNAME:캐릭터番号%.
		PRINTFORMW 유니폼의 하반신을 벗겨주자, 아름답게 단련된 둔부가 모습을 드러낸다.
		PRINTFORMW 그 모습은, 마치 어린 소녀가 아빠·엄마에게 혼나서 "엉덩이 찰싹" 당할 때와 같았다.
		PRINTFORMW 때릴 때마다 감사하는 거다. 이건 시합 전에 너에게 기합을 넣기 위해 하는 거니까.
		PRINTFORMW 그렇게 귓가에 전하고, 힘껏 손바닥으로 %CALLNAME:캐릭터番号%의 둔부를 내리쳤다！
		
		;尻叩き
		CALL EROTIC_MOVE("尻叩き",캐릭터番号,MASTER,600)
		PRINTL 
		
		SETCOLOR C_RED
		PRINTFORMW 찰싹!
		PRINTFORMW 찰싹!!
		PRINTFORMW 찰싹!!!
		RESETCOLOR

		PRINTFORMW 아픔과 굴욕으로 어쩔 수 없이 %CALLNAME:캐릭터番号%는 눈물이 고여 있다.
		PRINTFORMW ・・・감사의 말은 어쩌고?
		PRINTFORMW 그 말로, 아픔에 이를 악물며 "감사합니다!"와 "지도"에 대한 감사를 입에 담기 시작했다.
		PRINTFORMW 엉덩이에서 울려 퍼지는 소리와, 노예의 울음소리가 밀실 안에서 하모니를 이룬다。
		PRINTL 
		PRINTFORMW ・・・・・・・・・
		PRINTL 
		PRINTFORML 충분히 엉덩이를 맞고 기합을 받은 %CALLNAME:캐릭터番号%는 일어나 유니폼을 정돈하고,
		PRINTFORML 둔부의 아픔을 참으며 "감사합니다"라고 마지막으로 인사하고 시합장으로 향했다.
		
		CALL EVENT_FAVUP(캐릭터番号,50)
		JUEL:캐릭터番号:굴복 += 500+200*(ABL:캐릭터番号:마조끼)
		
	ELSE
		;조교 부족 패턴. 굴복하지 않은 경우, 별로 신나지 않는다.
		PRINTFORMW 이제 시합을 한다는 건데, %CALLNAME:캐릭터番号%에게서는 왠지 패기 같은 게 느껴지지 않는다.
		PRINTFORMW ・・・뭐, 앞으로 어떤 "지도"를 받을지 생각하면, 시합 걱정할 때가 아니겠지.
		PRINTFORMW 그렇다 해도, 패기가 느껴지지 않는 늘어진 정신으로 있으면, 시합에 내보낼 수 없다.
		PRINTFORMW %CALLNAME:캐릭터番号%에게 벽에 손을 대고 엉덩이를 이쪽으로 향하라고 지시했다.
		PRINTFORMW 도대체 무슨 목적으로 그런 지시를 내렸는지, %CALLNAME:캐릭터番号%는 잘 모르면서도 지시대로 자세를 취했다.
		SETCOLOR C_RED
		PRINTL 
		PRINTFORMW 찰싹!
		PRINTL 
		RESETCOLOR
		PRINTFORMW 갑자기 엉덩이에 통증이 달린 듯, %CALLNAME:캐릭터番号%는 놀라서 벽에서 손을 떼고 이쪽을 보려 했다.
		PRINTFORMW 자세 흐트러뜨리지 마! 그대로 자세로 정신을 집중해!!
		PRINTFORMW %CALLNAME:캐릭터番号%에게 호통치자, 당황하면서도 손을 댄 자세로 돌아왔다.
		PRINTFORMW 그대로 움직이지 않도록 지시하고, %CALLNAME:캐릭터番号%의 엉덩이를 계속 때렸다.
		
		;尻叩き
		CALL EROTIC_MOVE("尻叩き",캐릭터番号,MASTER,600)
		PRINTL 
		
		SETCOLOR C_RED
		PRINTFORMW 찰싹！
		PRINTFORMW 찰싹！！
		PRINTFORMW 찰싹！！！
		RESETCOLOR
		
		PRINTFORMW 맞을 때마다 참을 수 없는 아픔에 %CALLNAME:캐릭터番号%는 신음하듯 소리를 낸다。
		PRINTFORMW ・・・감사의 말은 어디 갔지?
		PRINTFORMW 너무나도 부당한 폭력 행위에 마음이 꺾인 %CALLNAME:캐릭터番号%는 "지도"라는 말에 반항할 수 없게 되어 있었다。
		PRINTFORMW 엉덩이의 아픔을 참으며 "・・・감사합니다"라고 한마디만 중얼거리듯 말했다。
		PRINTFORMW ・・・목소리가 작잖아!! 그런 모기 소리 같은 목소리로 시합 중에 소리 낼 생각이냐!!!
		PRINTFORML 횡포한 "지도"에도, 견뎌내는 선택지밖에 보이지 않게 된 %CALLNAME:캐릭터番号%는,
		PRINTFORMW 반쯤 자포자기한 목소리로 "감사합니다!"라고 힘껏 외쳤다。
		PRINTL 
		PRINTFORML 충분히 기합을 받은 둔부의 아픔을 참는 %CALLNAME:캐릭터番号%는,
		PRINTFORML 겨우 해방되자 "・・・감사합니다"라고 힘없이 마지막으로 인사하고 시합장으로 향했다。
		CALL EVENT_FAVUP(캐릭터番号,-100)
		JUEL:캐릭터番号:굴복 += 100+200*(ABL:캐릭터番号:마조끼)
		
	ENDIF

ELSEIF 조교メニュー == 30
	;[30]-抱きしめる　常に可能　：조교にならない感じの選択肢
	;陥落系で분기、また陥落前も봉사정신とM감각で若干変化する
	IF 친애취득
		;純愛パターン（친애もちの場合）　
		PRINTFORMW 끌어안은 %CALLNAME:캐릭터番号%는 촉촉한 눈빛으로 %CALLNAME:MASTER%를 사랑스럽게 바라보고 있었다。
		PRINTFORMW 이제부터 격렬한 시합이 펼쳐질 텐데, %CALLNAME:MASTER%의 품 안에 얌전히 안겨 있는 모습은 정말 귀엽다。
		SETCOLOR C_PINK
		PRINTFORMW ・・・꼭
		PRINTFORMW 무심코 끌어안아 버렸다。
		PRINTFORMW 그러자 %CALLNAME:캐릭터番号%도 팔을 등 뒤로 돌려 서로 껴안는 형태가 되었다。
		PRINTFORMW ・・・・・・・・・
		PRINTFORMW ・・・・・・・・・
		PRINTFORMW ・・・・・・・・・
		PRINTFORMW ・・・・・・두근.・・・・・・두근.
		PRINTFORMW %CALLNAME:캐릭터番号%의 심장 소리가 점점 빨라진다。
		PRINTFORMW ・・・・・・・・・
		PRINTFORMW ・・・・・・・・・
		PRINTFORMW ・・・・・・・・・
		PRINTFORML ・・・・・・・・・%CALLNAME:캐릭터番号%가 떨어지려 몸을 움직인 걸 눈치채고 껴안는 걸 멈추자,
		RESETCOLOR
		PRINTFORMW 이제부터 시합에 임하는, 운동선수의 얼굴로 바뀐 %CALLNAME:캐릭터番号%의 모습이 거기 있었다。
		PRINTL
		PRINTFORMW ・・・이기자.
		PRINTL
		PRINTFORML 그 한마디만 전하자, %CALLNAME:캐릭터番号%는 쾌활한 목소리로 "네! 감사합니다!"라고 대답했다。
		
	ELSEIF TALENT:캐릭터番号:연모 == 1 && CFLAG:캐릭터番号:호감도 >= 3000
		;연모ついてる状態で、かつ호감도３０００以上。かなり進んでいる状態
		PRINTFORMW 끌어안은 몸을 뒤로 돌린다。
		PRINTFORMW  %CALLNAME:캐릭터番号%는 놀란 듯한 표정을 지었다。
		PRINTFORMW 하지만 그대로 끌어안아주자, 긴장으로 굳어 있던 몸에서 힘이 빠져나갔다。
		PRINTFORMW 운동선수로서 단련되어 있긴 하지만, 이완된 상태의 육체는 상상 이상으로 부드럽다。
		SETCOLOR C_PINK
		PRINTL
		PRINTFORMW ・・・꼭
		PRINTL
		RESETCOLOR
		PRINTFORML 끌어안긴 채로 있던 %CALLNAME:캐릭터番号%였지만,
		PRINTFORMW 시합 전의 긴장은 잘 풀린 듯하다。
		PRINTFORMW 끌어안기를 끝낸 %CALLNAME:캐릭터番号%의 몸은 촉촉하게 열기를 띠고 있었다。
		PRINTFORMW 좋아, 다녀와라。
		PRINTFORML 그렇게 전해 시합장에 내보내자, %CALLNAME:캐릭터番号%는 쾌활한 목소리로 "네! 감사합니다!"라고 대답했다。
		
	ELSEIF TALENT:캐릭터番号:연모 == 1
		;연모ついてる状態。
		PRINTFORMW 끌어안은 몸을 뒤로 돌린다.
		PRINTFORMW  %CALLNAME:캐릭터番号%는 놀라서 무심코 손으로 밀며 떨어지려 한다.
		PRINTFORMW 하지만 그대로 끌어안아주자, 얼굴이 새빨개지면서도 끌어안기는 걸 받아들인 듯했다.
		SETCOLOR C_PINK
		PRINTL
		PRINTFORMW ・・・꼭
		PRINTL
		RESETCOLOR
		PRINTFORMW 처음엔 조금 굳어 있었지만, 끌어안으니 초조한 마음도 가라앉은 듯하다.
		PRINTFORML 끌어안긴 채로 있던 %CALLNAME:캐릭터番号%였지만,
		PRINTFORMW 시합 전의 긴장은 잘 풀린 듯하다.
		PRINTFORMW 끌어안기가 끝난 %CALLNAME:캐릭터番号%는, 너무나도 부끄러웠는지 얼굴이 새빨간 채로 고개를 숙였다.
		PRINTFORMW 좋아, 다녀와라.
		PRINTFORML 그렇게 전해 시합장에 내보내자, %CALLNAME:캐릭터番号%는 쾌활한 목소리로 "네! 감사합니다!"라고 대답했다.
	ELSE
		;普通の純愛モードの状態。
		PRINTFORMW 끌어안은 몸을 뒤로 돌린다.
		PRINTFORMW  %CALLNAME:캐릭터番号%는 놀라서 무심코 손으로 밀며 떨어뜨렸다.
		PRINTFORMW ・・・쿵 하고 밀쳐져 락커에 등을 부딪혔다.
		PRINTFORMW 실수로 너무 심하게 해버린 걸 %CALLNAME:캐릭터番号%가 사과했지만, 신경 쓰지 말라고 손짓했다.
		PRINTFORMW ・・・제대로 전하지 않은 게 잘못이었지.
		PRINTFORMW 시합 전의 긴장을 풀기 위한 거라고 전하고, 천천히 %CALLNAME:캐릭터番号%를 끌어안았다.
		SETCOLOR C_PINK
		PRINTL
		PRINTFORMW ・・・꼭
		PRINTL
		RESETCOLOR
		PRINTFORML 끌어안긴 채로 있던 %CALLNAME:캐릭터番号%였지만,
		PRINTFORMW 시합과는 다른 의미로 긴장했는지, 몸이 굳어 있었다.
		PRINTFORMW ・・・지금까지 이상으로, 더 %CALLNAME:캐릭터番号%와 친해져야겠다고 생각했다.
		PRINTFORMW 그렇게 생각하며 끌어안기를 끝내자, %CALLNAME:캐릭터番号%는 너무나도 부끄러웠는지 얼굴이 새빨간 채로 고개를 숙였다.
		PRINTFORMW 좋아, 다녀와라.
		PRINTFORML 그렇게 전해 시합장에 내보내자, %CALLNAME:캐릭터番号%는 부끄러운 듯이 "・・・감사합니다"라고 대답했다.
	ENDIF
ELSE
	RETURN 1
ENDIF

PRINTL 
CUSTOMDRAWLINE —
PRINTW 
RETURN 0


@CHARA_KIZUNA_59595(参照カードID,カテゴリ플래그)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 조교대상
#DIM DYNAMIC 絆レベル = 0
#DIM DYNAMIC 最大値チェック
#DIM DYNAMIC カテゴリ플래그
#DIM DYNAMIC アルゴ플래그 = 0
#DIM DYNAMIC 지속함정플래그 = 0

;参照カードID와의 인연を確認して返す
;RETURN 絆レベル：0～20の間の数値

;カテゴリ플래그：他のカテゴリで使えるよう拡張する時のための変数
;플래그番号はカテゴリ内の代表카드의 カードIDによって설정する
;ARG☆Sの場合はアドラのカードID(1680)をぶち込んでいて、
;1680の場合のみ신체능력と가창기능により絆レベルにボーナスがつく

;変な返り値が残ってて処理がおかしくならないよう処理
RESULT:1 = 0
RESULT:2 = 0

CALL CARD_EROTICS(参照カードID, 듀얼리스트)

[IF_DEBUG]
	PRINTFORMW 引数：{参照カードID}
	PRINTFORMW 캐릭터ID:{RESULT}
[ENDIF]

IF RESULT >= 1
	;カードIDを元にしてCSVの캐릭터IDを취득
	조교대상 = RESULT
	
	;호감도を確認(0-5)
	IF CFLAG:조교대상:호감도 >= 30000
		絆レベル += 5
	ELSEIF CFLAG:조교대상:호감도 >= 10000
		絆レベル += 4
	ELSEIF CFLAG:조교대상:호감도 >= 3000
		絆レベル += 3
	ELSEIF CFLAG:조교대상:호감도 >= 1000
		絆レベル += 2
	ELSEIF CFLAG:조교대상:호감도 >= 300
		絆レベル += 1
	ENDIF
	
	;우후후解禁상황を確認(0-1)
	SIF CFLAG:조교대상:태도 >= 2
		絆レベル += 1

	;친밀を確認(0-2)
	IF ABL:조교대상:친밀 >= 5
		絆レベル += 2
	ELSEIF ABL:조교대상:친밀 >= 3
		絆レベル += 1
	ENDIF
	
	;순종を確認(0-2)
	IF ABL:조교대상:순종 >= 5
		絆レベル += 2
	ELSEIF ABL:조교대상:순종 >= 3
		絆レベル += 1
	ENDIF
	
	;욕망を確認(0-1)
	SIF ABL:조교대상:욕망 >= 4
		絆レベル += 1
	
	;기교を確認(0-1)
	SIF ABL:조교대상:기교 >= 4
		絆レベル += 1
	
	;감각を確認(0-2)
	;６種のうち、最大のものが高ければ条件クリア할 수 있다。
	最大値チェック = MAX(ABL:조교대상:Ｃ감각,ABL:조교대상:Ｖ감각,ABL:조교대상:Ａ감각,ABL:조교대상:Ｂ감각,ABL:조교대상:Ｍ감각,ABL:조교대상:Ｅ감각)
	IF 最大値チェック >= 5
		絆レベル += 2
	ELSEIF 最大値チェック >= 3
		絆レベル += 1
	ENDIF
	
	;봉사정신、노출증、마조끼、새드끼を確認(0-2)
	;４種の조교関連項目のうち、最大のものが高ければ条件クリア할 수 있다。
	最大値チェック = MAX(ABL:조교대상:봉사정신,ABL:조교대상:노출증,ABL:조교대상:마조끼,ABL:조교대상:새드끼)
	IF 最大値チェック >= 5
		絆レベル += 2
	ELSEIF 最大値チェック >= 3
		絆レベル += 1
	ENDIF
	
	;性技を確認(0-2)
	;６種のうち、最大のものが高ければ条件クリア할 수 있다。
	最大値チェック = MAX(ABL:조교대상:손가락,ABL:조교대상:혀,ABL:조교대상:가슴,ABL:조교대상:허리,ABL:조교대상:질,ABL:조교대상:애널)
	IF 最大値チェック >= 5
		絆レベル += 2
	ELSEIF 最大値チェック >= 3
		絆レベル += 1
	ENDIF
	
	;각인취득を確認(0-2)
	;３種の각인レベルの合計値で조교の進度を確認する。
	;고통각인は純愛やってると上がらないケースもあるので閾値は少し低めに
	最大値チェック = MARK:조교대상:고통각인 + MARK:조교대상:쾌락각인 + MARK:조교대상:굴복각인
	IF 最大値チェック >= 6
		絆レベル += 2
	ELSEIF 最大値チェック >= 3
		絆レベル += 1
	ENDIF
	
	
	;ARG☆Sの絆の特有の추가加点項目※これ合わせるとMAX20点超過する。なので最後にMAX20に直す処理を挟む。
	;신체능력と가창기능によるボーナス(0-6)と、読み込んだカードIDがARG☆Sかをチェック
	IF カテゴリ플래그 == 1680
	
		IF ABL:조교대상:신체능력 >= 5
			絆レベル += 3
		ELSEIF ABL:조교대상:신체능력 >= 4
			絆レベル += 2
		ELSEIF ABL:조교대상:신체능력 >= 3
			絆レベル += 1
		ENDIF
		
		IF ABL:조교대상:가창기능 >= 5
			絆レベル += 3
		ELSEIF ABL:조교대상:가창기능 >= 4
			絆レベル += 2
		ELSEIF ABL:조교대상:가창기능 >= 3
			絆レベル += 1
		ENDIF
		
		;参照カードIDがARG☆SかチェックしてRESULT:1で1を返す
		;参照カードIDが지속함정かをチェックしてRESULT:2で1を返す
		TRYCALLFORM CARDNAME_{参照カードID}("カテゴリ")
		SIF MATCH(RESULTS,"ARG☆S")
			アルゴ플래그 = 1
		
		TRYCALLFORM CARD_{参照カードID}("종류")
		SIF RESULT == 지속함정
			지속함정플래그 = 1
		
	ENDIF
	
	RESULT:1 = アルゴ플래그
	RESULT:2 = 지속함정플래그
	
	[IF_DEBUG]
		PRINTFORMW 絆:{絆レベル}
		PRINTFORMW RESULT1:{RESULT:1}
		PRINTFORMW RESULT2:{RESULT:2}
	[ENDIF]
	
	
	;全てチェックした時点で絆レベルが21以上の경우、20に변경する。
	SIF 絆レベル >= 21
		絆レベル = 20
	
	RETURN 絆レベル
ELSE
	;あなた、또는 조교불가のため、0を返す。
	RETURN 0
ENDIF

RETURN 0


@CARDID_TALENTCHECK(参照カードID)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 조교대상
#DIM DYNAMIC 返り値플래그 = 0

;参照カードIDに対して、TALENT関連の情報をビット値でまとめて返す汎用関数
;今後카드의 条件判断で利用する項目次第で増やしていく

;返り値플래그 bit[0],[1]: [성별]の判断 &1 &2
;返り値플래그 bit[2],[3]: [처녀][再生처녀]の判断 &4 &8
;返り値플래그 bit[4]: [키스미경험]の判断 &16
;返り値플래그 bit[5]: [친애]の判断 &32
;返り値플래그 bit[6]: [굴복됨]の判断 &64

CALL CARD_EROTICS(参照カードID, 듀얼리스트)
IF RESULT >= 1
	;カードIDを元にしてCSVの캐릭터IDを취득
	조교대상 = RESULT
	
	;성별をチェック(最初なのでビット演算なしで直接代入)
	返り値플래그 = TALENT:조교대상:성별
	
	;처녀チェック
	SIF TALENT:조교대상:처녀 == 1
		返り値플래그 |= 4
	;再生처녀チェック
	SIF TALENT:조교대상:처녀 == 2
		返り値플래그 |= 8
	;키스미경험チェック
	SIF TALENT:조교대상:키스미경험 == 1
		返り値플래그 |= 16
	;친애チェック
	SIF TALENT:조교대상:연모 >= 2
		返り値플래그 |= 32
	;굴복됨チェック
	SIF TALENT:조교대상:굴복됨 >= 1
		返り値플래그 |= 64
	
	RETURN 返り値플래그
ELSE
	RETURN 0
ENDIF

RETURN 0


