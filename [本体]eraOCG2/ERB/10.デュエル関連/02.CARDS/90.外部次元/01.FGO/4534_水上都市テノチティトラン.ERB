@CARDNAME_4534,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 중기동 심장도시　－오메테오틀 테노치티틀란－
	CASE "略称"
		RESULTS = 테노치티틀란
	CASE "要塞名"
		RESULTS = 수상도시 테노치티틀란
	CASE "カテゴリ"
		RESULTS:0 = 포트리스
		RESULTS:1 = 프리텐더
		RESULTS:2 = 문 캔서
		RESULTS:3 = 영령
		RESULTS:4 = FATE
ENDSELECT

@CARD_4534(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 초차원
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 9
	CASE "コスト"
		RETURN 5
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 2000
	CASE "성별"
		RETURN 牝性
	CASE "生成カード"
		RETURN 104534
	CASE "추가日"
		RETURN 250703
ENDSELECT

RETURN 0

@CARDSUMMON_4534(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

RETURN 0

@CARD_EXPLANATION_4534(종류)
#DIM DYNAMIC 종류

PRINTL 『초차원』에 의해 강림。
PRINTL （이 카드는 카드의 효과로만 ＥＸ덱에서 필드에 특수소환・마법＆함정 존에 둘 수 있다）
CALL TEXT_DECORATION("D_HEART_F")
PRINTL 포트리스측　《수상도시 테노치티틀란》 코스트：５
PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,EX덱起動)
PRINTL ①：자신 필드에 카드가 ２장 이상 존재하는 경우에 발동할 수 있다。
PRINTL ＥＸ덱의 이 카드를 마법＆함정 존에 앞면 표시로 세트하고、"수상도시 테노치티틀란"이외의 자신 필드의 카드 ２장을 파괴한다。
CALLFORM CARD_ABLE(2,마함起動)
PRINTL ②：자신 묘지의 몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 몬스터를 특수 소환하고、자신의 패 / 필드의 몬스터 ２장을 파괴한다。
CALLFORM CARD_ABLE(0,효과後強制誘発)
PRINTL ③：몬스터가 전투 / 효과로 파괴될 때 마다、이 카드에 이프카요틀 카운터를 １개 놓는다。
PRINTL 그 후、이 카드의 이프카요틀 카운터가 ５개 이상인 경우、이 카드를 특수소환할 수 있다。
CALL TEXT_DECORATION("D_HEART_2")



@CARDCAN_4534(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4534)
SIF RESULT == 1
	RETURN 0
;F
IF 종류 == EX덱起動
	SIF 발동済_1(듀얼리스트,4534)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	RETURN 1
;F
ELSEIF 종류 == 마함起動
	SIF 발동済_2(듀얼리스트,4534)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲필드 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
;F
ELSEIF 종류 == 효과後強制誘発
	SIF ゾーン != "마함"
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"破壊全般",2)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4534(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID

IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4534,1)
	IF RESULT == 0
		CALL CARD_ADD(듀얼리스트,"EX덱","마함",場所,"表側표시")
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件固名前 = 중기동 심장도시　－오메테오틀 테노치티틀란－
		효과条件行先플래그 = 破壊行き
		효과条件複数 = 2
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4534,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲필드 = 1
			효과条件종류 = モン全般
			효과条件行先플래그 = 破壊行き
			효과条件複数 = 2
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 종류 == 효과後強制誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4534)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,"마함",場所,"個Ｃ",1)
		PRINTFORMW 「수상도시 테노치티틀란」에 이프카요틀 카운터가 놓였습니다。（현재치：{GETVAR(@"%듀얼리스트%_魔_個Ｃ:場所")}개）

		SIF GETVAR(@"%듀얼리스트%_魔_個Ｃ:場所") < 5
			RETURN 0
		CALL CARD_SS_CHECK(듀얼리스트,4534,ゾーン)
		SIF RESULT == 1
			RETURN 0

		PRINTL
		PRINTL 보구를 발동하시겠습니까？
		CALL YN_CHOICE(듀얼리스트)
		SIF RESULT != 0
			RETURN 0

		PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
		SETCOLOR C_YELLOW
		PRINTL
		PRINTFORMW 「나의 알타르、나의 쿠아위틀라、내가 사랑하는 틀라마카스키！」
		PRINTFORMW 「심장도시、기동！　오메테오틀 테노치티틀란！」
		PRINTL
		CUSTOMDRAWLINE ―
		RESETCOLOR
		WAIT

		CALL CARD_RECONSTRUCT(듀얼리스트,ゾーン,場所,104534)
		CALL RESETCONDITION
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF


@CARDNAME_104534,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 중기동 심장도시　－오메테오틀 테노치티틀란－
	CASE "略称"
		RESULTS = 테노치티틀란
	CASE "要塞名"
		RESULTS = 수상도시 테노치티틀란
	CASE "カテゴリ"
		RESULTS:0 = 포트리스
		RESULTS:1 = 프리텐더
		RESULTS:2 = 문 캔서
		RESULTS:3 = 영령
		RESULTS:4 = FATE
ENDSELECT

@CARD_104534(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 초차원
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 9
	CASE "コスト"
		RETURN 5
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 2000
	CASE "生成カード"
		RETURN 4534
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_104534(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①：상대는 자신 필드의 공격력 ２０００이하의 몬스터를 공격 / 효과의 대상으로 할 수 없으며、
PRINTL 필드의 이 카드는 １턴에 １번만 전투 / 효과로는 파괴되지 않는다。
CALLFORM CARD_ABLE(0,지속효과,攻守変動)
PRINTL ②：이 카드의 공격은 관통한다。
PRINTL 또한、이 카드가 공격력 ２５００이상의 몬스터와 전투를 행한 경우、데미지 스탭 종료시까지 공격력을 １０００올린다。
CALLFORM CARD_ABLE(11,필드起動,フリー誘発)
PRINTL ③：이 카드가 필드에 존재하는 한 １번만、자신 / 상대 턴에 발동할 수 있다。
PRINTL 상대 필드의 앞면 표시인 카드를 전부 묘지로 보낸다。
CALLFORM CARD_ABLE(0,身代효과)
PRINTL ④：이 카드가 필드를 벗어날 경우、대신에 마법＆함정 존에 둔다。





@CARDCAN_104534(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,104534)
SIF RESULT == 1
	RETURN 0
;M
IF 종류 == 지속효과 || 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	IF 종류 == 攻守変動
		SIF 戦闘場所(듀얼리스트,1) == 5
			RETURN 0
		SIF GETVAR(@"%対面者%_MZ_攻:戦闘場所(듀얼리스트,1)") < 2500
			RETURN 0
	ENDIF
	RETURN 1
;M
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
;M
ELSEIF 종류 == 身代효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF 破壊대상場所 != 場所
		RETURN 0
	SIF MATCH(破壊종류,"마함")
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_104534(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID

IF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"貫通")
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"一破")

	CALL FULLRESET
	효과条件攻撃力 = 2000
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"攻避")
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"対耐")

	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"変化元ID",4534,上書きモード)

ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,1000)

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,104534,11)
	IF RESULT == 0
		CALL FULLRESET
		효과条件표시 = 表側표시
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場全体",場所,"リリース")
	ENDIF

ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,104534)
	IF RESULT == 0
		CALL CARD_RECONSTRUCT(듀얼리스트,ゾーン,場所,4534)
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き,0,1)
		RETURN 1
	ENDIF
	RETURN 0
ENDIF