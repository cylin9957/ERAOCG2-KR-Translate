@CARDNAME_92975,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 아티팩트의 신지
	CASE "略称"
		RESULTS = ＡＦ의 신지
	CASE "カテゴリ"
		RESULTS:0 = 아티팩트
ENDSELECT


@CARD_92975(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "추가日"
		RETURN 250912
ENDSELECT

RETURN 0

@CARD_EXPLANATION_92975(종류)
#DIM DYNAMIC 종류

PRINTL 자신의 마법＆함정 존에 뒷면 표시인 카드가 ５장 존재하거나、
PRINTL 자신 필드에 "아티팩트" 몬스터가 존재하는 경우、이 카드는 상대 턴에 패에서 발동할 수 있다。
PRINTL 이 카드명의 ①의 효과는 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①：덱에서 "아티팩트" 몬스터 １장을 특수 소환한다。
CALLFORM CARD_ABLE(0,戦破壊→묘지)
PRINTL ②：이 카드가 상대턴에 파괴된 경우、필드의 카드 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 카드를 파괴한다。


@CARDCAN_92975(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 효과制限달성
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,92975)
SIF RESULT == 1
	RETURN 0


IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,92975)
		RETURN 0
	IF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 2
			RETURN 0
	ELSEIF ゾーン == "손패"
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 아티팩트
		효과条件표시 = 表側표시
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF 마함数(듀얼리스트) < 5 && RESULT != 1
			RETURN 0
		SIF ターンプレイヤー == 듀얼리스트
			RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 아티팩트
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 戦破壊→묘지
	SIF !GROUPMATCH(묘지送り종류,"破壊","戦闘破壊")
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	CALL FULLRESET
	SIF 듀얼리스트 == "자신"
		효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件除去種 = 묘지
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_92975(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92975,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 아티팩트
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92975)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"묘지")
	ENDIF
ENDIF

