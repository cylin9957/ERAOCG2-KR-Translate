@CARDNAME_54515,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 아티팩트－듀란달
	CASE "略称"
		RESULTS = ＡＦ듀란달
	CASE "カテゴリ"
		RESULTS:0 = 아티팩트
ENDSELECT


@CARD_54515(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2400
	CASE "守備力"
		RETURN 2100
	CASE "추가日"
		RETURN 250912
ENDSELECT

RETURN 0
@CARDSUMMON_54515(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 5

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,54515) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_54515(종류)
#DIM DYNAMIC 종류

PRINTL 레벨５ 몬스터×２
PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(0,효과後誘発)
PRINTL ①：상대 턴에、자신 필드에 "아티팩트" 몬스터가 특수 소환된 경우에 발동할 수 있다。
PRINTL 자신 필드의 몬스터를 소재로 해서 EX덱의 이 카드를 X소환한다。
CALLFORM CARD_ABLE(2,EX소환時)
PRINTL ②：이 카드의 Ｘ소환시에 발동할 수 있다。
PRINTL 덱에서 "아티팩트" 카드 １장을 자신의 마법＆함정 존에 세트한다。
PRINTL ③：자신 / 상대 턴에 １번、이하의 효과에서 １개를 선택해서 발동할 수 있다。
CALLFORM CARD_ABLE(10,발동誘発)
PRINTL ●상대의 패 / 필드 카드의 효과가 발동될 때、이 카드의 Ｘ소재를 １개 제거하고 발동할 수 있다。
PRINTL 　그 효과는 "상대 필드의 마법 / 함정 카드 １장을 골라 파괴한다"가 된다。
CALLFORM CARD_ABLE(10,필드起動 ,フリー誘発)
PRINTL ●이 카드의 Ｘ소재를 １개 제거하고 발동할 수 있다。
PRINTL 　서로는 패를 전부 덱으로 되돌리고 셔플한다。
PRINTL 　그 후、서로는 각각 자신이 덱으로 되돌린 수 만큼 덱에서 드로우한다。

@CARDCAN_54515(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,54515)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	SIF ゾーン != "EX덱"
		RETURN 0
	SIF 발동済_1(듀얼리스트,54515)
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,54515)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = 아티팩트
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"소환全般")
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == EX소환時
	SIF 발동済_2(듀얼리스트,54515)
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 아티팩트
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_DRAW_CHECK(1)
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	SIF 듀얼리스트 != "자신"
		RETURN
	CALL CARD_DRAW_CHECK
	RETURN 1

ELSEIF 종류 == 발동誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	SIF 마함数(듀얼리스트) < 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲필드 = 1
	효과範囲마함 = 1
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_54515(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC ドロー枚数

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54515,1)
	IF RESULT == 0
		TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:場所")}(듀얼리스트,場所)
		CALL XYZSUMMON(듀얼리스트, 場所)
	ENDIF

ELSEIF 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54515,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 아티팩트
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"裏側표시")
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54515,10,X消費1)
	IF RESULT == 0
		CALL URARA_CHANCE(듀얼리스트,処理中_효과ID,"雑多")
		IF RESULT == 0
			FOR カウンタ,0,紙束最大値
				SIF GETVAR(@"%듀얼리스트%_손패:カウンタ") == -1
					CONTINUE
				CALL CARD_DISPOSE(듀얼리스트,"손패",カウンタ,덱行き)
				ドロー枚数 ++
			NEXT
			CALL CARDDRAW_BASIC(듀얼리스트,ドロー枚数,2)

			VARSET ドロー枚数
			FOR カウンタ,0,紙束最大値
				SIF GETVAR(@"%対面者%_손패:カウンタ") == -1
					CONTINUE
				CALL CARD_DISPOSE(対面者,"손패",カウンタ)
				ドロー枚数 ++
			NEXT
			CALL CARDDRAW_BASIC(対面者,ドロー枚数,2)
		ENDIF
	ENDIF

ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54515,10)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대마함 = 1
		CALL CARD_REMOVAL_ADV(対面者,"単体除去",場所,"묘지")
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"無効")
		RETURN 1
	ENDIF
	RETURN 0
ENDIF