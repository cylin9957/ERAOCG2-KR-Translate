@CARDNAME_57801,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 이단의 포보스코보스
	CASE "略称"
		RESULTS = 포보스코보스
	CASE "カテゴリ"
		RESULTS:0 = 없음
ENDSELECT


@CARD_57801(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 2100
	CASE "守備力"
		RETURN 1500
	CASE "성별"
		RETURN 雄性
	CASE "추가日"
		RETURN 250804
ENDSELECT

RETURN 0

@CARDSUMMON_57801(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 4
지정素材종족 = 미확인족
;추가효과用
IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,57801) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_57801(종류)
#DIM DYNAMIC 종류

PRINTL 레벨 ４몬스터×２
PRINTL 이 카드명의 ①의 효과는 １턴에 １번밖에 사용할 수 없다。
CALL CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ①：상대 필드의 효과 몬스터 １장을 대상으로 하고 발동할 수 있다
PRINTL （이 카드가 미확인족 몬스터를 소재로 해서 X소환하고 있는 경우, 이 효과는 상대 턴에서도 발동할 수 있다）。
PRINTL 대상의 몬스터의 효과를 무효로 한다。
PRINTL 그 몬스터가 이 턴에 전투를 행한 경우, 추가로 그 컨트롤을 엔드 페이즈까지 얻을 수 있다。
CALL CARD_ABLE(0,지속효과)
PRINTL ②：이 카드가 몬스터와 전투를 행한 경우、그 ２장은 그 전투에선 파괴되지 않는다。

@CARDCAN_57801(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,57801)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,57801)
		RETURN 0
	IF GETVAR(@"%듀얼리스트%_MZ_条達:場所") == 0
		SIF 종류 == フリー誘発
			RETURN 0
		SIF メインフェイズ != 1
			RETURN 0
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_57801(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,57801,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件除去種 = 無効
		CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"選択")
		IF RESULT == 1
			CALL CARD_SELECT(듀얼리스트,RESULT:1)
			選択カード = RESULT
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"無効",2,1)
			
			SIF GETVAR(@"%対面者%_MZ_戦済:選択カード") == 0 || モンスター数(듀얼리스트) >= 5
				RETURN 0
			SIF 듀얼리스트 == "자신"
				PRINTL 몬스터의 컨트롤을 엔드 페이즈까지 얻으시겠습니까？
			CALL YN_CHOICE(듀얼리스트)
			SIF RESULT == いいえ
				RETURN 0

			CALL FULLRESET
			효과추가종류:0 = 5
			효과추가종류:1 = 695
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",選択カード,"強奪")
		ENDIF
	ENDIF

ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦耐")
	CALL STATCHANGE_EFFECT(対面者,"自身",戦闘場所(듀얼리스트,1),"戦耐")
ENDIF
