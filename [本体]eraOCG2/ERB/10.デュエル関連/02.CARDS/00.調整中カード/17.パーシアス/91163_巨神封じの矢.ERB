@CARDNAME_91163,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		;RESULTS = ティタノサイダー
		RESULTS = 티타노사이더
	CASE "略称"
		RESULTS = 티타노사이더
	CASE "カテゴリ"
		RESULTS:0 = 천공의 성역
		RESULTS:1 = 파샤스
ENDSELECT


@CARD_91163(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "連動ID"
		RETURN 1631
	;アポロウーサ（ID:1631）と連動
	CASE "추가日"
		RETURN 250915
ENDSELECT

RETURN 0

@CARD_EXPLANATION_91163(종류)
#DIM DYNAMIC 종류

PRINTL このカード名の①②の효과は１ターンに１度、いずれか１つしか使用できない。
CALLFORM CARD_ABLE(1,손패起動,마함起動,フリー誘発)
PRINTL ①：상대フィールドの융합・Ｓ・Ｘ・Ｌモンスター１体を대상として발동できる。
PRINTL （자신フィールドが「천공의 성역」である場合、この효과に対して상대はカードの효과を발동できない。）
PRINTL そのモンスターの攻撃力を０にし、その효과を無効にする。
CALLFORM CARD_ABLE(2,효과後誘発)
PRINTL ②：このカードが묘지に存在する状態で、상대がＥＸ덱からモンスターを특수소환した場合に발동できる。
PRINTL 자신の묘지の「티타노사이더」１枚を자신フィールドにセットする。

;【エンジェル・パーミッション】と直接的な関連性は薄いが、ちょうどいいパーミッション系の효과を持つ通常함정カードで、
;かつアポロウーサが천사족モンスターであるという相性もあったので、「티타노사이더」に白날개の矢を立てる。
;カテゴリでサーチもできるようにして、천공의 성역の系統なら採用も検討できるぐらいの性能になった・・・かもしれない。
;효과はほぼそのままで若干スレイに寄せた使用感に。聖域があるとチェーン불가になり、발동を無効にしたりフリチェで逃げられない。
;②の묘지효과は「묘지へ送られたターン～」の概念が煩雑なので、「ギャラクシー・サイクロン」に倣っていずれかターン１で簡略化。
;想定される상대の動きを考えると②がターンを跨ぐのは遅いのだが、とはいえよくある再利用可能だが使用後제외される制約でそれぞれターン１だと、
;「迷い風」のほぼ下位互換ぐらいのカードになって、元々のカードが「迷い風」と差別化できてた部分が消えてしまうので、苦肉の策でこの性能に。
;パーミッション想定であれば、再利用可能でアドを稼げる代わりに動きが遅い、という효과調整のほうがテーマギミックとの一貫性があるだろう。


@CARDCAN_91163(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 효과先
#DIM DYNAMIC 대상カード_ID
#DIM DYNAMIC 대상カード_종류
#DIM DYNAMIC 추가条件플래그 = 0

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,91163)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,91163)
		RETURN 0
	SIF 발동済_2(듀얼리스트,91163)
		RETURN 0
	;손패발동条件を満たしているかチェック（ＰゾーンにＳアリアドネーが居るかチェック）　RESULT == 1で판정
	;除去種無効でチェックしたら、カードが無効状態かどうかを판정できる気がするので試験的に１行추가。
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件名前 = 천공 사랑천사 아리아드네
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		추가条件플래그 |= 1
	
	IF ゾーン != "마함"
		SIF ゾーン != "손패" || 추가条件플래그 != 1
			RETURN 0
	ELSE
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	
	;상대フィールドに攻撃力1以上のEXモンスターが存在する必要がある。
	;효과無効が出来なくても대상にできるようなので、除去種 = 無効の条件は記述しない。(公式Q&Aに明記あって助かる)
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件종류 = EXモン全般
	효과条件以攻撃力 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	
	RETURN 1
	
ELSEIF 종류 == 효과後誘発
	SIF 발동済_1(듀얼리스트,91163)
		RETURN 0
	SIF 발동済_2(듀얼리스트,91163)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	;상대がＥＸ덱から특수소환した場合に발동可能。
	CALL FULLRESET
	효과条件종류 = 효과全般
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"EX특수소환",1)
	SIF RESULT != 1
		RETURN 0
	
	;最終소환先を確認して、ＥＸモンスターであることも確認。
	효과先 = FINAL_SUMMON_TARGET(듀얼리스트,1)
	대상カード_ID = GETVAR(@"%対面者%_필드:효과先")
	TRYCALLFORM CARD_{대상カード_ID},"종류"
	대상カード_종류 = RESULT
	SIF 대상カード_종류 <= 3 && 대상カード_종류 >= 10
		RETURN 0
	
	RETURN 1
ELSEIF 종류 == チェーン可否
	SIF !GROUPMATCH(チェーン可否종류, 손패起動, 마함起動, フリー誘発)
		RETURN 0
	SIF ゾーン == "묘지"
		RETURN 0
	SIF GETVARS(@"%듀얼리스트%_フィールド名") != "천공의 성역"
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_91163(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 효과先
#DIM DYNAMIC 설정レベル
#DIM DYNAMIC 대상カード_ID
#DIM DYNAMIC 대상カード_종류
#DIM DYNAMIC 選択結果

#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードB
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,91163,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件종류 = EXモン全般
		효과条件以攻撃力 = 1
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT != 9999
			CALL CARD_SELECT(듀얼리스트,RESULT)
			CALL SET_FIELD_INFO(대상者（CS）,参照先（CS）,選択カード（CS）,"永攻",-999999)
			CALL SET_FIELD_INFO(대상者（CS）,参照先（CS）,選択カード（CS）,"無効",2,上書きモード)
		ENDIF
	ENDIF
ELSEIF 종류 == 효과後誘発
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,91163,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件名前 = 티타노사이더
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"裏側표시")
	ENDIF
ENDIF


