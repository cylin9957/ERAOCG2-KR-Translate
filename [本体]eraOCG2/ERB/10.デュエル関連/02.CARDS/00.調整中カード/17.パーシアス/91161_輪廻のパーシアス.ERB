@CARDNAME_91161,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 윤회의 파샤스
	CASE "略称"
		RESULTS = 윤회의 파샤스
	CASE "カテゴリ"
		RESULTS:0 = 천공의 성역
		RESULTS:1 = 파샤스
ENDSELECT


@CARD_91161(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "レア度"
		RETURN SPR
	CASE "추가日"
		RETURN 250915
ENDSELECT

RETURN 0

@CARD_EXPLANATION_91161(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드의 발동에 대해서 상대는 효과를 발동할 수 없다。
PRINTL 이 카드명의 카드는 １턴에 １장밖에 발동할 수 없다。
CALLFORM CARD_ABLE(1,발동誘発)
PRINTL ①：상대가 마법 / 함정 / 몬스터의 효과를 발동했을 때에、자신의 패에서 일반 함정 카드 １장을 버리고、１０００ＬＰ를 지불하고 발동할 수 있다。
PRINTL 그 발동을 무효로 하고、덱으로 되돌린다。그 후、자신의 덱 / ＥＸ덱에서 "파샤스" 몬스터 １장을 특수 소환할 수 있다。
PRINTL 자신 필드가 "천공의 성역"인 경우、패 / 묘지 / 제외 상태에서 특수 소환할 수 있다。
PRINTL 
PRINTL 

;【エンジェル・パーミッション】の核心となる、万能無効型の카운터함정（相当のチェーン불가の함정カード）の２枚目。
;こちらも名称ターン１をつけて同一ターン中にサーチしてきて妨害추가には出来ないよう調整。
;발동コストが元々のカードでも死ぬほど重くて、まともに텍스트通りにコスト払って발동してたらリソース勝負が出来なくなるカードなのだが、
;チェーン불가で（ＥＸ対応の）リクルート효과も備えていて、발동さえ出来れば現代遊戯王でも最上位クラスの万能無効함정となっている。
;카운터함정（スペルスピード３）でのリクルート효과は、うらら等でも止められない刺激的なスペック。EXから出せるのも凄まじい。
;カテゴリとして「천공의 성역」のカード名を텍스트に含めたかったので、추가효과で出せる範囲を拡張できる。덱・EX덱以外を選ぶ意味も薄いが。
;카운터함정による특수소환の処理として、うらら효과・わらし효과を一時的に無効化する処理を挟んでいる。スルーさせるだけなので後で元に戻している。
;本来のOCG版では「解放のアリアドネ」でコストを踏み倒して使うことが想定されており、eraOCG2でもそのギミックを踏襲している。

;なお、本来は손패の카운터함정１枚を見せてから、손패を１枚捨てるコストを払うという処理になっている。
;카운터함정を손패に残したい場面を想定してのことと思うが、そこまで再現するほどのギミックでもないので簡略化して손패の通常함정１枚コストに변경。
;動き的に「ラビュリンス」との相性が良くなりそうな気もするが、ウェルカムで악마족縛りがつくので、わざわざこちらにも천사족縛りをつける程でもないか。


@CARDCAN_91161(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 추가条件플래그 = 0

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,91161)
SIF RESULT == 1
	RETURN 0

IF 종류 == 발동誘発
	SIF 발동済_1(듀얼리스트,91161)
		RETURN 0
	;손패발동条件を満たしているかチェック（ＰゾーンにＳアリアドネが居るかチェック）　RESULT == 1で판정
	;除去種無効でチェックしたら、カードが無効状態かどうかを판정できる気がするので試験的に１行추가。
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件名前 = 천공 사랑천사 아리아드네
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		추가条件플래그 |= 1
	
	IF ゾーン != "마함"
		SIF ゾーン != "손패" || 추가条件플래그 != 1
			RETURN 0
	ELSE
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	;자신フィールドにＳアリアドネが存在しない場合は、발동条件でＬＰコストと손패に通常함정カードが必要
	IF 추가条件플래그 != 1
		SIF GETVAR(@"ライフ_%듀얼리스트%") <= 1000
			RETURN 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종류 = 通常함정
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	
	CALL FULLRESET
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == チェーン可否
	RETURN 1
ENDIF



@CARDEFFECT_91161(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 추가条件플래그 = 0

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 발동誘発
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,91161,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件名前 = 천공 사랑천사 아리아드네
		효과条件除去種 = 無効
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			추가条件플래그 |= 1
		
		
		IF 추가条件플래그 != 1
			;LPコストと손패コストを支払う
			CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",1000)
			CALL FULLRESET
			효과範囲손패 = 1
			효과条件종류 = 通常함정
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			SIF RESULT != 1
				RETURN 0
		ENDIF
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"덱")
		IF RESULT == 1
			;無効にした場合、추가で「파샤스」モンスター１体を특수소환できる。（任意）
			;フィールドが천공의 성역の場合、リクルート範囲が広くなる
			SIF GETVARS(@"%듀얼리스트%_フィールド名") == "천공의 성역"
				추가条件플래그 |= 2
			
			;誘発娘ごときが카운터함정を止められると思うなの処理部分（ちからわざ）
			IF GETVAR(@"%듀얼리스트%_うらら효과") >= 1
				SETVAR @"%듀얼리스트%_うらら효과",0
				추가条件플래그 |= 4
			ENDIF
			IF GETVAR(@"%듀얼리스트%_わらし효과") >= 1
				SETVAR @"%듀얼리스트%_わらし효과",0
				추가条件플래그 |= 8
			ENDIF
			
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲EX덱 = 1
			IF 추가条件플래그 & 2
				효과範囲손패 = 1
				효과範囲묘지 = 1
				효과範囲제외 = 1
			ENDIF
			효과条件カテゴリ = 파샤스
			효과条件종류 = モン全般
			효과条件任意 = 1
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			
			;あっ、終わったんでうららさんわらしさんは再度待機してもらっていいっす
			SIF 추가条件플래그 & 4
				SETVAR @"%듀얼리스트%_うらら효과",1
			SIF 추가条件플래그 & 8
				SETVAR @"%듀얼리스트%_わらし효과",1
		ENDIF
		RETURN 1
	ENDIF
ENDIF


