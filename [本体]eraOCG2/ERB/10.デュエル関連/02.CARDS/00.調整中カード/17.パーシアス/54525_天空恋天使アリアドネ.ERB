@CARDNAME_54525,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 천공 사랑천사 아리아드네
	CASE "略称"
		RESULTS = 천공 아리아드네
	CASE "カテゴリ"
		RESULTS:0 = 파샤스
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_54525(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 800
	CASE "守備力"
		RETURN 1500
	CASE "分割"
		RETURN 펜듈럼
	CASE "성별"
		RETURN 牝性
	CASE "レア度"
		RETURN SPR
	CASE "추가日"
		RETURN 250915
ENDSELECT

RETURN 0

@CARDSUMMON_54525(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要レベル数 = 4
IF EX参照 == 0 || EX参照 == 1
	素材条件튜너 = 1
ELSEIF EX参照 >= 10
	素材条件튜너 = -1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,54525) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_54525(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("P_MAGIC")
PRINTL このカード名の②③のＰ효과はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①：このカードが마법＆함정ゾーンに存在する限り、자신は「파샤스」함정カードを손패から발동でき、
PRINTL 「파샤스」함정カードを발동する際のＬＰコスト・손패コストが不要になる。
;この효과は「파샤스」함정カード側で処理を記述している。
CALLFORM CARD_ABLE(2,마함起動)
PRINTL ②：자신メインフェイズに、１０００ＬＰを払うか손패の通常함정カード１枚を상대に見せて발동できる。
PRINTL 자신の덱からレベル４の천사족・빛속성モンスター１体を손패に加える。
CALLFORM CARD_ABLE(3,효과後誘発)
PRINTL ③：자신フィールドが「천공의 성역」の状態で、자신が通常함정カードの효과を발동した場合に발동できる。
PRINTL 덱から「파샤스」마법・함정カード１枚をフィールドにセットする。
CALL TEXT_DECORATION("P_MAGIC_2")
PRINTL 튜너＋튜너以外のモンスター１体以上
PRINTL このカード名の①のモンスター효과は１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,戦破壊→묘지)
PRINTL ①：このカードがフィールドを離れた場合に발동できる。
PRINTL ＥＸ덱の「천공 사랑천사 아리아드네」１体を자신のＰゾーンに置く。

;アリアドネーは元ネタの神話ではお姫様なので牝性モンスターとする。(苦しすぎる言い訳)
;解放のアリアドネは파샤스テーマとしては必要ではあるが、あのカードを손패に確保できるほど潤沢なサーチ手段を準備することは難しい。
;なのでヴァルモニカ方式で直接Pゾーンに置く타입の出し方をする想定で考えた結果、EX덱から置ければ強そうだと考えて制作してみた。
;今はＯＣＧにこの挙動をするカードは無さそうだが、いずれは作られそうなギミックな気がする。
;
;ＥＸの펜듈럼モンスターでＰ효과が強いというデザイン。リソース稼ぎ효과重視でＰ소환する系統の효과も持っていない。
;正攻法で出して置いてとすると大変なぶん、サーチ＋誘発とアド稼ぎは優秀。専用마법を駆使してＰゾーンに貼る動きは優先していきたい。


@CARDCAN_54525(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성 = 0
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,54525)
SIF RESULT == 1
	RETURN 0
	
IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_2(듀얼리스트,54525)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 1000
		条件달성 |= 1
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = 通常함정
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		条件달성 |= 2
	;LP1000以下(1)かつ손패に通常함정がない(2)場合は발동できない
	SIF 条件달성 & 1 && 条件달성 & 2
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件속성 = 빛속성
	효과条件종족 = 천사족
	효과条件종류 = モン全般
	효과条件レベル = 4
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_3(듀얼리스트,54525)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	SIF GETVARS(@"%듀얼리스트%_フィールド名") != "천공의 성역"
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 通常함정
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동")
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 파샤스
	효과条件종류 = 마함全般
	효과条件うらら解除 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 戦破壊→묘지
	SIF 발동済_1(듀얼리스트,54525)
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_54525(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM 選択カード


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54525,2)
	IF RESULT == 0
		;LPを1000払うか손패の通常함정を見せる。千年の処理を参考
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종류 = 通常함정
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲손패 = 1
			효과条件종류 = 通常함정
			효과条件任意 = 1
			CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		ELSE
			RESULT = 9999
		ENDIF
		SIF RESULT == 9999
			CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",1000)
		
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件속성 = 빛속성
		효과条件종족 = 천사족
		효과条件종류 = モン全般
		효과条件レベル = 4
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54525,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 파샤스
		효과条件종류 = 마함全般
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"裏側표시")
	ENDIF
ELSEIF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54525,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲EX덱 = 1
		효과条件名前 = 천공 사랑천사 아리아드네
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
	ENDIF
ELSEIF 종류 == 지속효과
	;지속효과の処理部分は파샤스通常함정カードに記述。この部分は空白で何も処理しない。
ENDIF

