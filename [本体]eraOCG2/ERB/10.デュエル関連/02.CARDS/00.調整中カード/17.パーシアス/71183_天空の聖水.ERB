@CARDNAME_71183,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 천공의 성수
	CASE "略称"
		RESULTS = 천공의 성수
	CASE "カテゴリ"
		RESULTS:0 = 천공의 성역
		RESULTS:1 = 파샤스
		RESULTS:2 = 히페리온
		RESULTS:3 = 대행자
ENDSELECT


@CARD_71183(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "レア度"
		RETURN SPR
	CASE "추가日"
		RETURN 250915
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71183(종류)
#DIM DYNAMIC 종류

PRINTL このカード名の①②の효과はそれぞれ１ターンに１度しか使用できない。
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①：以下の효과から１つを選択して발동できる。
PRINTL ●자신の덱から「천공의 성역」フィールド마법１枚を자신フィールドに表側표시で置く。
PRINTL ●자신の덱から「파샤스」、「히페리온」、「대행자」モンスター１体を손패に加える。
CALLFORM CARD_ABLE(2,身代효과)
PRINTL ②：자신フィールドの「파샤스」、「히페리온」、「대행자」モンスターカードが
PRINTL 戦闘・효과で破壊される場合、代わりに묘지のこのカードを제외できる。

;【대행자】の重要な初動カード。천공의 성역を扱う타입の천사족テーマ全体で手広く使えるサーチ札として作成。
;該当テーマのモンスター１体を손패に加える癖のない효과に加えて、덱から「천공의 성역」フィールド마법の直接발동も可能。
;これで速攻마법だったらもっと使い道あるのになーと思うところはあるが、縛りやコストがないなら通常마법が適正なバランスと想定。
;代わりにフィールド마법をカテゴリ지정でOKにして拡張性を持たせたが、元々のカードと異なり발동処理は伴わない点は注意が必要。
;その他、身代효과が효과破壊にも対応するようになっていたりと、地味な部分で現代的な효과に底上げ。


@CARDCAN_71183(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 条件달성カード名,10
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71183)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,71183)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종류 = モン全般
	효과条件カテゴリ:0 = 파샤스
	효과条件カテゴリ:1 = 대행자
	효과条件カテゴリ:2 = 히페리온
	효과条件スーパー종류 = フィールド
	효과条件スーパーカテゴリ = 천공의 성역
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 身代효과
	;身代条件
	SIF 발동済_2(듀얼리스트,71183)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF !GROUPMATCH(ゾーン,"묘지")
		RETURN 0
	VARSET RESULTS
	TRYCALLFORM CARDNAME_{破壊대상ID}("カテゴリ")
	SIF !GROUPMATCH(RESULTS,"파샤스","대행자","히페리온")
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"戦闘","효과")
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_71183(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성 = 0
#DIM DYNAMIC 選択結果
#DIM DYNAMIC 選択カード_ID

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71183,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;まず효과のどちらが使用できるか確認
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = フィールド
		효과条件カテゴリ = 천공의 성역
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			条件달성 |= 1
		
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = モン全般
		효과条件カテゴリ:0 = 파샤스
		효과条件カテゴリ:1 = 대행자
		효과条件カテゴリ:2 = 히페리온
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			条件달성 |= 2
		
		IF 条件달성 & 1 && 条件달성 & 2
			;효과を選べる場合のチェック
			IF 듀얼리스트 == "상대"
				;条件１：자신フィールドが既に「천공의 성역」であればモンスターをサーチ
				IF GETVARS(@"%듀얼리스트%_フィールド名") == "천공의 성역"
					選択結果 = いいえ
				ELSE
					;条件２：자신フィールドにフィールド마법が存在していればモンスターをサーチ
					CALL FULLRESET
					효과範囲마함 = 1
					효과条件종류 = フィールド
					효과条件표시 = 表側표시
					CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
					IF RESULT == 1
						選択結果 = いいえ
					ELSE
						;条件３：손패にサーチしたいモンスターが存在していればフィールド마법を置く、そうでないならモンスターをサーチ
						CALL FULLRESET
						효과範囲손패 = 1
						효과条件名前:0 = 역천사 발키리어
						효과条件名前:1 = 神秘の대행자 アース
						CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
						IF RESULT == 1
							選択結果 = はい
						ELSE
							選択結果 = いいえ
						ENDIF
					ENDIF
				ENDIF
			ELSE
				PRINTL どちらの효과を使用しますか？
				CALL YN_CHOICE(듀얼리스트,"●자신の덱から「천공의 성역」フィールド마법１枚を자신フィールドに表側표시で置く。","●자신の덱から「파샤스」、「히페리온」、「대행자」モンスター１体を손패に加える。")
				選択結果 = RESULT
			ENDIF
		ELSEIF 条件달성 & 1
			選択結果 = はい
		ELSE
			選択結果 = いいえ
		ENDIF
		
		IF 選択結果 == はい
			;덱から「천공의 성역」フィールド마법を置く処理
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件종류 = フィールド
			효과条件カテゴリ = 천공의 성역
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
		ELSE
			;덱から「파샤스」、「히페리온」、「대행자」モンスター１体を손패に加える処理
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件종류 = モン全般
			효과条件カテゴリ:0 = 파샤스
			효과条件カテゴリ:1 = 대행자
			효과条件カテゴリ:2 = 히페리온
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 身代효과
	;身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71183,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,제외行き)
		CALL FULLRESET
		효과条件종류 = モン全般
		효과条件カテゴリ:0 = 파샤스
		효과条件カテゴリ:1 = 대행자
		효과条件カテゴリ:2 = 히페리온
		CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"破耐",6)
		CALL FULLRESET
		효과条件종류 = モン全般
		효과条件カテゴリ:0 = 파샤스
		효과条件カテゴリ:1 = 대행자
		효과条件カテゴリ:2 = 히페리온
		CALL STATCHANGE_EFFECT(듀얼리스트,"마함",場所,"破耐",6)
		RETURN 1
	ENDIF
	RETURN 0
ENDIF


