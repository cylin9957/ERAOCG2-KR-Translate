@CARDNAME_26132,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 다크 시무르그
	CASE "略称"
		RESULTS = 다무르그
	CASE "カテゴリ"
		RESULTS:0 = 시무르그
ENDSELECT


@CARD_26132(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RESULT:1 = 바람속성
		RETURN 어둠속성
	CASE "종족"
		RETURN 비행야수족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2700
	CASE "守備力"
		RETURN 1000
	CASE "성별"
		RETURN 牝性
	CASE "추가日"
		RETURN 250917
ENDSELECT

RETURN 0

@CARD_EXPLANATION_26132(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 바람속성으로도 취급한다。
CALLFORM CARD_ABLE(0,손패起動)
PRINTL ①：자신 묘지의 바람속성 / 어둠속성 몬스터를 １장씩 제외하고 발동할 수 있다。
PRINTL 패의 이 카드를 특수 소환한다。
CALLFORM CARD_ABLE(0,묘지起動)
PRINTL ②：자신 패의 바람속성 / 어둠속성 몬스터를 １장씩 제외하고 발동할 수 있다。
PRINTL 묘지의 이 카드를 특수 소환한다。
CALLFORM CARD_ABLE(0,ターン終了時強制)
PRINTL ③：상대 엔드 페이즈에 발동한다。
PRINTL 상대의 필드에 세트된 카드의 수×５００ 데미지를 상대에게 준다。

@CARDCAN_26132(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,26132)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動 || 종류 == 묘지起動
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,26132,ゾーン)
	SIF RESULT == 1
		RETURN 0
	IF ゾーン == "손패"
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件속성 = 어둠속성
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件속성 = 바람속성
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ELSEIF ゾーン == "묘지"
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件속성 = 어둠속성
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件속성 = 바람속성
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	RETURN 1

ELSEIF 종류 == ターン終了時強制
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_26132(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動 || 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,26132)
	IF RESULT == 0
		CALL FULLRESET
		IF ゾーン == "손패"
			효과範囲묘지 = 1
		ELSEIF ゾーン == "묘지"
			효과範囲손패 = 1
		ENDIF
		효과条件속성 = 바람속성
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			IF ゾーン == "손패"
				효과範囲묘지 = 1
			ELSEIF ゾーン == "묘지"
				효과範囲손패 = 1
			ENDIF
			효과条件속성 = 어둠속성
			효과条件行先플래그 = 제외行き
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				IF ゾーン == "손패"
					효과範囲손패 = 1
				ELSEIF ゾーン == "묘지"
					효과範囲묘지 = 1
				ENDIF
				효과条件名前 = 다크 시무르그
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == ターン終了時強制
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,26132)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		효과条件표시 = 裏側표시
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT > 0
			CALL LIFE_CHANGE(듀얼리스트,"減少","효과",RESULT*500,1)
	ENDIF
ENDIF
