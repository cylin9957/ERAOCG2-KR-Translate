;-------------------------------------------------
;依頼を確認する
;-------------------------------------------------
@COM431
;貯蔵を確認する
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 소환_ページ送り
#DIM DYNAMIC リスト_표시
#DIM DYNAMIC 캐릭터クター
#DIM DYNAMIC カウンタ,2
#DIM DYNAMIC 衣種
#DIM DYNAMIC ローカル１
#DIM DYNAMIC 最大値
#DIM DYNAMIC 選択カード,10
#DIM DYNAMIC 一時보존,10
#DIM DYNAMIC 消去カード
#DIM DYNAMIC 選択依頼
#DIM DYNAMIC リザルト
#DIM DYNAMIC LTARGET,99999
#DIM DYNAMIC LTARGETNUM
#DIM DYNAMIC 설정현재위치

설정현재위치 = CFLAG:MASTER:현재위치
PRINTL
PRINTL
PRINTL
;いったんターゲットを非難させる（バグ回避用）

LTARGETNUM = TARGETNUM
FOR カウンタ,0,TARGETNUM
	LTARGET:カウンタ = TARGET:カウンタ
NEXT
LOCAL = TARGET
TARGET = -1
$REPEAT_LOAD
CALLFORM IRAI_ACT_{依頼_受領中}
リザルト = RESULT
IF リザルト == 2
	CALL DOUKOU_RELEASE
	CUSTOMDRAWLINE ―
	依頼플래그:依頼_受領中 = 2
	依頼_完了횟수:依頼_受領中 += 1	
	TRYCALLFORM IRAI_EVENT_{依頼_受領中}("이벤트種")
	SIF RESULT == 이벤트_物語
		物語依頼_完了済 = 1


	依頼_完了済 += 1
	ALIGNMENT CENTER
	PRINTL
	CALL CHARA_CUTIN("","MISSION COMPLETE!")
	PRINTL
	ALIGNMENT LEFT
	CUSTOMDRAWLINE ―
	PRINTFORMW 의뢰를 달성했습니다！
	TRYCALLFORM IRAI_REWARD_{依頼_受領中}

	CALL IRAI_RESET

	VARSET 依頼_受領中 
ELSEIF リザルト == 3
	CALL DOUKOU_RELEASE
	CUSTOMDRAWLINE ―
	ALIGNMENT CENTER
	PRINTL
	CALL CHARA_CUTIN("","MISSION FAILED…")
	PRINTL
	ALIGNMENT LEFT
	CUSTOMDRAWLINE ―
	PRINTFORMW 의뢰에 실패했습니다……
	
	PRINTL [0] - 재도전한다
	PRINTL [1] - 포기한다
	BINPUT
	IF RESULT == 0
		CFLAG:MASTER:현재위치 = 설정현재위치
		GOTO REPEAT_LOAD
	ENDIF
	依頼_完了済 += 1
	CALL IRAI_RESET
	VARSET 依頼_受領中 
ELSEIF リザルト == 4
	CALL DOUKOU_RELEASE
	CUSTOMDRAWLINE ―
	依頼플래그:依頼_受領中 = 2
	依頼_完了횟수:依頼_受領中 += 1	
	依頼_完了済 += 1
	ALIGNMENT CENTER
	PRINTL
	CALL CHARA_CUTIN("","TO BE CONTINUED")
	PRINTL
	ALIGNMENT LEFT
	PRINTFORMW （이 앞은 공사중입니다、갱신을 기다려주세요）
	CUSTOMDRAWLINE ―
	PRINTFORMW 의뢰_수령상태를 리셋합니다。
	CALL IRAI_RESET 
	VARSET 依頼_受領中 
ELSEIF リザルト == 5
	CALL DOUKOU_RELEASE
	SETCOLOR C_YELLOW
	CUSTOMDRAWLINE ―
	依頼플래그:依頼_受領中 = 2
	依頼_完了횟수:依頼_受領中 += 1	
	物語依頼_完了済 = 1
	依頼_完了済 += 1
	ALIGNMENT CENTER
	PRINTL
	CALL CHARA_CUTIN("","EPISODE COMPLETE!")
	PRINTL
	ALIGNMENT LEFT
	CUSTOMDRAWLINE ―
	PRINTFORMW 이변을 해결했습니다！
	
	TRYCALLFORM IRAI_REWARD_{依頼_受領中}

	RESETCOLOR
	CALL IRAI_RESET
	VARSET 依頼_受領中 
ELSEIF リザルト == 6
	SETCOLOR C_YELLOW
	CUSTOMDRAWLINE ―
	ALIGNMENT CENTER
	PRINTL
	CALL CHARA_CUTIN(""," GAME OVER")
	PRINTL
	RESETCOLOR
	ALIGNMENT LEFT
	CUSTOMDRAWLINE ―
	CALL GAME_OVER
	IF RESULT == 0
		CFLAG:MASTER:현재위치 = 설정현재위치
		GOTO REPEAT_LOAD
	ENDIF
ENDIF
PRINTL
PRINTL
PRINTL
;CALL SOURCE_CALC_환락(TARGET, MASTER, 100 * リザルト)
CALL IRAI_COND_RESET

SELECTCOM = 431
TFLAG:SELECTCOM보존 = SELECTCOM


TARGETNUM = LTARGETNUM
FOR カウンタ,0,LTARGETNUM
	TARGET:カウンタ = LTARGET:カウンタ
NEXT
TARGET = LOCAL
RETURN リザルト



;依頼受領
@COM_ABLE431
;이동実行판정
SIF !TFLAG:100
	RETURN 0
;一括관리
SIF GLOBAL_COMABLE(431)
	RETURN RESULT
SIF !MATCH(依頼_場所,CFLAG:MASTER:현재위치)
	RETURN 0
SIF 依頼플래그:依頼_受領中 != 1
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
RETURN 1



@DOUKOU_RELEASE
FOR LOCAL, 0, CHARANUM
	CFLAG:LOCAL:동행 = 0
	CFLAG:LOCAL:동행준비 = 0
NEXT


@CARD_GET_CHARA(캐릭터_ID,이벤트外)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 이벤트外

SIF CFLAG:캐릭터_ID:소유함 == 1 && アップデート処理 == 1
	RETURN 0

IF 이벤트外 != 1
	CALLFORM IRAI_EVENT_{依頼_受領中}("이벤트種")
	IF RESULT != 이벤트_ＲＥ && 依頼플래그:依頼_受領中 == 2
		PRINTFORMW 이 의뢰의 보수는 이미 받았습니다。
		RETURN 0
	ENDIF
ENDIF

IF 依頼_見返し中 >= 1
	RETURN 0
ENDIF

PRINTL
CFLAG:캐릭터_ID:소유함 = 1
SETCOLOR C_YELLOW
PRINTFORMW 《%NAME:캐릭터_ID%》를 손에 넣었습니다！
ITEM:(NO:캐릭터_ID) += 1
RESETCOLOR

@CARD_GET_SINGLE(カード_ID,이벤트外,획득枚数)
#DIM DYNAMIC カード_ID
#DIM DYNAMIC 이벤트外
#DIM DYNAMIC 획득枚数

SIF ITEM:カード_ID >= 1 && アップデート処理 == 1
	RETURN 0

IF 依頼_見返し中 >= 1
;	PRINTFORMW 이 의뢰의 보수는 이미 받았습니다。
	RETURN 0
ENDIF

PRINTL
SETCOLOR C_YELLOW
TRYCALLFORM CARDNAME_{カード_ID}, "名前"
PRINTFORMW 《%RESULTS%》를 손에 넣었습니다！
IF 획득枚数 >= 1
	ITEM:カード_ID += 획득枚数
ELSE
	ITEM:カード_ID += 1
ENDIF
RESETCOLOR

@ITEM_GET(아이템_ID,이벤트外)
#DIM DYNAMIC 아이템_ID
#DIM DYNAMIC 이벤트外
IF 依頼_見返し中 >= 1
;	PRINTFORMW この依頼の報酬は受け取り済です。
	RETURN 0
ENDIF

SIF ITEM:아이템_ID >= 1 && アップデート処理 == 1
	RETURN 0


PRINTL
SETCOLOR C_YELLOW
IF 아이템_ID == 501
	PRINTFORMW 『듀얼디스크：한정 천년 시리즈』를 손에 넣었습니다！
ELSE
	PRINTFORMW 『%ITEMNAME:아이템_ID%』를 손에 넣었습니다！
ENDIF
ITEM:아이템_ID += 1
RESETCOLOR

@TALENT_GET(素質_ID)
#DIM DYNAMIC 素質_ID

IF 依頼_見返し中 >= 1
;	PRINTFORMW この依頼の報酬は受け取り済です。
	RETURN 0
ENDIF

PRINTL
SETCOLOR C_YELLOW
PRINTFORMW %CALLNAME:MASTER%은/는 『%TALENTNAME:素質_ID%』을/를 획득했습니다!
TALENT:MASTER:素質_ID = 1
RESETCOLOR



@MONEY_GET(획득_金額,ウェイトレス)
#DIM DYNAMIC 획득_金額
#DIM DYNAMIC 표시金額
#DIM DYNAMIC ウェイトレス
IF 依頼_見返し中 >= 1 || 획득_金額 == 0
;	PRINTFORMW この依頼の報酬は受け取り済です。
	RETURN 0
ENDIF

PRINTL
IF 획득_金額 >= 1
	SETCOLOR C_YELLOW
	IF ウェイトレス == 1
		PRINTFORML %CALLNAME:MASTER%는 {획득_金額}엔을 받았습니다!
	ELSE
		PRINTFORMW %CALLNAME:MASTER%는 {획득_金額}엔을 받았습니다!
	ENDIF
ELSE
	SETCOLOR C_RED
	ABS 획득_金額
	IF ウェイトレス == 1
		PRINTFORML %CALLNAME:MASTER%는　{RESULT}엔을 지불했다.
	ELSE
		PRINTFORMW %CALLNAME:MASTER%는　{RESULT}엔을 지불했다.
	ENDIF
ENDIF
MONEY += 획득_金額
RESETCOLOR

PRINTFORM

@DP_GET(획득_金額)
#DIM DYNAMIC 획득_金額
#DIM DYNAMIC 표시金額
IF 依頼_見返し中 >= 1
;	PRINTFORMW この依頼の報酬は受け取り済です。
	RETURN 0
ENDIF

PRINTL
IF 획득_金額 >= 1
	SETCOLOR C_YELLOW
	PRINTFORMW {RESULT}DP를 획득했다。
ELSE
	ABS 획득_金額
	PRINTFORMW {RESULT}DP를 사용했다。
ENDIF

FLAG:듀얼포인트 += 획득_金額
RESETCOLOR

@EVENT_FAVUP(캐릭터_ID,상승値)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 상승値
IF 依頼_見返し中 >= 1
	RETURN 0
ENDIF

PRINTL
IF 상승値 >= 1
	SETCOLOR C_PINK
PRINTFORMW %CALLNAME:캐릭터_ID%의 호감도가 {상승値} 올랐습니다!
ELSE
	SETCOLOR C_RED
PRINTFORMW %CALLNAME:캐릭터_ID%의 호감도가 {상승値} 내렸습니다…
ENDIF

CFLAG:캐릭터_ID:호감도 += 상승値
CFLAG:캐릭터_ID:호감도 = LIMIT(CFLAG:캐릭터_ID:호감도, 0, 999999)

RESETCOLOR


@EVENT_MOVE(캐릭터_ID,目標위치,依頼場所플래그,エリア名지정)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 目標위치
#DIM DYNAMIC 依頼場所플래그
#DIMS DYNAMIC エリア名지정
IF 依頼_見返し中 >= 1
	RETURN 0
ENDIF
CFLAG:캐릭터_ID:현재위치 = 目標위치
SIF 依頼場所플래그 == 1
	依頼_場所 = 目標위치

IF エリア名지정 != ""
	SELECTCASE 目標위치
	CASE 場所_依頼用エリア０
		依頼用エリア０名称 = %エリア名지정%
	CASE 場所_依頼用エリア１
		依頼用エリア１名称 = %エリア名지정%
	CASE 場所_依頼用エリア２
		依頼用エリア２名称 = %エリア名지정%
	CASE 場所_依頼用エリア３
		依頼用エリア３名称 = %エリア名지정%
	CASE 場所_依頼用エリア４
		依頼用エリア４名称 = %エリア名지정%
	CASE 場所_依頼用エリア５
		依頼用エリア５名称 = %エリア名지정%
	CASE 場所_依頼用エリア６
		依頼用エリア６名称 = %エリア名지정%
	CASE 場所_依頼用エリア７
		依頼用エリア７名称 = %エリア名지정%
	CASE 場所_依頼用エリア８
		依頼用エリア８名称 = %エリア名지정%
	CASE 場所_依頼用エリア９
		依頼用エリア９名称 = %エリア名지정%

	CASE 場所_依頼用寝室エリア
		依頼用寝室エリア名称 = %エリア名지정%
	CASE 場所_依頼用浴室エリア
		依頼用寝室エリア名称 = %エリア名지정%
	CASE 場所_依頼用듀얼エリア
		依頼用듀얼エリア名称 = %エリア名지정%
	ENDSELECT
ENDIF

IF 캐릭터_ID == MASTER
	SETCOLOR C_GREEN
	PRINTL
PRINTFORMW >> %CALLNAME:MASTER%은/는 %GETPLACENAME(目標위치)%까지 이동했습니다.
	RESETCOLOR
ENDIF

@EVENT_DOUKOU(캐릭터_ID,동행時間)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 동행時間
#DIM DYNAMIC 上書き플래그

IF 依頼_見返し中 >= 1
	RETURN 0
ENDIF

SIF 동행時間 == 0
	동행時間 = 1440
CFLAG:캐릭터_ID:동행 = 동행時間
TARGET = 캐릭터_ID

RETURN 1

@EVENT_TIMER(経過時間,上書き플래그)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 経過時間
#DIM DYNAMIC 上書き플래그

IF 依頼_見返し中 >= 1
	RETURN 0
ENDIF

IF 上書き플래그 == 1
	TIME = 経過時間
ELSE
	TIME += 経過時間
ENDIF
RETURN 1



@IRAI_AREA_CHECK(目標位置)
#FUNCTION
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 目標位置
#DIM DYNAMIC 上書き플래그

IF 依頼_見返し中 >= 1
	RETURNF 1
ENDIF

SIF CFLAG:MASTER:현재위치 != 目標位置
	RETURNF 0
RETURNF 1