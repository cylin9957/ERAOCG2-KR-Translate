;-------------------------------------------------
;디저트카페로 초대한다
;-------------------------------------------------
@COM617 
#DIM DYNAMIC 회복率_체력
#DIM DYNAMIC 회복率_기력

LOCAL = RAND:100
LOCAL:1 = 90 + GET_SUCCESS_RATE() / 5
SIF LOCAL:1 > 99
	LOCAL:1 = 99
IF LOCAL <= 9
	;10％で大成功
	EXP:MASTER:대화경험 ++
	EXP:MASTER:데이트경험 ++
	EXP:TARGET:데이트경험 ++
	TFLAG:SELECTCOM분기 = 1
ELSEIF LOCAL >= LOCAL:1
	;10～1％で失敗
	TFLAG:SELECTCOM분기 = -1
ELSE
	;残りは成功
	TFLAG:SELECTCOM분기 = 0
ENDIF

;その場に居る데이트중TARGET全員で回す
LOCAL:1 = TARGET
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL <= 0
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):수면
		CONTINUE
	SIF CFLAG:(TARGET:LOCAL):데이트중 == 0
		CONTINUE
	TARGET = TARGET:LOCAL
	CALL COM617_1
NEXT
TARGET = LOCAL:1
회복率_체력 = MIN(BASE:MASTER:체력 + MAXBASE:MASTER:체력 / 10 , MAXBASE:MASTER:체력)
회복率_기력 = MIN(BASE:MASTER:기력 + MAXBASE:MASTER:기력 / 3 , MAXBASE:MASTER:기력)
CALL RECOVER_PERMIL(MASTER,회복率_체력,"체력",1)
CALL RECOVER_PERMIL(MASTER,회복率_기력,"기력",1)
EXP:MASTER:데이트경험 ++
TCVAR:MASTER:식사가능여부 = 1440 * DAY + TIME

TIME += 60
CALL MONEY_GET(-3000)

RETURN 1


@COM617_1
#DIM DYNAMIC 환락강도
#DIM DYNAMIC 정복강도
#DIM DYNAMIC 정애강도
#DIM DYNAMIC 수동강도
#DIM DYNAMIC 회복率_체력
#DIM DYNAMIC 회복率_기력

DOWNBASE:기력 += 10

;固定で획득するソース
환락강도 = 800

정애강도 = 300

;ABL:욕망をみる
IF ABL:욕망 <= 1
	환락강도 += (ABL:욕망 * 60)
ELSEIF ABL:욕망 <= 3
	환락강도 += 300 + (ABL:욕망 * 60)
ELSEIF ABL:욕망 <= 5
	환락강도 += 600 + (ABL:욕망 * 60)
ELSEIF ABL:욕망 <= 8
	환락강도 += 900 + (ABL:욕망 * 75)
ELSEIF ABL:욕망 <= 11
	환락강도 += 1500 + (ABL:욕망 * 75)
ELSE
	환락강도 += 2500 + (ABL:욕망 * 30)
ENDIF

;ABL:봉사정신をみる
IF ABL:봉사정신 <= 1
	정애강도 += (ABL:봉사정신 * 30)
ELSEIF ABL:봉사정신 <= 3
	정애강도 += 200 + (ABL:봉사정신 * 30)
ELSEIF ABL:봉사정신 <= 5
	정애강도 += 500 + (ABL:봉사정신 * 30)
ELSEIF ABL:봉사정신 <= 8
	정애강도 += 700 + (ABL:봉사정신 * 45)
ELSEIF ABL:봉사정신 <= 11
	정애강도 += 1200 + (ABL:봉사정신 * 45)
ELSE
	정애강도 += 1800 + (ABL:봉사정신 * 20)
ENDIF

;호감도をみる
IF CFLAG:호감도 <= 500
	환락강도 += CFLAG:호감도
ELSEIF CFLAG:호감도 <= 5000
	환락강도 += 750 + (CFLAG:호감도 - 500) / 3
ELSE
	환락강도 += 2000 + (CFLAG:호감도 - 5000) / 4
ENDIF
SIF 환락강도 < 0
	환락강도 = 0


수동강도 = 200 + 100 * ABL:순종
정복강도 = 200 + 100 * ABL:새드끼

IF TFLAG:SELECTCOM분기 == -1
	TIMES 환락강도 , 0.10
	TIMES 정애강도 , 0.10
	TIMES 정복강도 , 0.50
	TIMES 수동강도 , 0.50
ELSEIF TFLAG:SELECTCOM분기 == 0
	TIMES 환락강도 , 1.00
	TIMES 정애강도 , 1.00
	TIMES 정복강도 , 1.00
	TIMES 수동강도 , 1.00
ELSE
	TIMES 환락강도 , 2.00
	TIMES 정애강도 , 1.50
	TIMES 정복강도 , 2.00
	TIMES 수동강도 , 2.00
	;期限の良さ（ウチではまだ설정なし）
	SIF TALENT:15 < 1
		TALENT:15 = 1
ENDIF
CALL SOURCE_CALC_환락(TARGET, MASTER, 환락강도)
CALL SOURCE_CALC_정복(TARGET, MASTER, 정복강도)
CALL SOURCE_CALC_정애(TARGET, MASTER, 정애강도)
CALL SOURCE_CALC_수동(TARGET, MASTER, 수동강도)


회복率_체력 = MIN(BASE:TARGET:체력 + MAXBASE:TARGET:체력 / 10 , MAXBASE:TARGET:체력)
회복率_기력 = MIN(BASE:TARGET:기력 + MAXBASE:TARGET:기력 / 3 , MAXBASE:TARGET:기력)
CALL RECOVER_PERMIL(TARGET,회복率_체력,"체력",1)
CALL RECOVER_PERMIL(TARGET,회복率_기력,"기력",1)

EXP:TARGET:데이트경험 ++


;-------------------------------------------------
;実行판정
;-------------------------------------------------
;디저트카페로 초대한다
@COM_ABLE617
;毎日リセット甘味플래그
SIF MONEY < 3000
	RETURN 0
SIF TIME_PROGRESS(TCVAR:식사가능여부) < 240
	RETURN 0
;데이트중以外
SIF CFLAG:TARGET:데이트중 != 1
	RETURN 0
SIF CFLAG:MASTER:현재위치 != 50
	RETURN 0
SIF GLOBAL_COMABLE(617)
	RETURN RESULT
;상대가 居ない
SIF !TARGET
	RETURN 0
;수면中
SIF CFLAG:수면
	RETURN 0
SIF CFLAG:우후후 == 2
	RETURN 0
RETURN 1