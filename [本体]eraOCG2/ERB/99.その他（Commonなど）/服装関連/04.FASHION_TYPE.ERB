@FASHION_TYPE(캐릭터_ID)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 着せ替えセット
#DIM DYNAMIC 着用セット
#DIM DYNAMIC F_메이드
#DIM DYNAMIC F_바니
#DIM DYNAMIC F_외출服
#DIM DYNAMIC F_수영복
#DIM DYNAMIC F_牛着
#DIM DYNAMIC 외출OK服
#DIM DYNAMIC 패션在り
SELECTCASE EQUIP:캐릭터_ID:모자
CASE 6;ヘッドドレス
	F_메이드 += 2
CASE 14;うさ耳バンド
	F_바니 += 3
ENDSELECT

SELECTCASE EQUIP:캐릭터_ID:원피스
CASE 7;메이드服
	F_메이드 += 5
CASE 4 , 20, 60;ハイレグ/逆바니
	F_바니 += 2
CASE 30 TO 35
	F_수영복 += 5
ENDSELECT

SELECTCASE EQUIP:캐릭터_ID:양말
CASE 3;ストッキング
	F_메이드 += 2
ENDSELECT

VARSET RESULT
CALL FASHION_MODERN(캐릭터_ID)
IF RESULT == 1
	RESULTS = 현대복
	패션在り = 1
ENDIF

IF F_牛着 >= 5
	RESULTS = 소수영복
	패션在り = 1
ENDIF
IF F_바니 >= 5
	RESULTS = 바니
	패션在り = 1
ENDIF
IF F_수영복 >= 5
	RESULTS = 수영복
	패션在り = 1
ENDIF
IF F_메이드 >= 5
	RESULTS = 메이드
	패션在り = 1
ENDIF
IF 패션在り == 1	
	RETURN 1
ENDIF

RETURN 0


@FASHION_MODERN(캐릭터_ID)
#DIM DYNAMIC F_외출服
#DIM DYNAMIC 캐릭터_ID
;――외출て問題ない服装――――――――――――――――――――――――――――――――――――――――――――――
IF EQUIP:캐릭터_ID:신발 != 0
	F_외출服 += 1
ENDIF

SELECTCASE EQUIP:캐릭터_ID:이너
CASE 1 TO 7, 10,11,15 TO 19
	F_외출服 += 3
ENDSELECT

SELECTCASE EQUIP:캐릭터_ID:외투
CASE 1 TO 6, 8 TO 10, 29
	F_외출服 += 2
ENDSELECT

SELECTCASE EQUIP:캐릭터_ID:원피스
CASE 10 TO 12, 1,5,14, 16,21
	F_외출服 += 2
CASEELSE
	F_외출服 -= 2
ENDSELECT


SELECTCASE EQUIP:캐릭터_ID:치마
CASE 1 TO 3, 22
	F_외출服 += 2
ENDSELECT

SELECTCASE EQUIP:캐릭터_ID:바지
CASE 1 TO 10, 12, 13
	F_외출服 += 2
ENDSELECT

SELECTCASE EQUIP:캐릭터_ID:신발
CASE 1 TO 10
	F_외출服 += 1
ENDSELECT


SELECTCASE EQUIP:캐릭터_ID:양말
CASE 1 TO 10
	F_외출服 += 1
ENDSELECT


SIF EQUIP:캐릭터_ID:치마 == 0 && EQUIP:캐릭터_ID:원피스 == 0 && EQUIP:캐릭터_ID:바지 == 0
	F_외출服 -= 3

SELECTCASE EQUIP:캐릭터_ID:상반신속옷１
CASE 0
	IF EQUIP:캐릭터_ID:치마 == 0 && EQUIP:캐릭터_ID:원피스 == 0 && EQUIP:캐릭터_ID:바지 == 0
		F_외출服 -= 3
	ELSE
		F_외출服 -= 1
	ENDIF
ENDSELECT

SELECTCASE EQUIP:캐릭터_ID:하반신속옷２
CASE 0
	F_외출服 -= 1
ENDSELECT

SELECTCASE EQUIP:캐릭터_ID:16
CASE 17
	F_외출服 -= 2
ENDSELECT
SELECTCASE EQUIP:캐릭터_ID:17
CASE 17
	F_외출服 -= 2
ENDSELECT
SELECTCASE EQUIP:캐릭터_ID:18
CASE 17
	F_외출服 -= 2
ENDSELECT

IF F_외출服 >= 5
	RETURN 1
ENDIF
RETURN 0

;――――――――――――――――――――――――――――――――――――――――――――――――


@FASHION_SENSE(캐릭터_ID)
#DIM DYNAMIC 캐릭터_ID

SIF TALENT:캐릭터_ID:수치심 == -1
	RETURN 0
SIF TALENT:캐릭터_ID:무관심
	RETURN 0
SIF TALENT:캐릭터_ID:즉시타락
	RETURN 0

RETURN 1