;紅魔카지노作者（morph）による改造
;基本的には実処理は변경せずに記述法を変えただけ
;
;비고：셔플のタイミングについて
;BJで採用しているような方式にすればプレイヤーに勝たせることもできるんだけど
;・最高倍率がx300
;・後半になるほどプレイヤー有利になる（今までの捨て札から予想できる）
;・플레이어는 自由にBETを変化させられる
;この条件で셔플を毎回しない形式にするとプレイヤーが勝ちすぎる予感がする
;最初の5回は1BET、残りは最高BETで勝負とか猛威をふるうのでは？
;とはいえ現状のPOKERはBETによる거래もなく
;ゲーム性（プレイヤーからの介入要素）に乏しいような気もするなー
@CASINO_POKER
#DIM PL = 0   ;プレイヤーに割り振る番号、とりあえず1人プレイなので0でよし
#DIM P_BET    ;BET金額
#DIM ORI_BET  ;特に使ってないっぽい
#DIM HOLD     ;BIT利用、손패0～4のHOLD상황
#DIM PK_RATE  ;役による倍率
#DIM TOTAL, 3 ;結果、TOTAL:1＝損失、TOTAL:2＝利益
VARSET TOTAL
;参加플레이어는 ひとり
NUM_CARD_PL = 1

IF !FLAG:듀얼포인트
	PRINTFORML 딜러
	PRINTFORML 　손님, 본 카지노에서는 전용 화폐 'DP'만으로 게임을 즐기실 수 있습니다
	PRINTFORMW 　번거로우시겠지만 DP를 구입해주시기 바랍니다
	RETURN
ENDIF
CALL CASINO_INPUTLOOP("カード賭け金")
IF !RESULT
	P_BET = 0
ELSE
	P_BET = RESULT
	ORI_BET = RESULT
	TOTAL:1 += P_BET
	CALL CASINO_MONEY("ＤＰ", -P_BET)
ENDIF

WHILE P_BET
	VARSET HOLD
	CALL PLAY_CARDS("SHUFFLE")
	PRINTFORML 
	PRINTFORMW 딜러는 덱을 셔플했다
	CUSTOMDRAWLINE ―

	;패를 リセットして5枚引く
	CALL PLAY_CARDS("RESET_HAND")
	CALL PLAY_CARDS("POKER_FIRST_HAND", PL)
	
	
	DO
		CALL CARD_DISP("HAND_BUTTON", PL)
		PRINTFORML 
		FOR LOCAL, 0, 5
			IF GETBIT(HOLD, LOCAL)
				PRINTBUTTON "[　ＨＯＬＤ　]", LOCAL
			ELSE
				PRINTBUTTON "[ＣＨＡＮＧＥ]", LOCAL
			ENDIF
		NEXT
		PRINTFORML 
		DRAWLINE
		PRINTFORML [9] - 　ＤＲＡＷ　
		BINPUT
		SELECTCASE RESULT
		CASE 9
			BREAK
		CASE 0 TO 4
			INVERTBIT HOLD, RESULT
		ENDSELECT
	LOOP 1
	IF HOLD == 0b11111
	ELSE
		PRINTFORMW 카드를 교환합니다
		FOR LOCAL, 0, 5
			SIF GETBIT(HOLD, LOCAL)
				CONTINUE
			CALL PLAY_CARDS("REHIT", PL, LOCAL)
		NEXT
	ENDIF
	PRINTFORML 
	CALL CARD_DISP("HAND", PL)
	WAIT
	
	;結果の판정
	PK_RATE = CARD_PLAYER("GET_POKER_RATE", PL)
	P_BET = P_BET * PK_RATE / 100
	FONTSTYLE 3
	SETCOLOR C_YELLOW
	SELECTCASE PK_RATE
	;RATEは％値
	CASE 30000
		LOCALS = ＲＯＹＡＬ ＦＬＵＳＨ！
	CASE 5000
		LOCALS = ＳＴＲＡＩＧＨＴ ＦＬＵＳＨ！
	CASE 3000
	;これだけスカーレットカラーにしてみる
		SETCOLOR C_RED
		LOCALS = ４ ＯＦ Ａ ＫＩＮＤ！
	CASE 2000
		LOCALS = ＦＵＬＬ ＨＯＵＳＥ！
	CASE 1000
		LOCALS = ＦＬＵＳＨ！
	CASE 500
		LOCALS = ＳＴＲＡＩＧＨＴ！
	CASE 300
		LOCALS = ３ ＯＦ Ａ ＫＩＮＤ！
	CASE 100
		LOCALS = ＴＷＯ　ＰＡＩＲ
	CASE 50
		LOCALS = ＯＮＥ　ＰＡＩＲ
	CASE 0
		LOCALS = ＮＯ　ＰＡＩＲ
	ENDSELECT
	PRINTFORML 
	PRINTFORML %LOCALS%
	PRINTFORML 
	RESETCOLOR
	FONTREGULAR
	PRINTFORMW ＲＥＷＡＲＤ：{P_BET}
	CALL CASINO_MONEY("ＤＰ", P_BET)
	TOTAL:2 += P_BET
	TIME += 5
	
	;ゲームの続行판정
	SIF NEMUKE() >= 700
		BREAK
	SIF FLAG:듀얼포인트 <= 0
		BREAK
	
	PRINTFORML 　자、승부를 계속할까요？
	PRINTFORML 
	CALL CASINO_INPUTLOOP("カード続行")
	SELECTCASE RESULT
	CASE 0
		P_BET = 0
	CASE 1
		IF FLAG:듀얼포인트 < P_BET
			PRINTFORML 딜러
			PRINTFORML 　소지한 ＤＰ가 부족한것같네요
			PRINTFORML 　내기금을 재설정해주세요
			PRINTFORML 
			CALL CASINO_INPUTLOOP("カード賭け金")
			P_BET = RESULT
			ORI_BET = RESULT
			SIF RESULT
				PRINTFORML 
		ELSE
			P_BET = ORI_BET
		ENDIF
	CASE 2
		PRINTFORML 딜러
		PRINTFORML 　그럼 내기금을 입력해주세요
		PRINTFORML 
		CALL CASINO_INPUTLOOP("カード賭け金")
		P_BET = RESULT
		ORI_BET = RESULT
		SIF RESULT
			PRINTFORML 
	ENDSELECT
	
	IF P_BET
		TOTAL:1 += P_BET
		CALL CASINO_MONEY("ＤＰ", -P_BET)
		PRINTFORML 딜러
		PRINTFORML 　네、내기금{P_BET}ＤＰ 확실하게 받았습니다
		PRINTFORMW 　그럼 계속해서 잘부탁드립니다
	ENDIF
WEND

IF NEMUKE() >= 700
	PRINTFORML 
	PRINTFORML 딜러
	PRINTFORML 　저기, 손님?
	PRINTFORML 　상당히 피곤해 보이시네요
	PRINTFORML 　오늘은 이쯤에서 게임을 마치시는 게 어떨까요
	PRINTFORMW 　다음에 또 방문해주시길 기다리고 있겠습니다
ELSEIF FLAG:듀얼포인트 <= 0
	PRINTFORML 
	PRINTFORML 딜러
	PRINTFORML 　가지고 계신 DP가 모두 떨어진 것 같네요
	PRINTFORMW 　다음에 또 방문해주시길 기다리고 있겠습니다
ELSE
	PRINTFORML 딜러
	PRINTFORML 　알겠습니다
	PRINTFORMW 　다음에 또 방문해주시길 기다리고 있겠습니다
ENDIF

;カモはまとめて精算, カモシステムをよく理解していないのでまずかったらすみません……
;賭け金を使っていたらリザルト표시
IF TOTAL:1
	CALL CASINO_MONEY("カモ", TOTAL:1)
	CALL CASINO_DISP("RESULT", TOTAL:1, TOTAL:2)
ENDIF
VARSET TOTAL
