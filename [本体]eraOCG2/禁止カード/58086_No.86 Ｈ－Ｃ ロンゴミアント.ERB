@CARDNAME_58086,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = No.86 Ｈ－Ｃ ロンゴミアント
	CASE "略称"
		RESULTS = #86 ロンゴミ
	CASE "カテゴリ"
		RESULTS:0 = ナンバーズ
		RESULTS:1 = ヒロイック
ENDSELECT


@CARD_58086(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN エクシーズ
	CASE "属性"
		RETURN 光属性
	CASE "種族"
		RETURN 戦士族
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 1500
	CASE "性別"
		RETURN 雄性
	CASE "制限"
		RETURN 制限
ENDSELECT

RETURN 0

@CARDSUMMON_58086(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 5
素材条件レベル = 4
素材条件数自由 = 1
;追加効果用

IF EX参照 == 1
	CALL XYZCAN(決闘者,召喚カード,58086) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_58086(種類)
#DIM DYNAMIC 種類

PRINTL レベル４モンスター×２体以上
CALLFORM CARD_ABLE(0,ターン終了時強制)
PRINTL ①：相手エンドフェイズに発動する。
PRINTL このカードのＸ素材を１つ取り除く。
CALLFORM CARD_ABLE(0,永続効果)
PRINTL ②：このカードのＸ素材の数によって、このカードは以下の効果を得る。
PRINTL ●１つ以上：このカードは戦闘では破壊されない。
PRINTL ●２つ以上：このカードの攻撃力・守備力は１５００アップする。
PRINTL ●３つ以上：このカードは相手のカードの効果を受けない。
PRINTL ●４つ以上：相手はモンスターを特殊召喚できない。
CALLFORM CARD_ABLE(10,戦場起動)
PRINTL ●５つ以上：１ターンに１度、発動できる。
PRINTL 　　　　　　相手フィールドのカードを全て破壊する。

@CARDSUMMON_AA_58086

SETCOLOR C_L_RED
PRINT 　　　　 ／￣￣￣￣￣￣￣￣＼　 　
PRINTL 　　　　　　　　　　　　　　　　　　　
PRINT 　　　 ／　　　　　　　　　　＼　 
PRINTL 　　　　　　　　　　　　 ／￣＼　　　 
PRINT 　　　│　　 ／￣￣￣￣＼　　 │  
PRINTL 　　　　　　　　　　　 ／　　 │　　  
PRINT 　　　│　　│　／￣＼　│　　│  
PRINTL 　　　　　　　　　　 ／　　　／　　　 
PRINT 　　　│　　│（　　　）│　　│  
PRINTL 　　　　　　　　　 ／　　　／　　　　 
PRINT 　　　 ＼__／　 ＼＿／　 ＼__／　 
PRINTL 　　　　　　　　　│　　 ／　　　　　 
PRINT 　　　　　　　　　　　　　　　　　
PRINTL 　　　　　　　　　 ＼＿／　　　　　　 
PRINT 　　　　　　／￣￣￣￣￣＼　　　　
PRINTL 　　　　　　　　　 　　　　　　　　　 
PRINT 　　　　　／　　　　　　　＼　　　
PRINTL 　　　 ／￣￣＼　　／￣＼　　／￣￣＼ 
PRINT 　　　　／　　　／￣＼　　　＼　　
PRINTL 　　　│　　　│　(　　　)　│　　　│
PRINT 　　　／　　　／　　　＼　　　＼　
PRINTL 　　　│　　　│　 ＼＿／　 │　　　│
PRINT 　　（　　　（　　　　　）　　　）
PRINTL 　　　│　　　 ＼＿＿＿＿＿／　　　 │
PRINT 　　　＼　　　＼　　　／　　　／　
PRINTL 　　　 ＼　　　　　　　　　　　　　／ 
PRINT 　　　　＼　　　＼＿／　　　／　　
PRINTL 　　　　 ＼＿＿＿＿＿＿＿＿＿＿＿／　 
PRINT 　　　　　＼　　　　　　　／　　　
PRINTL 　　　　　　　　　　　　　　　　　　　
PRINT 　　　　　　＼＿＿＿＿＿／　　　　
PRINTL 　　　　　　　　　　　　　　　　　　　
RESETCOLOR
WAIT


@CARDCAN_58086(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,58086)
SIF RESULT == 1
	RETURN 0

IF 種類 == 戦場起動
	SIF 効果発動済(決闘者,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_Ｘ:場所") <= 3
		RETURN 0
	CALL FULLRESET
	効果範囲相手戦場 = 1
	効果範囲相手魔罠 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == 永続効果 || 種類 == 攻守変動
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_58086(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 戦場起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,58086,10,X消費1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(決闘者,"全場全体",場所,"墓地")
	ENDIF

ELSEIF 種類 == 永続効果
	SIF GETVAR(@"%決闘者%_MZ_Ｘ:場所") >= 0
		CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"戦耐")
	IF GETVAR(@"%決闘者%_MZ_Ｘ:場所") >= 2
		CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"モ耐")
		CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"魔耐")
		CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"罠耐")
	ENDIF
	IF GETVAR(@"%決闘者%_MZ_Ｘ:場所") >= 3
		CALL SET_PLAYER_INFO(決闘者,"SS阻害状態",3,1)
	ENDIF
	
ELSEIF 種類 == 攻守変動
	SIF GETVAR(@"%決闘者%_MZ_Ｘ:場所") >= 1
		CALL STATCHANGE_BASIC(決闘者,"自身",場所,1500,1500)
ENDIF